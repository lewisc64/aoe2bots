#do once
  set up basics
  
  assign 4 builders to barracks
  
  sn-maximum-attack-group-size = 1
  sn-minimum-attack-group-size = 1
  sn-number-attack-groups = 500
  
  sn-special-attack-type2 = villager
#end do

lure boars

manage scouting
do basic diplomacy
do basic targeting

#if current-age >= feudal-age 
  escrow 10 food
  escrow 10 gold
#end if

research feudal-age with food escrow
research castle-age with food and gold escrow

research mill upgrades with food escrow
research lumber camp upgrades with food escrow
research ri-wheel-barrow with wood and food escrow
research ri-hand-cart with wood and food escrow

research ri-man-at-arms with food and gold escrow

research blacksmith infantry upgrades with food and gold escrow

#when

  research ri-arson with food and gold escrow
  
#then always

  research ri-pikeman with food and gold escrow
  research ri-halberdier with food and gold escrow
  
  research ri-long-swordsman with food and gold escrow
  research ri-two-handed-swordsman with food and gold escrow
  research ri-champion with food and gold escrow
  
  research ri-tracking with food and gold escrow
  
  research imperial-age with food and gold escrow
  
#end when

research ri-masonry
research ri-architecture

build lumber camps
#if building-type-count lumber-camp >= 1
  build mills
#end if
build 1 blacksmith
build 1 market
build 1 university
build 1 monastery

#if current-age >= imperial-age
  train 120 villager with 30 trade using escrow for caravan
#else
  train 120 villager
#end if
research ri-loom with gold escrow

train militiaman-line => train spearman-line
train 1 monk

#if up-research-status c: feudal-age < research-pending

  #do once
    distribute villagers 20 80 0 0
  #end do
  
  build houses
  build 8 farm
  
#else

  #do once
    distribute villagers 30 60 10 0
  #end do
  
  #when
    build gold mining camps
  #then
    sn-camp-max-distance += 8
  #end when
  build houses with 15 headroom
  
  #if wood-amount > 100
    build farms
  #end if
  
  build forward 4 barracks
  
  auto balance wood and food and gold
  
#end if

#if current-age == castle-age
  build forward 5 barracks
#else if current-age == imperial-age
  build forward 6 barracks
#end if
