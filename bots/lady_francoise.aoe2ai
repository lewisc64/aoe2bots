// play as franks

const minimum-army = 12

#do once
  set name to "Lady Francoise"
#end do

load "charlotte/stats.aoe2ai"
load "templates/fast_castle.aoe2ai"

#template diplo-chat-alliance-offer
  @up-get-player-color {player} -1
  #select random
    chat const to all "An alliance, %s?" 7031232
  #randor
    chat const to all "We should ally, %s. For the sake of both of us." 7031232
  #randor
    chat const to all "I suggest we form an alliance, %s." 7031232
  #end select
#end template

#template diplo-chat-our-betrayal
  @up-get-player-color {player} -1
  #select random
    chat const to all "Our story together is drawing to a close, %s." 7031232
  #randor
    chat const to all "%s. Ready your blade." 7031232
  #randor
    chat const to all "%s. It is time." 7031232
  #end select
#end template

#template diplo-chat-their-betrayal
  #select random
    chat to all "It was inevitable it seems."
  #randor
    chat to all "I have become too trusting."
  #randor
    chat to all "The hands of trechery reach far and deep."
  #end select
#end template

#template diplo-chat-accepted-our-alliance
  chat to all "Very good."
#end template

#template diplo-chat-accepted-their-alliance
  @up-get-player-color {player} -1
  chat const to all "We have an agreement, %s." 7031232
#end template

#template diplo-chat-refused-our-alliance
  @up-get-player-color {player} -1
  #select random
    chat const to all "You will live to regret that decision, %s." 7031232
  #randor
    chat const to all "A curse upon your name, %s." 7031232
  #end select
#end template

#template diplo-chat-refused-their-alliance
  #select random
    chat to all "Be silent."
  #randor
    chat to all "Again, no."
  #end select
#end template

#template diplo-chat-friendly-fire-warning
  @up-get-player-color {player} -1
  #select random
    chat const to all "A mistake %s, of course. Don't do it again." 7031232
  #randor
    chat const to all "Cease your childish games, %s." 7031232
  #end select
#end template

#template diplo-chat-friendly-fire-betrayal
  chat to all "I have had enough of you."
#end template

#template diplo-chat-resign
#end template

#template diplo-should-player-be-marked
  #if military-population >= 10
    goal temp = 0
    @up-get-player-fact {player} military-population 0 temp
    #if up-compare-goal temp c:>= 5
      goal {marked_goal} = 1
    #end if
  #end if
#end template

load "templates/diplomacy_engine.aoe2ai"

#template escrow-resources
  escrow {percentage} food with maximum 1300
  escrow {percentage} gold with maximum 800
#end template

#if up-research-status c: castle-age >= research-pending

  #if unit-type-count-total knight-line < 10 or up-research-status c: ri-paladin >= research-pending
    insert escrow-resources(percentage=0)
    #if up-research-status c: ri-paladin >= research-pending
      @up-release-escrow
    #end if
  #else if unit-type-count-total knight-line < 20
    insert escrow-resources(percentage=10)
  #else if unit-type-count-total knight-line < 30
    insert escrow-resources(percentage=20)
  #else
    insert escrow-resources(percentage=40)
  #end if

  #do once
    set up basics
    assign 4 builders to town-center-foundation
    
    sn-maximum-attack-group-size = 1
    sn-minimum-attack-group-size = 1
    sn-special-attack-type2 = villager
    
    goal town-size-attack = 0
    
    #if current-age == imperial-age and false // disabled
      goal town-size-attack = 1
      @up-reset-unit c: -1
    #end if
    
    #if research-completed ri-paladin and research-completed ri-capped-ram
      sn-enable-training-queue = 1
    #end if
  #end do
  
  manage scouting

  #if building-type-count-total stable >= 2
    #do once
      distribute villagers 35 55 10 0
    #end do
    auto balance wood and food and gold
    #if strategic-number sn-gold-gatherer-percentage < 15
      distribute 2 villagers from wood and food to gold
    #end if
    #if strategic-number sn-food-gatherer-percentage < 40
      distribute 2 villagers from wood and gold to food
    #end if
  #else
    #do once
      distribute villagers 40 50 10 0
    #end do
  #end if
  
  #if building-type-count-total town-center == 0 and stone-amount < 100
    buy stone
  #end if
  build 1 town-center
  
  build 1 barracks
  
  #if current-age == imperial-age
    build 5 stable
    #if up-compare-goal non-escrowed-wood c:>= 2000
      build 16 stable
    #end if
  #else
    build 2 stable
  #end if
  
  #if building-type-count-total stable >= 2
    #when
      build 1 university
    #then always
      #when
        build 1 monastery
      #then always
        build 3 town-center
      #end when
    #end when
  #end if
  
  build houses with 15 headroom
  build lumber camps
  #when
    build gold mining camps
  #then
    sn-camp-max-distance += 8
  #end when
  
  build 1 market
  build 1 blacksmith
  
  #if military-population >= 10 and building-type-count-total town-center >= 3
    build 1 siege-workshop
  #end if
  
  #if wood-amount >= 200 or building-type-count-total stable >= 2
    build farms
  #end if
  
  research ri-wheel-barrow with escrow
  #if current-age >= imperial-age
    research ri-hand-cart with escrow
  #end if
  research mill upgrades with escrow
  research gold mining camp upgrades with escrow
  research lumber camp upgrades with escrow
  research imperial-age with escrow
  
  research ri-bloodlines with escrow
  research ri-husbandry with escrow
  research cavalry blacksmith upgrades with escrow
  #when
    research ri-cavalier with escrow
  #then always
    research ri-capped-ram with escrow
    research ri-siege-ram with escrow
  #end when
  research ri-paladin with escrow
  
  #respond to 10 monk-set
    research ri-heresy with escrow
    research ri-devotion with escrow
    research ri-faith with escrow
  #end respond
  
  #respond to 10 missionary
    research ri-heresy with escrow
    research ri-devotion with escrow
    research ri-faith with escrow
  #end respond
  
  #if current-age >= imperial-age and up-compare-goal non-escrowed-gold c:> 150
    train 120 villagers with 30 trade using escrow for caravan
  #else
    train 120 villager
  #end if
  
  train 1 monk with escrow
  
  #if food-amount >= 150 or goal under-attack 1
    #if goal under-attack 1 and not goal threat-source castle
      train knight-line
    #else
      train 5 battering-ram-line
      train knight-line
    #end if
  #end if
  
  do basic targeting
  
  #if (up-compare-goal target-military-percentage c:>= 120 or up-compare-goal population-space-remaining c:<= 5) and unit-type-count knight-line >= minimum-army
    goal attacking = 1
  #else if up-compare-goal target-military-percentage c:<= 70 and goal attacking 1
    retreat
    goal attacking = 0
  #end if
  
  #if goal town-size-attack 0
    #if goal attacking 1
      attack with duc
    #else
      defend with duc
    #end if
    auto expand town size
  #else
    sn-number-attack-groups = 0
    #if goal attacking 1
      town size attack
    #else
      auto expand town size
    #end if
  #end if
  
  trade excess wood and food and gold at 2000
  
  #if goal under-attack 1
    trade excess wood and food and gold at 300
  #end if
  
  #if goal attacking 1
    trade excess wood and food and gold at 500
  #end if

#end if

#do once

  #delay by 4 real seconds
    chat to all "It is fate that we have met here, of all places."
    #delay by 4 real seconds
      chat to all "Let us weave tales of grandeur."
    #end delay
  #end delay

  #if research-completed ri-husbandry
    #select random persistant
      chat to all "A swift steed my father promised me. A thousand times I begged."
      #delay by 8 real seconds
        chat to all "He passed long ago. But while rotting in his grave, he kept his word."
        #delay by 8 real seconds
          chat to all "Now I understand."
        #end delay
      #end delay
    #randor
      chat to all "Only once have I encountered true tricksters"
      #delay by 5 real seconds
        chat to all "They smiled. They laughed. They pretended to care."
        #delay by 5 real seconds
          chat to all "Now their souls adorn the palace of my mind. All tortured."
        #end delay
      #end delay
    #randor
      chat to all "I took my vow at a young age."
      #delay by 3 real seconds
        chat to all "As I drank from the chalice, I told lies."
        #delay by 4 real seconds
          chat to all "They gorged on my sweetened words, and made me a lady."
          #delay by 5 real seconds
            chat to all "Drunk on their own reflections."
          #end delay
        #end delay
      #end delay
    #randor
      chat to all "A farmer once came to my court."
      #delay by 4 real seconds
        chat to all "Sickly and weak, he begged me for boon."
        #delay by 8 real seconds
          chat to all "Naturally I refused."
          #delay by 2 real seconds
            chat to all "Compassion only goes so far. Don't you think?"
          #end delay
        #end delay
      #end delay
    #randor
      chat to all "The sun glinted off my lance as I levelled it at my opponent."
      #delay by 3 real seconds
        chat to all "They held theirs, shaking with inexperience."
        #delay by 3 real seconds
          chat to all "We clashed and were both thrown from our mounts."
          #delay by 3 real seconds
            chat to all "I recovered quickly. They died the next day."
          #end delay
        #end delay
      #end delay
    #randor
      chat to all "Singing my praises, we made a toast."
      #delay by 3 real seconds
        chat to all "So cheerful. So fun."
        #delay by 2 real seconds
          chat to all "Not worth it."
        #end delay
      #end delay
    #randor
      chat to all "A slug of metal crunched against my ribcage, blood filling the cavity."
      #delay by 3 real seconds
        chat to all "I fell and hit the mud."
        #delay by 2 real seconds
          chat to all "He watched me from afar, disappointment written across his face."
          #delay by 3 real seconds
            chat to all "My one true love."
          #end delay
        #end delay
      #end delay
    #randor
      chat to all "He shot me a grin, and layed down his cards."
      #delay by 3 real seconds
        chat to all "Perhaps I reacted too passionately."
        #delay by 3 real seconds
          chat to all "The man screamed so much, I thought it almost impressive."
          #delay by 5 real seconds
            chat to all "Filthy cheat."
          #end delay
        #end delay
      #end delay
    #end select
  #end if
  
  #if research-completed ri-cavalier
    #select random persistant
      chat to all "As tears pooled up in his eyes, I grit my teeth."
      #delay by 4 real seconds
        chat to all "He asked what it was all for. What it all meant."
        #delay by 3 real seconds
          chat to all "I looked him in the eye, and severed him in twain."
        #end delay
      #end delay
    #randor
      chat to all "My head under a boot, I hear a scratchy voice worming into my thoughts:"
      #delay by 5 real seconds
        chat to all "'Hurts, doesn't it?'"
        #delay by 3 real seconds
          chat to all "'Feeling your skin bubble and your blood jet through your pores...'"
          #delay by 5 real seconds
            chat to all "It did not hurt."
          #end delay
        #end delay
      #end delay
    #randor
      chat to all "I never believed in goblins."
      #delay by 2 real seconds
        chat to all "Creatures of jealousy, I'm told. Born from your thoughts."
        #delay by 6 real seconds
          chat to all "They claimed to see them all around me."
          #delay by 2 real seconds
            chat to all "Perhaps their noses obscured their vision."
          #end delay
        #end delay
      #end delay
    #randor
      chat to all "Why do they read such fairytales?"
      #delay by 3 real seconds
        chat to all "The whims of some doddery fool?"
        #delay by 3 real seconds
          chat to all "Clinging to phantasms, casting aside the material."
          #delay by 4 real seconds
            chat to all "Time will forget them."
          #end delay
        #end delay
      #end delay
    #randor
      chat to all "Under dancing candlelight, my mother held me in her arms."
      #delay by 4 real seconds
        chat to all "Her eyes were cold. Expressionless."
        #delay by 2 real seconds
          chat to all "All that anger and pain, washed away by time."
          #delay by 4 real seconds
            chat to all "Only her shadow stayed with me."
            #delay by 2 real seconds
              chat to all "An unjudging echo of safety. It was perfect."
            #end delay
          #end delay
        #end delay
      #end delay
    #end select
  #end if

  #if research-completed ri-paladin
    #select random persistant
      chat to all "I had nightmares as a child."
      #delay by 3 real seconds
        chat to all "Demons crawling on my back, peeling off rolls of skin with their claws."
        #delay by 8 real seconds
          chat to all "The demons were saints, and I their lamb."
        #end delay
      #end delay
    #randor
      chat to all "People were born cruel, and people were born foolish. As was I."
      #delay by 7 real seconds
        chat to all "But I have changed."
        #delay by 3 real seconds
          chat to all "Go ahead. Lift up my veil. Watch my eyes sparkle."
        #end delay
      #end delay
    #randor
      chat to all "A wide-eyed girl looked up at the stars."
      #delay by 5 real seconds
        chat to all "Were they pretty?"
        #delay by 2 real seconds
          chat to all "Were they beautiful?"
          #delay by 3 real seconds
            chat to all "All she saw was fire."
          #end delay
        #end delay
      #end delay
    #randor
      chat to all "And for what? Altruistic virtues?"
      #delay by 4 real seconds
        chat to all "A thin guise."
        #delay by 3 real seconds
          chat to all "Look around you. Don't you see?"
          #delay by 4 real seconds
            chat to all "You are a foul monster."
          #end delay
        #end delay
      #end delay
    #end select
  #end if

#end do