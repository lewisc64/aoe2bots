// play as franks

const minimum-army = 12

load "charlotte/stats.aoe2ai"
load "templates/fast_castle.aoe2ai"

#select random persistant
  do basic diplomacy
#randor
  do basic diplomacy without backstabbing
#end select

#template escrow-resources
  #if escrow-amount food < 1300
    escrow {percentage} food
  #else
    escrow 0 food
  #end if
  #if escrow-amount gold < 800
    escrow {percentage} gold
  #else
    escrow 0 gold
  #end if
#end template

#if up-research-status c: castle-age >= research-pending

  #if unit-type-count-total knight < 10 or up-research-status c: ri-paladin >= research-pending
    insert escrow-resources(percentage=0)
  #else if unit-type-count-total knight < 20
    insert escrow-resources(percentage=10)
  #else if unit-type-count-total knight < 30
    insert escrow-resources(percentage=20)
  #else
    insert escrow-resources(percentage=40)
  #end if

  #do once
    assign 4 builders to town-center-foundation
    sn-maximum-gold-drop-distance = 8
    sn-maximum-stone-drop-distance = 8
    sn-maximum-hunt-drop-distance = 48
    @up-modify-sn sn-maximum-food-drop-distance s:= sn-maximum-town-size
    
    sn-maximum-attack-group-size = 1
    sn-minimum-attack-group-size = 1
    sn-special-attack-type2 = villager
    
    goal town-size-attack = 0
    
    #if current-age == imperial-age
      sn-maximum-town-size += 7
      sn-minimum-town-size += 7
      sn-safe-town-size += 7
      goal town-size-attack = 1
      @up-reset-unit c: -1
    #end if
  #end do

  #if building-type-count-total stable >= 2
    #do once
      distribute villagers 35 55 10 0
    #end do
    auto balance wood and food and gold
    #if strategic-number sn-gold-gatherer-percentage < 15
      distribute 2 villagers from wood and food to gold
    #end if
    #if strategic-number sn-food-gatherer-percentage < 40
      distribute 2 villagers from food and gold to food
    #end if
  #else
    #do once
      distribute villagers 40 50 10 0
    #end do
  #end if
  
  
  build 1 barracks
  
  #if current-age == imperial-age
    build 5 stable
  #else
    build 2 stable
  #end if
  
  #if building-type-count-total stable >= 2
    #when
      build 1 university
    #then always
      #when
        build 1 monastery
      #then always
        build 3 town-center
      #end when
    #end when
  #end if
  
  build houses with 15 headroom
  build lumber camps
  #when
    build gold mining camps
  #then
    sn-camp-max-distance += 8
  #end when
  
  build 1 market
  build 1 blacksmith
  
  #if military-population >= 10 and building-type-count-total town-center >= 3
    build 1 siege-workshop
  #end if
  
  #if wood-amount >= 200 or building-type-count-total stable >= 2
    build farms
  #end if
  
  research ri-wheel-barrow with wood and food escrow
  #if current-age >= imperial-age
    research ri-hand-cart with wood and food escrow
  #end if
  research mill upgrades with wood and food escrow
  research gold mining camp upgrades with wood and food escrow
  research lumber camp upgrades with wood and food escrow
  research imperial-age with food and gold escrow
  
  research ri-bloodlines with food and gold escrow
  research ri-husbandry with food escrow
  research cavalry blacksmith upgrades with food and gold escrow
  #when
    research ri-cavalier with food and gold escrow
  #then always
    research ri-capped-ram with wood and food escrow
    research ri-siege-ram with wood and food escrow
  #end when
  #when
    research ri-paladin with food and gold escrow
  #then always
    research ri-chemistry with food and gold escrow
  #end when
  
  #respond to 10 monk-set
    research ri-heresy with gold escrow
  #end respond
  
  #if current-age >= imperial-age and up-compare-goal non-escrowed-gold c:> 150
    train 120 villagers with 30 trade using escrow for caravan
  #else
    train 120 villager
  #end if
  
  train 1 monk with gold escrow
  
  #if food-amount >= 150 or town-under-attack
    #if town-under-attack
      train knight-line
    #else
      train 5 battering-ram-line
      train knight-line
    #end if
  #end if
  
  trade excess wood and food and gold at 2000
  
  #if town-under-attack
    trade excess wood and food and gold at 300
  #end if
  
  #do once
    target closest enemy
    scout enemy
  #end do
  #if not player-in-game target-player
    target closest enemy
    scout enemy
  #end if
  
  #if (up-compare-goal target-military-percentage c:>= 120 or up-compare-goal population-space-remaining c:<= 5) and unit-type-count knight-line >= minimum-army
    goal attacking = 1
  #else if up-compare-goal target-military-percentage c:<= 70 and goal attacking 1
    retreat
    goal attacking = 0
  #end if
  
  #if goal town-size-attack 0
    #if goal attacking 1
      sn-number-attack-groups = 200
    #else
      sn-number-attack-groups = 0
    #end if
  #else
    #if goal attacking 1
      sn-maximum-town-size = 255
    #else
      @up-modify-sn sn-maximum-town-size s:= sn-minimum-town-size
    #end if
  #end if

#end if

#do once

  chat to all "It is fate that we have met here, of all places."
  #delay by 15 seconds
    chat to all "Let us weave tales of grandeur."
  #end delay

  #if research-completed ri-husbandry
    #select random persistant
      chat to all "A swift steed my father promised me. A thousand times I begged."
      #delay by 16 seconds
        chat to all "He passed long ago. But while rotting in his grave, he kept his word."
        #delay by 16 seconds
          chat to all "Now I understand."
        #end delay
      #end delay
    #randor
      chat to all "Only once have I encountered true tricksters"
      #delay by 10 seconds
        chat to all "They smiled. They laughed. They pretended to care."
        #delay by 11 seconds
          chat to all "Now their souls adorn the palace of my mind. All tortured."
        #end delay
      #end delay
    #randor
      chat to all "I took my vow at a young age."
      #delay by 6 seconds
        chat to all "As I drank from the chalice, I told lies."
        #delay by 8 seconds
          chat to all "They gorged on my sweetened words, and made me a lady."
          #delay by 10 seconds
            chat to all "Drunk on their own reflections."
          #end delay
        #end delay
      #end delay
    #randor
      chat to all "A farmer once came to my court."
      #delay by 8 seconds
        chat to all "Sickly and weak, he begged me for boon."
        #delay by 16 seconds
          chat to all "Naturally I refused."
          #delay by 5 seconds
            chat to all "Compassion only goes so far. Don't you think?"
          #end delay
        #end delay
      #end delay
    #randor
      chat to all "The sun glinted off my lance as I levelled it at my opponent."
      #delay by 6 seconds
        chat to all "They held theirs, shaking with inexperience."
        #delay by 6 seconds
          chat to all "We clashed and were both thrown from our mounts."
          #delay by 7 seconds
            chat to all "I recovered quickly. They died the next day."
          #end delay
        #end delay
      #end delay
    #end select
  #end if
  
  #if research-completed ri-cavalier
    #select random persistant
      chat to all "As tears pooled up in his eyes, I grit my teeth."
      #delay by 14 seconds
        chat to all "He asked what it was all for. What it all meant."
        #delay by 10 seconds
          chat to all "I looked him in the eye, and severed him in twain."
        #end delay
      #end delay
    #randor
      chat to all "My head under a boot, I hear a scratchy voice worming into my thoughts:"
      #delay by 16 seconds
        chat to all "'Hurts, doesn't it?'"
        #delay by 7 seconds
          chat to all "'Feeling your skin bubble and your blood jet through your pores...'"
          #delay by 16 seconds
            chat to all "It did not hurt."
            #delay by 5 seconds
              chat to all "Not for me."
            #end delay
          #end delay
        #end delay
      #end delay
    #randor
      chat to all "I never believed in goblins."
      #delay by 5 seconds
        chat to all "Creatures of jealousy, I'm told. Born from your thoughts."
        #delay by 12 seconds
          chat to all "They claimed to see them all around me."
          #delay by 5 seconds
            chat to all "Perhaps their noses obscured their vision."
          #end delay
        #end delay
      #end delay
    #end select
  #end if

  #if research-completed ri-paladin
    #select random persistant
      chat to all "I had nightmares as a child."
      #delay by 6 seconds
        chat to all "Demons crawling on my back, peeling off rolls of skin with their claws."
        #delay by 16 seconds
          chat to all "The demons were saints, and I their lamb."
        #end delay
      #end delay
    #randor
      chat to all "People were born cruel, and people were born foolish. As was I."
      #delay by 15 seconds
        chat to all "But I have changed."
        #delay by 6 seconds
          chat to all "Go ahead. Lift up my veil. Watch my eyes sparkle."
        #end delay
      #end delay
    #randor
      chat to all "A wide-eyed girl looked up at the stars."
      #delay by 10 seconds
        chat to all "Were they pretty?"
        #delay by 4 seconds
          chat to all "Were they beautiful?"
          #delay by 8 seconds
            chat to all "All she saw was fire."
          #end delay
        #end delay
      #end delay
    #end select
  #end if

#end do