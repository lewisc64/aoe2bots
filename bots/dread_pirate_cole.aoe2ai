// yarr

const sea-fish = 905

#do once
  set name to "Dread Pirate Cole"
  #if not civ-selected bohemians
    chat to all "I be not the right civ. Bohemia is where me heart lie."
  #else
    chat to all "Take what ye can, give nothin' back."
  #end if
#end do

#do once grouped
  enable walls on perimeter 1
  enable walls on perimeter 2
  goal full-water-map = 0
  goal partial-water-map = 0
  #if cc-players-unit-type-count 0 sea-fish >= 1 and game-time < 5
    #if building-type-count stone-wall-line == 0 and building-type-count palisade-wall == 0 and wall-completed-percentage 2 == 100
      goal full-water-map = 1
    #else
      goal partial-water-map = 1
    #end if
  #end if
  #if goal full-water-map 1 or goal partial-water-map 1
    goal water-map = 1
  #else
    goal water-map = 0
  #end if
#end do

// diplomacy

#template diplo-chat-alliance-offer
  @up-get-player-color {player} -1
  #select random
    chat const to all "Join my crew, %s." 7031232
  #randor
    chat const to all "%s, join me search for loot." 7031232
  #end select
#end template

#template diplo-chat-our-betrayal
  @up-get-player-color {player} -1
  #select random
    chat to all "A change in the wind says I."
  #randor
    chat const to all "Avast, %s! Surrender yer doubloons!" 7031232
  #end select
#end template

#template diplo-chat-their-betrayal
  @up-get-player-color {player} -1
  #select random
    chat const to all "Blast! %s! Ye mutinous swab!" 7031232
  #randor
    chat const to all "Argh! %s! Ye damn scallywag!" 7031232
  #randor
    chat const to all "Slap me thrice! %s! Ye filthy bilge rat!" 7031232
  #end select
#end template

#template diplo-chat-accepted-our-alliance
  chat to all "A fine day."
#end template

#template diplo-chat-accepted-their-alliance
  chat to all "We have an accord."
#end template

#template diplo-chat-refused-our-alliance
  chat to all "I thought it be generous."
#end template

#template diplo-chat-refused-their-alliance
  chat to all "May ye be lost to the depths, ye filthy dog."
#end template

#template diplo-chat-friendly-fire-warning
  chat to all "Would ye stop blowin' holes in my ship?!"
#end template

#template diplo-chat-friendly-fire-betrayal
  chat to all "I be reachin' the end of me rope! Fire!!"
#end template

#template diplo-chat-resign
  chat to all "A captain... Always goes down with his ship..."
#end template

#template diplo-should-player-be-marked
  #if players-building-type-count {player} dock >= 1
    goal {marked_goal} = 1
  #end if
#end template

load "templates/diplomacy_engine.aoe2ai"
load "templates/fast_castle.aoe2ai"

#if up-research-status c: castle-age >= research-pending

  #do once
    set up basics
    distribute villagers 40 30 30 0
    sn-maximum-attack-group-size = 1
    sn-minimum-attack-group-size = 1
  #end do
  
  #if up-research-status c: ri-houfnice < research-pending
    #if military-population < 10
      escrow 10 wood
      escrow 10 food
      escrow 10 gold
    #else if military-population < 20
      escrow 20 wood
      escrow 20 food
      escrow 20 gold
    #else if military-population < 30
      escrow 40 wood
      escrow 40 food
      escrow 40 gold
    #else
      escrow 60 wood
      escrow 60 food
      escrow 60 gold
    #end if
  #else
    escrow 0 wood
    escrow 0 food
    escrow 0 gold
    @release-escrow wood
    @release-escrow food
    @release-escrow gold
  #end if
  
  manage scouting
  do basic targeting
  auto balance wood and food and gold

  build 1 town-center
  build 1 blacksmith
  build 1 university
  build 1 siege-workshop
  build 1 monastery
  #if current-age == imperial-age
    build 2 siege-workshop
  #end if
  #if goal water-map 1
    build 3 dock
  #end if
  #if goal full-water-map 0
    build 1 barracks
    build 3 archery-range
  #end if
  build lumber camps
  #when
    build gold mining camps
  #then
    sn-camp-max-distance += 8
  #end when
  build houses with 15 headroom
  build farms
  
  research ri-loom with gold escrow
  research lumber camp upgrades with wood and food escrow
  research gold mining camp upgrades with wood and food escrow
  research mill upgrades with wood and food escrow
  
  research imperial-age with food and gold escrow
  research ri-chemistry with food and gold escrow
  #if goal water-map 1 and building-type-count dock >= 1
    research ri-fletching with food and gold escrow
    research ri-bodkin-arrow with food and gold escrow
    research ri-bracer with food and gold escrow
    research ri-gillnets with wood and food escrow
    research ri-careening with food and gold escrow
    research ri-war-galley with food and gold escrow
    research ri-galleon with wood and food escrow
    research ri-deck-guns with wood and food escrow
  #end if
  #if goal full-water-map 0
    research ri-padded-archer-armor with food and gold escrow
    research ri-leather-archer-armor with food and gold escrow
    research ri-ring-archer-armor with food and gold escrow
    research ri-houfnice with food and gold escrow
  #end if
  
  goal warboat-count-goal = 0
  goal soldier-count-goal = 0
  @up-get-fact warboat-count 0 warboat-count-goal
  @up-get-fact soldier-count 0 soldier-count-goal
  
  #if goal full-water-map 0 and up-compare-goal soldier-count-goal g:<= warboat-count-goal or building-type-count dock == 0 or goal water-map 0
    train hand-cannoneer
    #if research-completed ri-houfnice
      train houfnice
    #else
      train bombard-cannon
    #end if
  #else
    train galley-line
    train cannon-galleon-line
  #end if

  #if current-age == imperial-age
    #if goal water-map 0
      train 120 villagers with 40 trade
    #else
      train 100 villager
      #if players-building-type-count any-ally dock >= 1 and warboat-count >= 10
        train 20 trade-cog
      #end if
    #end if
  #else
    train 120 villager
  #end if
  train 10 fishing-ship

  #if military-population >= 30
    #do once
      #if goal water-map 1
        chat to all "Avast, ye! Ready the guns! Stay the mainsail!"
      #else
        chat to all "Avast, ye! Ready the guns!"
      #end if
    #end do
    sn-number-boat-attack-groups = 200
    sn-number-attack-groups = 200
    insert diplo-mark-random-ally-if-no-enemies
  #else
    sn-number-boat-attack-groups = 0
    sn-number-attack-groups = 0
  #end if

  auto expand town size to 30

#end if

// misc dialog

#do once
  #if unit-type-count fire-ship-line >= 10
    chat to all "The sea beckons, and I be heeding her call."
  #end if
  
  #if unit-type-count fire-ship-line >= 50
    chat to all "I be the scourge of the seven seas, know it well."
  #end if

  #if unit-type-count cannon-galleon-line >= 10
    chat to all "The finest cannons in all the world!"
  #end if

  #if unit-type-count bombard-cannon >= 1 and goal water-map 0
    chat to all "I miss the sea..."
  #end if
  
  #if research-completed ri-ring-archer-armor
    chat to all "Nothin' like fresh powder to wake ye up."
  #end if
#end do
