#do once
  set up basics
#end do

#if up-research-status c: castle-age < research-pending
  
  manage scouting

  
  #if building-type-count-total market >= 1 and building-type-count-total blacksmith >= 1
    research ri-double-bit-axe
    research ri-horse-collar
  #end if
  
  #if up-research-status c: ri-double-bit-axe >= research-pending and up-research-status c: ri-horse-collar >= research-pending
    research castle-age
  #end if
  
  micro sheep
  #if unit-type-count villager >= 10
    lure boars
  #end if
  push deer

  build 1 town-center
  build houses
  #if game-time > 60
    build lumber camps
  #end if
  build mule carts
  micro mule carts
  #if building-type-count lumber-camp >= 1 or building-type-count mule-cart >= 1
    build mills
  #end if
  build 1 market
  build 1 blacksmith
  
#end if

#if up-research-status c: feudal-age < research-pending

  #do once
    distribute villagers 20 80 0 0
    sn-enable-training-queue = 1
  #end do once
  
  #if civilian-population >= 20
    #do once
      distribute villagers 40 60 0 0
    #end do
  #end if

  #if unit-type-count-total villager >= 24
    sn-enable-training-queue = 0
    research feudal-age
  #end if
  
  train 40 villager
  research ri-loom
  
  #if up-pending-objects c: villager <= 1 and strategic-number sn-enable-training-queue == 1
    drop off food
  #end if
  
  #if building-type-count-total lumber-camp >= 1
    build farms
  #end if
  
#else if up-research-status c: castle-age < research-pending
  
  research ri-loom

  #when
    build gold mining camps
  #then
    sn-camp-max-distance += 3
  #end when
  
  #if wood-amount > 100
    build farms
  #end if

  #do once
    distribute villagers 50 40 10 0
    #if building-type-count-total blacksmith >= 1 and building-type-count-total market >= 1
      distribute villagers 30 60 10 0
    #end if
  #end do
  train villager
  
  #if food-amount < 600 or gold-amount < 200
    sell wood when wood >= 200
  #end if
  
  #if food-amount < 800
    buy food when gold >= 300
  #end if
  
  #if gold-amount < 200
    sell food when food >= 900
  #end if

#end if
