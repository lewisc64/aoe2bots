load "templates/fast_castle.aoe2ai"

do basic diplomacy

#if up-research-status c: castle-age >= research-pending
  
  #do once
    distribute villagers 18 50 15 17
    escrow 10 food
    escrow 10 gold
    assign 8 builders to castle
    sn-percent-building-cancellation = 0
    sn-maximum-attack-group-size = 1
    sn-minimum-attack-group-size = 1
    sn-number-attack-groups = 200
  #end do
  
  manage scouting
  
  sell wood when wood > 1500
  sell food when food > 1500
  buy stone when gold > 1500
  
  build 3 town-center
  #if up-pending-objects c: castle == 0
    build forward castle near town-center
  #else
    build castle
  #end if
  build houses
  build farms
  build lumber camps
  build 1 market
  build 1 blacksmith
  build 1 barracks
  
  #when
    build stone mining camps
    build gold mining camps
  #then
    sn-camp-max-distance += 8
  #end when
  
  research mill upgrades with wood and food escrow
  research lumber camp upgrades with wood and food escrow
  research stone mining camp upgrades with wood and food escrow
  research gold mining camp upgrades with wood and food escrow
  
  research imperial-age with food and gold escrow
  research blacksmith infantry upgrades with food and gold escrow
  research ri-tracking with food and gold escrow
  research ri-squires with food and gold escrow
  research ri-arson with food and gold escrow
  #when
    research my-unique-unit-upgrade with food and gold escrow
  #then
    escrow 0 food
    escrow 0 gold
  #end when
  
  train 120 villagers with 30 trade
  train 3 trebuchet
  train my-unique-unit

#end if