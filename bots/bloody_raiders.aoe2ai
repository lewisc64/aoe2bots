// play as slavs

#do once
  set up basics
  sn-maximum-attack-group-size = 1
  sn-minimum-attack-group-size = 1
  sn-number-attack-groups = 200
  sn-special-attack-type2 = villager
  
  assign 8 builders to castle
  assign 4 builders to town-center-foundation
#end do

manage scouting
do basic diplomacy

build 1 town-center
build lumber camps
#if current-age >= feudal-age
  #when
    build gold mining camps
    build stone mining camps
  #then
    sn-camp-max-distance += 8
  #end when
#end if

build mills
#if current-age == dark-age and game-time < 720
  build 3 farm
#else
  #if wood-amount >= 200 or building-type-count-total farm < 8
    build farms
  #end if
#end if

build castle
#if building-type-count-total stable >= 1
  build 1 blacksmith
  build 1 university
  #if building-type-count-total blacksmith >= 1
    build 1 market
  #end if
#end if

#if up-research-status c: feudal-age >= research-pending
  build 1 barracks
#end if

#if current-age == feudal-age
  build 4 stable
#else if current-age == castle-age
  build 5 stable
  build 2 siege-workshop
#else
  build 8 stable
  build 3 siege-workshop
#end if

lure boars

#if up-research-status c: feudal-age < research-pending
  #do once
    distribute villagers 20 80 0 0
  #end do
  #if civilian-population >= 18
    #do once
      distribute villagers 40 60 0 0
    #end do
  #end if
#else
  #do once
    distribute villagers 37 47 12 4
  #end do
#end if

#if current-age == dark-age
  build houses with 5 headroom
#else if current-age == feudal-age
  build houses with 10 headroom
#else
  build houses with 20 headroom
#end if

research feudal-age
research castle-age
research imperial-age

research ri-loom
research ri-wheel-barrow
research ri-hand-cart
research mill upgrades
research lumber camp upgrades
research gold mining camp upgrades

research ri-bloodlines
research ri-husbandry
research cavalry blacksmith upgrades
research ri-light-cavalry
research ri-hussar
research my-unique-unit-upgrade

research ri-siege-engineers
research ri-onager
research ri-siege-onager

#if current-age >= castle-age
  train 120 villagers with 30 trade
#else
  train 120 villager
#end if

#if building-type-count castle >= 1 and current-age == imperial-age and unit-type-count-total trebuchet-set < 10 and up-pending-objects c: trebuchet-set == 0 and not town-under-attack
  train trebuchet
#else
  train 4 mangonel-line
  
  #if unit-type-count-total mangonel-line >= 4
    train my-unique-unit-line
  #end if
  
  #if game-time < 2400 or up-research-status c: castle-age >= research-pending or up-pending-objects c: scout-cavalry-line < 2 or town-under-attack
    train scout-cavalry-line
  #end if
#end if

do basic targeting

trade excess wood and food and gold at 2000
#if current-age >= feudal-age
  auto balance wood and food and gold
#end if
auto expand town size to 30
