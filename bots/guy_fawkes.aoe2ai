load "templates/fast_castle.aoe2ai"

do basic diplomacy

#if current-age >= castle-age
  
  #do once
    distribute villagers 25 40 15 20
    
    assign 8 builders to castle
    assign 4 builders to town-center-foundation
  #end do
  
  manage scouting
  
  #when
    build gold mining camps
    build stone mining camps
  #then
    sn-camp-max-distance += 8
  #end when
  build lumber camps
  build castle
  build 1 university
  build 1 blacksmith
  build houses with 15 headroom
  
  build mills
  #if wood-amount >= 200
    build farms
  #end if
  
  #if building-type-count-total castle > 0
    build 5 town-center
  #end if
  
  research town center upgrades
  research lumber camp upgrades
  research mill upgrades
  research mining camp upgrades
  research ri-murder-holes
  research ri-conscription
  research ri-hoardings
  research ri-ballistics
  research ri-architecture
  research ri-masonry
  research ri-fletching
  research ri-bodkin-arrow
  research ri-bracer
  research ri-chemistry
  research ri-siege-engineers
  
  train 130 villager with 30 trade
  train petard
  
  buy stone when gold > 1000
  sell wood when wood > 1000
  sell food when food > 1500
  
  do basic targeting
  
  #if unit-type-count-total petard >= 50
    sn-maximum-town-size = 255
    #do once
      chat to all "DOWN WITH THE GOVERNMENT!"
    #end do
  #else
    auto expand town size to 30
  #end if
  
  auto balance wood and food and gold

#end if

#if building-type-count castle > 0
  #do once
    chat to all "How do explosives work again?"
  #end do
#end if