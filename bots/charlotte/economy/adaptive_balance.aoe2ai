// should be loaded after the director

const max-wood = 1600
const max-food = 1600
const max-gold = 1600
const max-stone = 1600

const half-max-wood = 800
const half-max-food = 800
const half-max-gold = 800

const minimum-wood-gatherer-percentage = 30
const minimum-food-gatherer-percentage = 30
const minimum-gold-gatherer-percentage = 10

const wood-threshold = 300
const food-threshold = 300
const gold-threshold = 300
const stone-threshold = 300

const good-wood-price = 75
const good-food-price = 75
const good-wood-sell-price = 150
const good-food-sell-price = 150

const excellent-wood-price = 50
const excellent-food-price = 50
const excellent-wood-sell-price = 200
const excellent-food-sell-price = 200

#if wood-amount > 200
  build 1 market
#end if

#template send-excess-to-ally
  #if up-allied-resource-amount any-ally {resource} < {resource}-threshold and up-compare-goal non-escrowed-{resource} c:> max-{resource}
    tribute 100 {resource} to this-any-ally
    goal did-tribute = 1
    chat to self "sending {resource} to ally"
  #end if
#end template

#template handle-ally-resource-request
  #reply to ally taunt {taunt}
    #if up-compare-goal non-escrowed-{resource} c:> {resource}-threshold and building-type-count market >= 1
      tribute 100 {resource} to this-any-ally
      goal did-tribute = 1
      insert say-yes-friendly(player="every-ally")
    #else
      insert say-no-friendly(player="every-ally")
    #end if
  #end reply
#end template

goal did-tribute = 0

#if goal team-game 1
  insert send-excess-to-ally(resource="wood")
  insert send-excess-to-ally(resource="food")
  insert send-excess-to-ally(resource="gold")
  insert send-excess-to-ally(resource="stone")
  
  insert handle-ally-resource-request(taunt=3,resource="food")
  insert handle-ally-resource-request(taunt=4,resource="wood")
  insert handle-ally-resource-request(taunt=5,resource="gold")
  insert handle-ally-resource-request(taunt=6,resource="stone")
#end if

#if goal team-game 0 or goal did-tribute 0
  trade excess wood and food and gold at max-gold
#end if

#if town-under-attack and goal attacking 0
  trade excess wood and food and gold at gold-threshold
#else
  #if commodity-buying-price wood <= excellent-wood-price and (up-compare-goal non-escrowed-wood c:< wood-threshold or up-compare-goal non-escrowed-gold c:> gold-threshold) and up-compare-goal non-escrowed-gold c:> gold-threshold and wood-amount < half-max-wood
    chat to self "buy wood, excellent price."
    buy wood
  #end if

  #if commodity-buying-price food <= excellent-food-price and (up-compare-goal non-escrowed-food c:< food-threshold or up-compare-goal non-escrowed-gold c:> gold-threshold) and up-compare-goal non-escrowed-gold c:> gold-threshold and food-amount < half-max-food
    chat to self "buy food, excellent price."
    buy food
  #end if

  #if commodity-selling-price wood >= excellent-wood-sell-price and (up-compare-goal non-escrowed-wood c:> wood-threshold or up-compare-goal non-escrowed-gold c:< gold-threshold) and up-compare-goal non-escrowed-wood c:> wood-threshold and gold-amount < half-max-gold
    chat to self "sell wood, excellent price."
    sell wood
  #end if

  #if commodity-selling-price food >= excellent-food-sell-price and (up-compare-goal non-escrowed-food c:> food-threshold or up-compare-goal non-escrowed-gold c:< gold-threshold) and up-compare-goal non-escrowed-food c:> food-threshold and gold-amount < half-max-gold
    chat to self "sell food, excellent price."
    sell food
  #end if

  #if up-compare-goal non-escrowed-gold c:> gold-threshold
    #if commodity-buying-price wood <= good-wood-price and up-compare-goal non-escrowed-wood c:< wood-threshold
      chat to self "buy wood, good price."
      buy wood
    #end if
    #if commodity-buying-price food <= good-food-price and up-compare-goal non-escrowed-food c:< food-threshold
      chat to self "buy food, good price."
      buy food
    #end if
  #else
    #if commodity-selling-price wood >= good-wood-sell-price and up-compare-goal non-escrowed-wood c:> wood-threshold
      chat to self "sell wood, good price."
      sell wood
    #end if
    #if commodity-selling-price food >= good-food-sell-price and up-compare-goal non-escrowed-food c:> food-threshold
      chat to self "sell food, good price."
      sell food
    #end if
  #end if
#end if

#repeat every 60 seconds
  #if up-compare-goal non-escrowed-wood c:> wood-threshold
    #if up-compare-goal non-escrowed-food c:> food-threshold
      #if up-compare-goal non-escrowed-gold c:> gold-threshold
        // do nothing, all above thresholds.
      #else
        distribute 4 villagers from wood and food to gold
      #end if
    #else
      #if up-compare-goal non-escrowed-gold c:> gold-threshold
        distribute 4 villagers from wood and gold to food
      #else
        distribute 4 villagers from wood to food and gold
      #end if
    #end if
  #else
    #if up-compare-goal non-escrowed-food c:> food-threshold
      #if up-compare-goal non-escrowed-gold c:> gold-threshold
        distribute 4 villagers from food and gold to wood
      #else
        distribute 4 villagers from food to wood and gold
      #end if
    #else
      #if up-compare-goal non-escrowed-gold c:> gold-threshold
        distribute 4 villagers from gold to wood and food
      #else
        // do nothing, all below thresholds.
      #end if
    #end if
  #end if
#end repeat

#if strategic-number sn-wood-gatherer-percentage < minimum-wood-gatherer-percentage
  distribute 2 villagers from food and gold to wood
#end if

#if strategic-number sn-food-gatherer-percentage < minimum-food-gatherer-percentage
  distribute 2 villagers from wood and gold to food
#end if

#if strategic-number sn-gold-gatherer-percentage < minimum-gold-gatherer-percentage
  distribute 2 villagers from wood and food to gold
#end if

#if goal do-stone-mining 1
  #select random persistant
    insert divert-to-stone(percent="8")
  #randor
    insert divert-to-stone(percent="4")
  #end select
#else
  insert divert-all-from-stone
#end if
