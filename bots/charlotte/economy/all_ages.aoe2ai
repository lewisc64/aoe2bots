// nomad TC
#if game-time < 60
  build 1 town-center with wood and stone escrow
#end if

// find sheep
#if game-time < 60 and unit-type-count-total live-sheep == 0
  sn-percent-civilian-explorers = 100
  sn-cap-civilian-explorers = 200
  sn-total-number-explorers = 200
  sn-number-explore-groups = 200
#else
  sn-cap-civilian-explorers = 1
  sn-total-number-explorers = 1
  sn-number-explore-groups = 1
  #if military-population >= 1 or game-time >= 400
    #do once
      sn-percent-civilian-explorers = 0
      sn-cap-civilian-explorers = 0
    #end do
  #end if
#end if

// lure boars
lure boars

// stop clogging everything!
#if strategic-number sn-task-ungrouped-soldiers == 1
  sn-task-ungrouped-soldiers = 0
#end if
#repeat every 30 seconds
  #if goal attacking 0
    sn-task-ungrouped-soldiers = 1
  #end if
#end repeat

// gather relics
build 1 monastery
train 1 monk

// build houses with two vills at the start of the game
#if building-type-count house == 0
  #do once
    assign 2 builders to house
  #end do
#else
  #do once
    assign 1 builder to house
  #end do
#end if

build houses with 5 headroom

// lumber camps
#when
  build lumber camps
#then
  sn-camp-max-distance += 1
#end when

// town centers
#if up-compare-goal town-center-count g:< desired-town-centers
  #if current-age >= castle-age
    build 1 market
    #if strategic-number sn-stone-gatherer-percentage == 0
      buy stone when stone < 100
    #end if
  #end if
  build town-center
#end if

// castle forwards?
#if up-pending-objects c: castle == 0
  #select random
    build castle
  #randor
    #if goal attacking 1
      build forward castle near town-center
    #end if
  #end select
#end if

// mining camps
#when
  #if strategic-number sn-gold-gatherer-percentage > 0
    build gold mining camps
  #end if
  #if strategic-number sn-stone-gatherer-percentage > 0
    build stone mining camps
  #end if
#then
  sn-camp-max-distance += 4
#end when

#if current-age == dark-age

  #do once
    distribute villagers 20 80 0 0
    #if civilian-population >= 10
      distribute villagers 25 75 0 0
    #end if
    #if civilian-population >= 15
      distribute villagers 30 70 0 0
    #end if
    #if civilian-population >= 18
      distribute villagers 40 60 0 0
    #end if
    #if civilian-population >= 25
      distribute villagers 50 50 0 0
    #end if
  #end do

  build mills
  #if building-type-count-total lumber-camp >= 1
    build 1 mill
  #end if
  build safety mill
  
  #if wood-amount >= 100 and up-pending-objects c: farm == 0
    build farms
  #end if
  
  #if food-amount < 50 and up-pending-objects c: villager <= 1
    drop off food
  #end if

#end if

#if up-research-status c: feudal-age >= research-pending

  #if goal produce-unique-units 1
    #do once
      distribute villagers 37 37 18 8
    #end do
  #else
    #select random persistant
      #do once
        distribute villagers 37 37 18 8
      #end do
    #randor
      #do once
        distribute villagers 38 38 20 4
      #end do
    #randor
      #do once
        distribute villagers 40 40 20 0
      #end do
    #end select
  #end if
  
  build houses with 20 headroom
  
  #if wood-amount >= 200
    build farms
    #if (dropsite-min-distance sea-fish > 10 or dropsite-min-distance sea-fish == -1) and unit-type-count-total fishing-ship > 0
      //build fish traps
    #end if
  #end if
  
#end if

#if current-age >= castle-age
  delete walls
#end if