#do once
  #if up-research-status c: imperial-age >= research-pending
    sn-maximum-town-size = imperial-age-town-size
    sn-minimum-town-size = imperial-age-town-size
    sn-safe-town-size = imperial-age-town-size
    sn-maximum-food-drop-distance = imperial-age-town-size
    sn-maximum-gold-drop-distance = 16
    sn-maximum-stone-drop-distance = 16
  #else if up-research-status c: castle-age >= research-pending
    sn-maximum-town-size = castle-age-town-size
    sn-minimum-town-size = castle-age-town-size
    sn-safe-town-size = castle-age-town-size
    sn-maximum-food-drop-distance = castle-age-town-size
    sn-maximum-gold-drop-distance = 8
    sn-maximum-stone-drop-distance = 8
  #else if up-research-status c: feudal-age >= research-pending
    sn-maximum-town-size = feudal-age-town-size
    sn-minimum-town-size = feudal-age-town-size
    sn-safe-town-size = feudal-age-town-size
    sn-maximum-food-drop-distance = feudal-age-town-size
    sn-maximum-gold-drop-distance = 8
    sn-maximum-stone-drop-distance = 8
  #end if
#end do

// stop clogging everything!
#if strategic-number sn-task-ungrouped-soldiers == 1
  sn-task-ungrouped-soldiers = 0
#end if
#repeat every 30 seconds
  #if goal attacking 0
    sn-task-ungrouped-soldiers = 1
  #end if
#end repeat

// how late do we trade?
#if goal team-game 1 and current-age >= castle-age and (goal full-water-map 0 or map-type team-islands)
  build 1 market
  #if players-building-type-count any-ally market >= 1
    #select random persistant
      goal produce-trade-carts = 1
    #randor
      #if current-age >= imperial-age
        goal produce-trade-carts = 0
      #end if
    #end select
  #end if
#end if

// do we gather relics?
#select random persistant
  build 1 monastery
  train 1 monk
#randor
  // do not gather relics
#end select

// build houses with two vills at the start of the game
#if building-type-count house == 0
  #do once
    assign 2 builders to house
  #end do
#else
  #do once
    assign 1 builder to house
  #end do
#end if

build houses with 5 headroom
#when
  build lumber camps
#then
  sn-camp-max-distance += 1
#end when

#if up-compare-goal town-center-count g:< desired-town-centers
  #if current-age >= castle-age
    build 1 market
    #if strategic-number sn-stone-gatherer-percentage == 0
      buy stone when stone < 100
    #end if
  #end if
  build town-center
#end if

// castle forwards?
#select random
  build castle
#randor
  #if goal attacking 1
    build forward castle near town-center
  #end if
#end select

#when
  #if strategic-number sn-gold-gatherer-percentage > 0
    build gold mining camps
  #end if
  #if strategic-number sn-stone-gatherer-percentage > 0
    build stone mining camps
  #end if
#then
  sn-camp-max-distance += 4
#end when

#if current-age == dark-age

  #do once
    distribute villagers 25 75 0 0
  #end do
  
  lure boars

  build mills
  build safety mill
  
  #if wood-amount >= 100 and up-pending-objects c: farm == 0 and (goal water-map 0 or up-compare-goal total-fishing-ships g:>= desired-fishing-ships)
    build farms
  #end if
  
  #if food-amount < 50 and up-pending-objects c: villager <= 1
    drop off food
  #end if

#else if current-age == feudal-age
  
#end if

#if up-research-status c: feudal-age >= research-pending

  #if goal produce-unique-units 1
    #do once
      distribute villagers 37 37 18 8
    #end do
  #else
    #select random persistant
      #do once
        distribute villagers 37 37 18 8
      #end do
    #randor
      #do once
        distribute villagers 38 38 20 4
      #end do
    #randor
      #do once
        distribute villagers 40 40 20 0
      #end do
    #end select
  #end if
  
  build houses with 20 headroom
  
  #if wood-amount >= 200 and (goal water-map 0 or up-compare-goal total-fishing-ships g:>= desired-fishing-ships)
    build farms
    #if dropsite-min-distance sea-fish > 10 or dropsite-min-distance sea-fish == -1 and unit-type-count-total fishing-ship > 0
      //build fish traps
    #end if
  #end if
  
#end if

#if current-age == castle-age
  delete walls
#end if