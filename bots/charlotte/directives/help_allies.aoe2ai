// must load after all_switched!

// ally still in dark age after 15 minutes
#if current-age >= feudal-age and players-current-age any-ally == dark-age and game-time >= 900
  
  // ally is not building a lumber camp or mill.
  #if (players-building-type-count this-any-ally lumber-camp == 0 or players-building-type-count this-any-ally mill == 0) and up-allied-resource-amount this-any-ally wood < 100
    #if goal enough-wood 1 and players-tribute-memory this-any-ally wood < 500
      tribute 100 wood to this-any-ally
    #end if
  #end if
  
  // ally is not aging up due to food
  #if up-allied-resource-amount this-any-ally food < 500 and goal enough-food 1
    tribute 100 food to this-any-ally
  #end if
#end if

// ally potentially stuck in feudal
#if current-age == castle-age and players-current-age any-ally == feudal-age

  // ally is not building the required buildings
  #if players-building-type-count this-any-ally blacksmith == 0 and (players-building-type-count this-any-ally market == 0 or players-building-type-count this-any-ally archery-range == 0 or players-building-type-count this-any-ally stable == 0)
    #if goal enough-wood 1 and up-allied-resource-amount this-any-ally wood < 200 and players-tribute-memory this-any-ally wood < 1000
      tribute 100 wood to this-any-ally
    #end if
  #end if

  // ally is not aging up due to food
  #if up-allied-resource-amount this-any-ally food < 800 and goal enough-food 1 and players-tribute-memory this-any-ally food < 1600
    tribute 100 food to this-any-ally
  #end if
  
  // ally is not aging up due to gold
  #if up-allied-resource-amount this-any-ally gold < 200 and goal enough-gold 1 and players-tribute-memory this-any-ally gold < 400
    tribute 100 gold to this-any-ally
  #end if
#end if
