// enable training queue if there's no research
#if goal attempted-researches 0
  sn-enable-training-queue = 1
#else
  sn-enable-training-queue = 0
#end if

// cancel buildings based on dead builders
#do once
  goal dead-builders = 0
  goal dead-female-builders = 0
#end do
@up-get-fact unit-type-count dead-male-builder dead-builders
@up-get-fact unit-type-count dead-female-builder dead-female-builders
@up-modify-goal dead-builders g:+ dead-female-builders

#if up-compare-goal dead-builders c:>= 8
  sn-percent-building-cancellation = 100
#else if up-compare-goal dead-builders c:>= 1
  sn-percent-building-cancellation = 20
#else
  sn-percent-building-cancellation = 0
#end if

// increase production over time
#if up-compare-goal population-space-remaining c:> 10 and wood-amount >= 1000 and food-amount >= 1000 and gold-amount >= 1000
  #repeat every 10 minutes
    goal desired-military-buildings-per-type += 1
    goal desired-town-centers += 1
  #end repeat
#end if

// build houses with two vills at the start of the game
#if building-type-count house == 0
  #do once
    assign 2 builders to house
  #end do
#else
  #do once
    assign 1 builder to house
  #end do
#end if

// stop clogging everything!
#if strategic-number sn-task-ungrouped-soldiers == 1
  sn-task-ungrouped-soldiers = 0
#end if
#repeat every 30 seconds
  #if goal attacking 0
    sn-task-ungrouped-soldiers = 1
  #end if
#end repeat

// walls are bad
#if current-age >= castle-age
  delete walls
#end if

// force drop off food
#if current-age == dark-age and food-amount < 50 and up-pending-objects c: villager <= 1
  drop off food
#end if

// do not scout when under attack
#if town-under-attack
  #if strategic-number sn-number-explore-groups >= 1
    sn-number-explore-groups = 0
    sn-total-number-explorers = 0
    @up-reset-scouts
  #end if
#else if strategic-number sn-number-explore-groups == 0
  sn-number-explore-groups = 1
  sn-total-number-explorers = 1
  scout enemy
#end if

// build feitorias
#if civ-selected portuguese
  #select random persistant
    // do nothing
  #randor
    build 1 feitoria
  #randor
    build 2 feitoria
  #end select
#end if

// mine stone in feudal age if unique units are selected
#if goal produce-unique-units 1 and current-age == feudal-age
  goal do-stone-mining = 1
#end if

// build more town centers once fully boomed
#if up-compare-goal total-villagers g:>= desired-villagers
  #do once
    @up-modify-goal desired-town-centers c:max 6
  #end do
#end if

// run scout towards sheep
#if game-time < 600
  $clear
  $find local 1 scout-cavalry-line
  $find local 1 eagle-warrior-line
  $focus gaia
  $find remote livestock-class
  $move
#end if
