// must be loaded after any research.

#do once
  goal attack-ready = 0
  goal attacking = 0
  goal do-backstep = 0
  goal emergency-trash = 0
  goal do-counter-systems = 1
#end do

load "age/feudal_rush.aoe2ai"

load "misc_strats.aoe2ai"

#select random persistant
  load "fortification/full.aoe2ai"
#randor
  load "fortification/murder_holes_only.aoe2ai"
#end select

load "unit_selection/fishing.aoe2ai"
load "unit_selection/trading.aoe2ai"

#if goal attack-ready 1
  #do once
    chat to self "attack ready!"
  #end do
  
  load "civ_decisions.aoe2ai"
  
  #if goal full-water-map 0 and gold-amount < 3000 and up-compare-goal no-gold-mining-time c:>= 120 and resource-found gold and unit-type-count trade-cart < 10 and game-time >= 1800
    goal emergency-trash = 1
  #end if
  
  #if gold-amount > 4000
    goal emergency-trash = 0
  #end if
  
  #if goal emergency-trash 1
    load "emergency_trash.aoe2ai"
  #else
    load "unit_selection/random_unit_selection.aoe2ai"
    #if goal do-counter-systems 1
      load "unit_selection/being_countered_system.aoe2ai"
      load "unit_selection/counter_system.aoe2ai"
    #end if
    
    #do once
      goal no-unit-rule-passes = 0
      goal director-units-selected = 0
    #end do
    insert random-unit-selection-count-units-selected(goal="director-units-selected")
    #if goal director-units-selected 0
      goal no-unit-rule-passes += 1
    #else
      goal no-unit-rule-passes = 0
    #end if
    #if up-compare-goal no-unit-rule-passes c:>= 60
      chat to self "ATTENTION: failed to select units, turning off counter systems"
      goal do-counter-systems = 0
    #end if
  #end if
  
  #if players-building-count any-enemy >= 1
    load "unit_selection/mandatory_siege.aoe2ai"
  #end if
  load "unit_selection/transporting.aoe2ai"
  
  load "attack_decision/aggressive.aoe2ai"
  load "pacing/balanced_random.aoe2ai"
  
  #if up-compare-goal treaty-time-left >= 600
    load "land_control/passive.aoe2ai"
    load "water_control/passive.aoe2ai"
  #else if goal full-water-map 1
    load "land_control/passive.aoe2ai"
    load "water_control/aggressive.aoe2ai"
  #else
    load "land_control/aggressive.aoe2ai"
    load "water_control/reactive.aoe2ai"
  #end if
  
  load "targeting.aoe2ai"
  
  #if not goal enemy-players-count 0
    #if unit-type-count transport-ship > 0
      load "attack_method/attack_now.aoe2ai"
    #else
      load "attack_method/attack_groups.aoe2ai"
    #end if
  #end if
#else
  goal do-research = 1
  goal produce-units = 0
#end if

#if goal resign 0 and goal locked-teams 0 and (goal team-game 1 or goal free-for-all 1)
  load "diplomacy/diplomacy.aoe2ai"
#else if goal resign 1
  @set-stance every-enemy ally
  @set-stance every-neutral ally
#end if

load "help_allies.aoe2ai"
load "resigning.aoe2ai"
