const alliance-request-timeout = 60
const allied-victory-timeout = 120

#template diplo-set-up-player-goals
  goal player-{player}-is-bad-egg = 0
  goal player-{player}-alliance-request-time-stamp = 0
  goal allied-with-{player} = 0
  goal tried-alliance-with-{player} = 0
#end template

#template handle-stance-changes
  #if player-in-game {player}
  
    #if goal player-{player}-is-bad-egg 0 and goal allied-with-{player} 0
      #if players-stance {player} ally
        #if stance-toward {player} enemy or stance-toward {player} neutral
          #if goal accept-alliance-requests 1
            insert say-accept-alliance-line(player={player})
            chat to self "accepting alliance request from player {player}"
            @set-stance {player} ally
            goal allied-with-{player} = 1
          #end if
        #else
          insert say-alliance-begin-line(player={player})
          chat to self "player {player} accepted our alliance request"
          goal allied-with-{player} = 1
        #end if
      #end if
    #end if
    
    #if goal allied-with-{player} 1 and not players-stance {player} ally
      insert say-alliance-betrayal-line(player={player})
      //chat to self "betrayed by player {player}"
      goal player-{player}-is-bad-egg = 1
      goal allied-with-{player} = 0
      @set-stance {player} enemy
    #end if
    
    #if goal threat-player {player} and not stance-toward {player} enemy
      //chat to self "attacked by player {player}"
      goal allied-with-{player} = 0
      @set-stance {player} enemy
    #end if
    
    #if goal allied-with-{player} 0 and stance-toward {player} ally and goal player-{player}-is-bad-egg 0
      @up-modify-goal temp g:= game-time-goal
      @up-modify-goal temp g:- player-{player}-alliance-request-time-stamp
      #if up-compare-goal temp c:>= alliance-request-timeout
        insert say-request-alliance-timeout-line(player={player})
        chat to self "alliance request to player {player} timed out"
        @set-stance {player} neutral
        goal tried-alliance-with-{player} = 1
      #end if
    #end if
    
  #else if players-stance {player} ally and not stance-toward {player} ally // ally players that resigned
    @set-stance {player} ally
  #end if
#end template

#template betray-ally
  #if player-in-game {player} and goal allied-with-{player} 1
    insert say-do-betrayal-line(player={player})
    //chat to self "betraying player {player}"
    @set-stance {player} enemy
    goal player-{player}-is-bad-egg = 1
    goal allied-with-{player} = 0
  #end if
#end template

#template enemy-if-neutral
  #if player-in-game {player} and stance-toward {player} neutral
    //chat to self "making neutral player {player} our enemy"
    @set-stance {player} enemy
  #end if
#end template

#template ally-if-not-bad-egg
  #if player-in-game {player} and goal player-{player}-is-bad-egg 0 and not stance-toward {player} ally and goal tried-alliance-with-{player} 0
    insert say-request-alliance-line(player={player})
    chat to self "requesting alliance from player {player}"
    @set-stance {player} ally
    @up-modify-goal player-{player}-alliance-request-time-stamp g:= game-time-goal
  #end if
#end template

#do once
  goal want-alliance = 0
  goal accept-alliance-requests = 1
  insert diplo-set-up-player-goals(player=1)
  insert diplo-set-up-player-goals(player=2)
  insert diplo-set-up-player-goals(player=3)
  insert diplo-set-up-player-goals(player=4)
  insert diplo-set-up-player-goals(player=5)
  insert diplo-set-up-player-goals(player=6)
  insert diplo-set-up-player-goals(player=7)
  insert diplo-set-up-player-goals(player=8)
  @set-stance every-enemy neutral
  goal no-enemies-stamp = 0
#end do

#select random persistant
  load "pragmatic.aoe2ai"
#randor
  load "backstabbing.aoe2ai"
#randor
  load "pair_seeking.aoe2ai"
#randor
  load "introverted.aoe2ai"
#end select

#if not (goal enemy-players-count 0 and goal neutral-players-count 0)
  @up-modify-goal no-enemies-stamp g:= game-time-goal
#end if

@up-modify-goal temp g:= game-time-goal
@up-modify-goal temp g:- no-enemies-stamp
#if up-compare-goal temp c:>= allied-victory-timeout and game-time >= 2400 // the final betrayal.
  #do once
    insert say-no-allied-victory-line
    goal player-1-is-bad-egg = 1
    goal player-2-is-bad-egg = 1
    goal player-3-is-bad-egg = 1
    goal player-4-is-bad-egg = 1
    goal player-5-is-bad-egg = 1
    goal player-6-is-bad-egg = 1
    goal player-7-is-bad-egg = 1
    goal player-8-is-bad-egg = 1
    goal team-game = 0
    goal free-for-all = 1
    goal locked-teams = 1
    @set-stance every-ally enemy
  #end do
#end if

#if goal want-alliance 1
  #select random
    insert ally-if-not-bad-egg(player=1)
  #randor
    insert ally-if-not-bad-egg(player=2)
  #randor
    insert ally-if-not-bad-egg(player=3)
  #randor
    insert ally-if-not-bad-egg(player=4)
  #randor
    insert ally-if-not-bad-egg(player=5)
  #randor
    insert ally-if-not-bad-egg(player=6)
  #randor
    insert ally-if-not-bad-egg(player=7)
  #randor
    insert ally-if-not-bad-egg(player=8)
  #end select
#end if

#if goal enemy-players-count 0 and goal want-alliance 0
  #select random
    insert enemy-if-neutral(player=1)
  #randor
    insert enemy-if-neutral(player=2)
  #randor
    insert enemy-if-neutral(player=3)
  #randor
    insert enemy-if-neutral(player=4)
  #randor
    insert enemy-if-neutral(player=5)
  #randor
    insert enemy-if-neutral(player=6)
  #randor
    insert enemy-if-neutral(player=7)
  #randor
    insert enemy-if-neutral(player=8)
  #end select
#end if

#if goal want-alliance -1
  #select random
    insert betray-ally(player=1)
  #randor
    insert betray-ally(player=2)
  #randor
    insert betray-ally(player=3)
  #randor
    insert betray-ally(player=4)
  #randor
    insert betray-ally(player=5)
  #randor
    insert betray-ally(player=6)
  #randor
    insert betray-ally(player=7)
  #randor
    insert betray-ally(player=8)
  #end select
#end if

insert handle-stance-changes(player=1)
insert handle-stance-changes(player=2)
insert handle-stance-changes(player=3)
insert handle-stance-changes(player=4)
insert handle-stance-changes(player=5)
insert handle-stance-changes(player=6)
insert handle-stance-changes(player=7)
insert handle-stance-changes(player=8)
