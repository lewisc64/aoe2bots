#template random-unit-selection-count-units-selected
  goal {goal} = 0
  @up-modify-goal {goal} g:+ produce-archers
  @up-modify-goal {goal} g:+ produce-hand-cannoneers
  @up-modify-goal {goal} g:+ produce-skirmishers
  @up-modify-goal {goal} g:+ produce-cavalry-archers
  @up-modify-goal {goal} g:+ produce-militia
  @up-modify-goal {goal} g:+ produce-spearmen
  @up-modify-goal {goal} g:+ produce-eagles
  @up-modify-goal {goal} g:+ produce-scouts
  @up-modify-goal {goal} g:+ produce-knights
  @up-modify-goal {goal} g:+ produce-camels
  @up-modify-goal {goal} g:+ produce-battle-elephants
  @up-modify-goal {goal} g:+ produce-mangonels
  @up-modify-goal {goal} g:+ produce-scorpions
  @up-modify-goal {goal} g:+ produce-unique-units
  @up-modify-goal {goal} g:+ produce-konniks
  @up-modify-goal {goal} g:+ produce-monks
  @up-modify-goal {goal} g:+ produce-genitours
  @up-modify-goal {goal} g:+ produce-condottiero
  @up-modify-goal {goal} g:+ produce-missionaries
  // ships do not count towards unit total.
#end template

insert random-unit-selection-count-units-selected(goal="total-units-selected")

#if up-compare-goal total-units-selected g:< desired-units-per-type
  #select random
    #if goal produce-cavalry-archers 0
      @up-modify-goal produce-archers g:= good-archers
    #end if
    
  #randor
    #if not goal total-units-selected 0
      @up-modify-goal produce-hand-cannoneers g:= good-hand-cannoneers
    #end if
    
  #randor
    @up-modify-goal produce-skirmishers g:= good-skirmishers
    
  #randor
    #if goal produce-archers 0
      @up-modify-goal produce-cavalry-archers g:= good-cavalry-archers
    #end if
    
  #randor
    #if goal produce-eagles 0
      @up-modify-goal produce-militia g:= good-militia
    #end if
    
  #randor
    #if goal produce-eagles 0
      @up-modify-goal produce-spearmen g:= good-spearmen
    #end if
    
  #randor
    #if goal produce-spearmen 0 and goal produce-militia 0
      @up-modify-goal produce-eagles g:= good-eagles
    #end if
    
  #randor
    #if goal produce-knights 0 and goal produce-battle-elephants 0
      @up-modify-goal produce-scouts g:= good-scouts
    #end if
    
  #randor
    #if goal produce-scouts 0 and goal produce-battle-elephants 0
      @up-modify-goal produce-knights g:= good-knights
    #end if
    
  #randor
    @up-modify-goal produce-camels g:= good-camels
    
  #randor
    #if goal produce-scouts 0 and goal produce-knights 0
      @up-modify-goal produce-battle-elephants g:= good-battle-elephants
    #end if
    
  #randor
    @up-modify-goal produce-mangonels g:= good-mangonels
    
  #randor
    @up-modify-goal produce-scorpions g:= good-scorpions
    
  #randor
    @up-modify-goal produce-unique-units g:= good-unique-units
    
  #randor
    @up-modify-goal produce-monks g:= good-monks
  
  #randor
    @up-modify-goal produce-genitours g:= good-genitours
    
  #randor
    #if not goal total-units-selected 0
      @up-modify-goal produce-condottiero g:= good-condottiero
    #end if
  
  #randor
    @up-modify-goal produce-missionaries g:= good-missionaries
  
  #randor
    @up-modify-goal produce-konniks g:= good-konniks
    
  #end select
#end if

#if goal produce-fire-ships 0 and goal produce-galleys 0
  #select random
    #if goal produce-galleys 0
      @up-modify-goal produce-galleys g:= good-galleys
    #end if
  #randor
    #if goal produce-fire-ships 0
      @up-modify-goal produce-fire-ships g:= good-fire-ships
    #end if
  #randor
    @up-modify-goal produce-galleys g:= good-galleys
    @up-modify-goal produce-fire-ships g:= good-fire-ships
  #end select
#end if
