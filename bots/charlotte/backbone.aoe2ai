const ri-yeomen = 3
const ri-el-dorado = 4
const ri-garland-wars = 24
const ri-beserkergang = 49
const ri-perfusion = 457
const ri-logistica = 61
const ri-mahouts = 7
const ri-sipahi = 491
const ri-rocketry = 52
const ri-chivalry = 493
const ri-bearded-axe = 83
const ri-shinkichon = 445
const ri-howdah = 626
const ri-manipur-cavalry = 627
const ri-furor-celtica = 5
const ri-tusk-swords = 622
const ri-double-crossbow = 623
const ri-mercenaries = 514
const ri-farimba = 577
const ri-chatras = 628
const ri-recurve-bow = 515
const ri-inquisition = 492
const ri-atlatl = 460
const ri-greek-fire = 464
const ri-andean-sling = 516
const ri-sultans = 506
const ri-pavise = 494
const ri-silk-road = 499
const ri-thalassocracy = 624
const ri-carrack = 572
const ri-shatagni = 507

const ri-elite-genitour = 599
const genitour-line = 1079

const condottiero = 184

const live-sheep = 958
const sea-fish = 905
const tc-arrow = 54
const watch-tower-arrow = 786
const castle-arrow = 746
const castle-arrow-chemistry = 747
const ranged-unit-class = 900
const infantry-unit-class = 906
const cavalry-unit-class = 912
const dead-trade-cart = 178
const dead-trade-cart-loaded = 205
const dead-male-builder = 213
const dead-female-builder = 225

const max-town-centers = 3
const dark-age-military-buildings = 1
const feudal-age-military-buildings = 3
const castle-age-military-buildings = 4
const imperial-age-military-buildings = 5
const min-detection-units = 5

#template all-land-unit-goal-names
  {before}villagers{after}
  {before}trade-carts{after}
  {before}archers{after}
  {before}hand-cannoneers{after}
  {before}skirmishers{after}
  {before}cavalry-archers{after}
  {before}militia{after}
  {before}spearmen{after}
  {before}eagles{after}
  {before}scouts{after}
  {before}knights{after}
  {before}camels{after}
  {before}battle-elephants{after}
  {before}battering-rams{after}
  {before}mangonels{after}
  {before}scorpions{after}
  {before}bombard-cannons{after}
  {before}trebuchets{after}
  {before}unique-units{after}
  {before}monks{after}
  {before}genitours{after}
  {before}condottiero{after}
  {before}missionaries{after}
#end template

#template all-naval-unit-goal-names
  {before}fishing-ships{after}
  {before}galleys{after}
  {before}fire-ships{after}
  {before}cannon-galleons{after}
  {before}transport-ships{after}
#end template

#template all-unit-goal-names
  insert all-land-unit-goal-names(before="{before}",after="{after}")
  insert all-naval-unit-goal-names(before="{before}",after="{after}")
#end template

#template clear-unit-production-goals
  insert all-unit-goal-names(before="goal produce-",after=" = 0")
#end template

#template clear-good-units
  insert all-unit-goal-names(before="goal {prefix}good-",after=" = 0")
#end template

#template divert-to-stone
  #nomerge
    #if strategic-number sn-stone-gatherer-percentage < {percent}
      distribute 1 villagers from wood to stone
    #end if
    #if strategic-number sn-stone-gatherer-percentage < {percent}
      distribute 1 villagers from food to stone
    #end if
    #if strategic-number sn-stone-gatherer-percentage < {percent}
      distribute 1 villagers from gold to stone
    #end if
  #end nomerge
#end template

#template divert-all-from-stone
  #nomerge
    #if strategic-number sn-stone-gatherer-percentage > 0
      distribute 1 villagers from stone to wood
    #end if
    #if strategic-number sn-stone-gatherer-percentage > 0
      distribute 1 villagers from stone to food
    #end if
    #if strategic-number sn-stone-gatherer-percentage > 0
      distribute 1 villagers from stone to gold
    #end if
  #end nomerge
#end template

#template ensure-castles
  #if {condition}
    build castle
  
    goal do-stone-mining = 1
    
    #if gold-amount >= 300 and stone-amount < 650 and current-age >= castle-age
      build 1 market
      buy stone
    #end if
  #end if
#end template

#do once
  set up basics
  target walls
  
  enable walls on perimeter 1
  enable walls on perimeter 2
  
  goal resign = 0
  
  sn-maximum-attack-group-size = 1
  sn-minimum-attack-group-size = 1
  sn-maximum-boat-attack-group-size = 1
  sn-minimum-boat-attack-group-size = 1
  sn-garrison-rams = 0
	sn-group-form-distance = 30
  sn-special-attack-type2 = villager
  sn-minimum-water-body-size-for-dock = 999
  
  goal full-water-map = 0
  goal partial-water-map = 0
  
  #if cc-players-unit-type-count 0 sea-fish >= 5 and game-time < 5
    #if building-type-count stone-wall-line == 0 and building-type-count palisade-wall == 0 and wall-completed-percentage 2 == 100
      goal full-water-map = 1
    #else
      goal partial-water-map = 1
    #end if
  #end if
  
  #if goal full-water-map 1 or goal partial-water-map 1
    goal water-map = 1
  #else
    goal water-map = 0
  #end if
  
  goal desired-military-buildings-per-type = 2
  goal desired-town-centers = max-town-centers
  
  goal desired-villagers = 120
  goal desired-trade-carts = 30
  goal desired-fishing-ships = 10
  
  #select random persistant
    goal desired-units-per-type = 1
  #randor
    goal desired-units-per-type = 2
  #end select
  
  // previous numbers assume pop cap of 200, normalise to actual pop cap.
  goal pop-cap-goal = 0
  @up-get-fact population-cap 0 pop-cap-goal
  @up-modify-goal desired-villagers g:* pop-cap-goal
  @up-modify-goal desired-villagers c:/ 200
  @up-modify-goal desired-fishing-ships g:* pop-cap-goal
  @up-modify-goal desired-fishing-ships c:/ 200
  
  goal desired-age = 0
  @up-modify-goal desired-age c:= imperial-age
  
  goal produce-units = 1
  goal allow-land-units = 1
  goal allow-naval-units = 1
  goal do-research = 1
  goal attacking = 0
  
  assign 8 builders to castle
  assign 4 builders to town-center-foundation
  
  goal temp = 0
#end do

@up-modify-goal desired-trade-carts g:= desired-villagers
@up-modify-goal desired-trade-carts s:* sn-gold-gatherer-percentage
@up-modify-goal desired-trade-carts c:/ 100

#select random persistant
  goal do-stone-mining = 1
#randor
  goal do-stone-mining = 0
#end select

#do once
  insert clear-unit-production-goals
  insert clear-good-units(prefix="")
#end do

load "chat_pack.aoe2ai"
load "stats.aoe2ai"

#do once
  goal team-game = 0
  goal free-for-all = 0
  goal locked-teams = 1
  @set-stance my-player-number enemy
#end do

#if stance-toward my-player-number enemy
  #do once
    goal locked-teams = 0
    @set-stance my-player-number ally
  #end do
#end if

#if goal free-for-all 0 and players-stance any-ally ally and stance-toward any-ally ally
  #do once
    goal team-game = 1
  #end do
#end if

#if goal team-game 0 and player-valid 1 and player-valid 2 and player-valid 3
  #do once
    goal free-for-all = 1
  #end do
#end if

#if current-age == dark-age and up-compare-goal desired-military-buildings-per-type c:< dark-age-military-buildings
  goal desired-military-buildings-per-type = dark-age-military-buildings
#else if current-age == feudal-age and up-compare-goal desired-military-buildings-per-type c:< feudal-age-military-buildings
  goal desired-military-buildings-per-type = feudal-age-military-buildings
#else if current-age == castle-age and up-compare-goal desired-military-buildings-per-type c:< castle-age-military-buildings
  goal desired-military-buildings-per-type = castle-age-military-buildings
#else if current-age == imperial-age and up-compare-goal desired-military-buildings-per-type c:< imperial-age-military-buildings
  goal desired-military-buildings-per-type = imperial-age-military-buildings
#end if

goal attempted-researches = 0
