//load "../research/town_center/castle_age.aoe2ai"

#if current-age >= castle-age
  #do once
    goal castle-count = 0
  #end do
  @up-get-fact building-type-count-total castle castle-count
  #if up-compare-goal castle-count g:< desired-military-buildings-per-type
    insert build-castles
  #end if
#end if

#if unit-available my-unique-unit-line

  #if current-age == imperial-age
    load "../research/castle/my_unique_unit_upgrade.aoe2ai"
  #end if
  
  #do once
    goal old-desired-military-buildings-per-type = 0
  #end do
  @up-modify-goal old-desired-military-buildings-per-type g:= desired-military-buildings-per-type
  goal desired-military-buildings-per-type = 0
  
  #if civ-selected aztec or civ-selected celtic or civ-selected ethiopian or civ-selected frankish or civ-selected gothic or civ-selected indian
    load "types/infantry.aoe2ai"
  #end if
  #if civ-selected incan or civ-selected japanese or civ-selected malay or civ-selected malian or civ-selected teutonic or civ-selected viking
    load "types/infantry.aoe2ai"
  #end if
  #if civ-selected berbers or civ-selected korean or civ-selected mongol or civ-selected burmese or civ-selected cumans
    load "types/cavalry_archer.aoe2ai"
  #end if
  #if civ-selected briton or civ-selected chinese or civ-selected italian or civ-selected mayan or civ-selected vietnamese
    load "types/ranged.aoe2ai"
  #end if
  #if civ-selected byzantine or civ-selected hun or civ-selected magyar or civ-selected persian or civ-selected saracen or civ-selected slavic or civ-selected lithuanians or civ-selected tatars
    load "types/cavalry.aoe2ai"
  #end if
  
  @up-modify-goal desired-military-buildings-per-type g:= old-desired-military-buildings-per-type
  
  #if civ-selected briton
    load "../research/castle/yeomen.aoe2ai"
  #end if
  
  #if civ-selected magyar
    load "../research/castle/corvinian_army.aoe2ai"
  #end if
  
  #if civ-selected italian
    load "../research/castle/pavise.aoe2ai"
  #end if
  
  #if civ-selected incan
    load "../research/castle/fabric_shields.aoe2ai"
  #end if
  
  #if civ-selected saracen
    load "../research/castle/zealotry.aoe2ai"
  #end if
  
  #if current-age == imperial-age
    #if civ-selected byzantine
      load "../research/castle/logistica.aoe2ai"
    #end if
    
    #if civ-selected persian
      load "../research/castle/mahouts.aoe2ai"
    #end if
    
    #if civ-selected chinese
      load "../research/castle/rocketry.aoe2ai"
    #end if
    
    #if civ-selected frankish
      load "../research/castle/bearded_axe.aoe2ai"
    #end if
    
    #if civ-selected khmer
      load "../research/castle/double_crossbow.aoe2ai"
    #end if
    
    #if civ-selected burmese
      load "../research/castle/manipur_cavalry.aoe2ai"
    #end if
    
    #if civ-selected portuguese
      load "../research/university/siege_engineers.aoe2ai"
    #end if
  #end if
  
  #if civ-selected khmer
    #if current-age == imperial-age
      load "../research/university/siege_engineers.aoe2ai"
      load "../research/university/chemistry.aoe2ai"
      load "../research/blacksmith/plate_barding.aoe2ai"
    #else
      load "../research/blacksmith/chain_barding.aoe2ai"
    #end if
    load "../research/university/ballistics.aoe2ai"
    load "../research/stable/husbandry.aoe2ai"
    load "../research/stable/bloodlines.aoe2ai"
    
  #end if
  
  #if civ-selected spanish
    #if current-age == imperial-age
      load "../research/blacksmith/ring_archer_armor.aoe2ai"
    #else
      load "../research/blacksmith/leather_archer_armor.aoe2ai"
    #end if
    load "../research/stable/husbandry.aoe2ai"
    load "../research/stable/bloodlines.aoe2ai"
  #end if
  
  #if civ-selected turkish
    #if current-age == imperial-age
      load "../research/blacksmith/ring_archer_armor.aoe2ai"
    #else
      load "../research/blacksmith/leather_archer_armor.aoe2ai"
    #end if
  #end if
  
  #if goal produce-units 1 and goal enough-wood-for-units 1 and goal enough-food-for-units 1 and goal enough-gold-for-units 1
    train my-unique-unit-line
  #end if
#end if