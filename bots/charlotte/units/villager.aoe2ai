load "../research/town_center/prerequisites.aoe2ai"

#if unit-available villager
  #if current-age >= feudal-age
    load "../research/town_center/loom.aoe2ai"
  #end if
  
  #if current-age == feudal-age
    load "../research/town_center/wheelbarrow.aoe2ai"
    load "../research/lumber_camp/double_bit_axe.aoe2ai"
    load "../research/mill/horse_collar.aoe2ai"
    #if resource-found gold
      load "../research/mining_camp/gold_mining.aoe2ai"
    #end if
  #else if current-age == castle-age
    load "../research/lumber_camp/bow_saw.aoe2ai"
    load "../research/mill/heavy_plow.aoe2ai"
  #else if current-age == imperial-age
    load "../research/town_center/hand_cart.aoe2ai"
    load "../research/lumber_camp/two_man_saw.aoe2ai"
    load "../research/mill/crop_rotation.aoe2ai"
  #end if
  
  #if current-age >= castle-age
    #if resource-found gold
      load "../research/mining_camp/gold_shaft_mining.aoe2ai"
    #end if
  #end if
  
  #do once
    goal desired-villagers-with-subtractions = 0
  #end do
  @up-modify-goal desired-villagers-with-subtractions g:= desired-villagers
  
  #if goal produce-trade-carts 1
    @up-modify-goal desired-villagers-with-subtractions g:- desired-trade-carts
  #end if
  
  #if goal produce-fishing-ships 1
    @up-modify-goal desired-villagers-with-subtractions g:- desired-fishing-ships
  #end if
  
  #if building-type-count feitoria >= 1
    #do once
      goal feitoria-pop-usage = 0
    #end do
    @up-get-fact building-type-count feitoria feitoria-pop-usage
    @up-modify-goal feitoria-pop-usage c:* 20
    @up-modify-goal desired-villagers-with-subtractions g:- feitoria-pop-usage
  #end if

  #if up-compare-goal total-villagers g:< desired-villagers-with-subtractions
    train villager
  #else if up-compare-goal total-villagers g:> desired-villagers-with-subtractions and up-compare-goal population-space-remaining c:<= 10
    delete unit villager
  #end if
  
  #if (game-time >= 60 or civ-selected mayan or civ-selected chinese) and up-pending-objects c: villager == 0
    load "../research/town_center/loom.aoe2ai"
  #end if
#end if
