load "villager.aoe2ai"

#if goal produce-trade-carts 1
  load "trade_cart.aoe2ai"
#end if

#if goal produce-fishing-ships 1
  load "fishing_ship.aoe2ai"
#end if

#do once
  goal old-produce-units = 0
  goal current-unit-amount = 0
  goal expected-units-per-type = 0
#end do

goal total-type-production = 0

#template add-to-total-type-production
  #if unit-available {unit}
    @up-modify-goal total-type-production g:+ {goal}
  #end if
#end template

#if goal allow-land-units 1
  insert add-to-total-type-production(unit="archer-line",goal="produce-archers")
  insert add-to-total-type-production(unit="hand-cannoneer",goal="produce-hand-cannoneers")
  insert add-to-total-type-production(unit="skirmisher-line",goal="produce-skirmishers")
  insert add-to-total-type-production(unit="genitour-line",goal="produce-genitours")
  insert add-to-total-type-production(unit="cavalry-archer-line",goal="produce-cavalry-archers")
  
  insert add-to-total-type-production(unit="militiaman-line",goal="produce-militia")
  insert add-to-total-type-production(unit="condottiero",goal="produce-condottiero")
  insert add-to-total-type-production(unit="spearman-line",goal="produce-spearmen")
  insert add-to-total-type-production(unit="eagle-warrior-line",goal="produce-eagles")
  
  insert add-to-total-type-production(unit="scout-cavalry-line",goal="produce-scouts")
  insert add-to-total-type-production(unit="knight-line",goal="produce-knights")
  insert add-to-total-type-production(unit="camel-line",goal="produce-camels")
  insert add-to-total-type-production(unit="battle-elephant-line",goal="produce-battle-elephants")
  
  insert add-to-total-type-production(unit="battering-ram-line",goal="produce-battering-rams")
  insert add-to-total-type-production(unit="armored-elephant-line",goal="produce-armored-elephants")
  insert add-to-total-type-production(unit="mangonel-line",goal="produce-mangonels")
  insert add-to-total-type-production(unit="scorpion-line",goal="produce-scorpions")
  insert add-to-total-type-production(unit="bombard-cannon",goal="produce-bombard-cannons")
  
  insert add-to-total-type-production(unit="trebuchet",goal="produce-trebuchets")
  insert add-to-total-type-production(unit="my-unique-unit-line",goal="produce-unique-units")
  insert add-to-total-type-production(unit="konnik-set",goal="produce-konniks")
  
  insert add-to-total-type-production(unit="monk",goal="produce-monks")
  insert add-to-total-type-production(unit="missionary",goal="produce-missionaries")
#end if

#if goal allow-naval-units 1
  insert add-to-total-type-production(unit="transport-ship",goal="produce-transport-ships")
  insert add-to-total-type-production(unit="galley-line",goal="produce-galleys")
  insert add-to-total-type-production(unit="fire-ship-line",goal="produce-fire-ships")
  insert add-to-total-type-production(unit="cannon-galleon-line",goal="produce-cannon-galleons")
  insert add-to-total-type-production(unit="demolition-ship-line",goal="produce-demolition-ships")
#end if

@up-get-fact military-population 0 expected-units-per-type
@up-modify-goal expected-units-per-type g:/ total-type-production

//@up-chat-data-to-all "total type production: %d" g: total-type-production
//@up-chat-data-to-all "expected units per type: %d" g: expected-units-per-type

load "prerequisites.aoe2ai"

#template load-unit
  #if goal {production_goal} 1 and unit-available {unit}
    @up-modify-goal old-produce-units g:= produce-units
    
    @up-get-fact unit-type-count {unit} current-unit-amount
    #if up-compare-goal current-unit-amount g:> expected-units-per-type and military-population >= 5 or goal throttle-units 1 and up-pending-objects c: {unit} >= 1
      goal produce-units = 0
    #end if
    
    load "{unit_path}"
    @up-modify-goal produce-units g:= old-produce-units
  #end if
#end template

#if goal allow-land-units 1
  insert load-unit(production_goal="produce-archers",unit="archer-line",unit_path="archer.aoe2ai")
  insert load-unit(production_goal="produce-hand-cannoneers",unit="hand-cannoneer",unit_path="hand_cannoneer.aoe2ai")
  insert load-unit(production_goal="produce-skirmishers",unit="skirmisher-line",unit_path="skirmisher.aoe2ai")
  insert load-unit(production_goal="produce-genitours",unit="genitour-line",unit_path="genitour.aoe2ai")
  insert load-unit(production_goal="produce-cavalry-archers",unit="cavalry-archer-line",unit_path="cavalry_archer.aoe2ai")

  insert load-unit(production_goal="produce-militia",unit="militiaman-line",unit_path="militia.aoe2ai")
  insert load-unit(production_goal="produce-condottiero",unit="condottiero",unit_path="condottiero.aoe2ai")
  insert load-unit(production_goal="produce-spearmen",unit="spearman-line",unit_path="spearman.aoe2ai")
  insert load-unit(production_goal="produce-eagles",unit="eagle-warrior-line",unit_path="eagle_warrior.aoe2ai")

  insert load-unit(production_goal="produce-scouts",unit="scout-cavalry-line",unit_path="scout_cavalry.aoe2ai")
  insert load-unit(production_goal="produce-knights",unit="knight-line",unit_path="knight.aoe2ai")
  insert load-unit(production_goal="produce-camels",unit="camel-line",unit_path="camel.aoe2ai")
  insert load-unit(production_goal="produce-battle-elephants",unit="battle-elephant-line",unit_path="battle_elephant.aoe2ai")

  insert load-unit(production_goal="produce-battering-rams",unit="battering-ram-line",unit_path="battering_ram.aoe2ai")
  insert load-unit(production_goal="produce-armored-elephants",unit="armored-elephant-line",unit_path="armored_elephant.aoe2ai")
  insert load-unit(production_goal="produce-mangonels",unit="mangonel-line",unit_path="mangonel.aoe2ai")
  insert load-unit(production_goal="produce-scorpions",unit="scorpion-line",unit_path="scorpion.aoe2ai")
  insert load-unit(production_goal="produce-bombard-cannons",unit="bombard-cannon",unit_path="bombard_cannon.aoe2ai")

  insert load-unit(production_goal="produce-trebuchets",unit="trebuchet-set",unit_path="trebuchet.aoe2ai")
  insert load-unit(production_goal="produce-unique-units",unit="my-unique-unit-line",unit_path="unique_unit.aoe2ai")
  insert load-unit(production_goal="produce-konniks",unit="konnik-set",unit_path="konnik.aoe2ai")

  insert load-unit(production_goal="produce-monks",unit="monk-set",unit_path="monk.aoe2ai")
  insert load-unit(production_goal="produce-missionaries",unit="missionary",unit_path="missionary.aoe2ai")
#end if

#if goal allow-naval-units 1
  insert load-unit(production_goal="produce-transport-ships",unit="transport-ship",unit_path="transport_ship.aoe2ai")
  insert load-unit(production_goal="produce-galleys",unit="galley-line",unit_path="galley.aoe2ai")
  insert load-unit(production_goal="produce-fire-ships",unit="fire-ship-line",unit_path="fire_ship.aoe2ai")
  insert load-unit(production_goal="produce-cannon-galleons",unit="cannon-galleon-line",unit_path="cannon_galleon.aoe2ai")
  insert load-unit(production_goal="produce-demolition-ships",unit="demolition-ship-line",unit_path="demolition_ship.aoe2ai")
#end if
