load "villager.aoe2ai"
#if goal produce-trade-carts 1
  load "trade_cart.aoe2ai"
#end if
#if goal water-map 1
  load "fishing_ship.aoe2ai"
#end if

#do once
  goal old-produce-units = 0
  goal trained-unit = 0 // unused
  goal current-unit-amount = 0
  goal expected-units-per-type = 0
#end do

goal total-type-production = 0

#if goal enough-wood-for-units 1 and goal enough-gold-for-units 1
  @up-modify-goal total-type-production g:+ produce-archers
  @up-modify-goal total-type-production g:+ produce-cavalry-archers
  @up-modify-goal total-type-production g:+ produce-battering-rams
  @up-modify-goal total-type-production g:+ produce-mangonels
  @up-modify-goal total-type-production g:+ produce-scorpions
  @up-modify-goal total-type-production g:+ produce-bombard-cannons
  @up-modify-goal total-type-production g:+ produce-trebuchets
  @up-modify-goal total-type-production g:+ produce-galleys
  @up-modify-goal total-type-production g:+ produce-fire-ships
  @up-modify-goal total-type-production g:+ produce-cannon-galleons
#end if

#if goal enough-food-for-units 1 and goal enough-gold-for-units 1
  #if unit-available hand-cannoneer
    @up-modify-goal total-type-production g:+ produce-hand-cannoneers
  #end if
  @up-modify-goal total-type-production g:+ produce-militia
  @up-modify-goal total-type-production g:+ produce-eagles
  @up-modify-goal total-type-production g:+ produce-knights
  @up-modify-goal total-type-production g:+ produce-camels
  @up-modify-goal total-type-production g:+ produce-battle-elephants
#end if

#if goal enough-food-for-units 1
  @up-modify-goal total-type-production g:+ produce-scouts
#end if

#if goal enough-wood-for-units 1 and goal enough-food-for-units 1
  @up-modify-goal total-type-production g:+ produce-skirmishers
  @up-modify-goal total-type-production g:+ produce-spearmen
#end if

#if goal enough-wood-for-units 1 and goal enough-food-for-units 1 and goal enough-gold-for-units 1 and (building-type-count castle >= 2 or goal desired-units 1)
  @up-modify-goal total-type-production g:+ produce-unique-units
#end if

#if goal enough-gold-for-units 1
  @up-modify-goal total-type-production g:+ produce-monks
#end if

@up-get-fact military-population 0 expected-units-per-type
@up-modify-goal expected-units-per-type g:/ total-type-production

load "prerequisites.aoe2ai"

#subroutine load-unit
  #if goal {production_goal} 1 and unit-available {unit}
    @up-modify-goal old-produce-units g:= produce-units
    
    @up-get-fact unit-type-count {unit} current-unit-amount
    #if up-compare-goal current-unit-amount g:> expected-units-per-type
      goal produce-units = 0
    #end if
    
    load "{unit_path}"
    @up-modify-goal produce-units g:= old-produce-units
  #end if
#end subroutine

call load-unit(production_goal="produce-archers",unit="archer-line",unit_path="archer.aoe2ai")
call load-unit(production_goal="produce-hand-cannoneers",unit="hand-cannoneer",unit_path="hand_cannoneer.aoe2ai")
call load-unit(production_goal="produce-skirmishers",unit="skirmisher-line",unit_path="skirmisher.aoe2ai")
call load-unit(production_goal="produce-cavalry-archers",unit="cavalry-archer-line",unit_path="cavalry_archer.aoe2ai")

call load-unit(production_goal="produce-militia",unit="militiaman-line",unit_path="militia.aoe2ai")
call load-unit(production_goal="produce-spearmen",unit="spearman-line",unit_path="spearman.aoe2ai")
call load-unit(production_goal="produce-eagles",unit="eagle-warrior-line",unit_path="eagle_warrior.aoe2ai")

call load-unit(production_goal="produce-scouts",unit="scout-cavalry-line",unit_path="scout_cavalry.aoe2ai")
call load-unit(production_goal="produce-knights",unit="knight-line",unit_path="knight.aoe2ai")
call load-unit(production_goal="produce-camels",unit="camel-line",unit_path="camel.aoe2ai")
call load-unit(production_goal="produce-battle-elephants",unit="battle-elephant-line",unit_path="battle_elephant.aoe2ai")

call load-unit(production_goal="produce-battering-rams",unit="battering-ram-line",unit_path="battering_ram.aoe2ai")
call load-unit(production_goal="produce-mangonels",unit="mangonel-line",unit_path="mangonel.aoe2ai")
call load-unit(production_goal="produce-scorpions",unit="scorpion-line",unit_path="scorpion.aoe2ai")
call load-unit(production_goal="produce-bombard-cannons",unit="bombard-cannon",unit_path="bombard_cannon.aoe2ai")

call load-unit(production_goal="produce-trebuchets",unit="trebuchet-set",unit_path="trebuchet.aoe2ai")
call load-unit(production_goal="produce-unique-units",unit="my-unique-unit-line",unit_path="unique_unit.aoe2ai")

call load-unit(production_goal="produce-monks",unit="monk-set",unit_path="monk.aoe2ai")

call load-unit(production_goal="produce-galleys",unit="galley-line",unit_path="galley.aoe2ai")
call load-unit(production_goal="produce-fire-ships",unit="fire-ship-line",unit_path="fire_ship.aoe2ai")
call load-unit(production_goal="produce-cannon-galleons",unit="cannon-galleon-line",unit_path="cannon_galleon.aoe2ai")
