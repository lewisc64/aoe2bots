// resource values

#do once
  goal escrowed-wood = 0
  goal escrowed-food = 0
  goal escrowed-gold = 0
  goal escrowed-stone = 0
  goal non-escrowed-wood = 0
  goal non-escrowed-food = 0
  goal non-escrowed-gold = 0
  goal non-escrowed-stone = 0
#end do

@up-get-fact escrow-amount wood escrowed-wood
@up-get-fact escrow-amount food escrowed-food
@up-get-fact escrow-amount gold escrowed-gold
@up-get-fact escrow-amount stone escrowed-stone

@up-get-fact resource-amount wood non-escrowed-wood
@up-get-fact resource-amount food non-escrowed-food
@up-get-fact resource-amount gold non-escrowed-gold
@up-get-fact resource-amount stone non-escrowed-stone

@up-modify-goal non-escrowed-wood g:- escrowed-wood
@up-modify-goal non-escrowed-food g:- escrowed-food
@up-modify-goal non-escrowed-gold g:- escrowed-gold
@up-modify-goal non-escrowed-stone g:- escrowed-stone

#do once
  goal game-time-goal = 0
#end do
@up-get-fact game-time 0 game-time-goal

// time spent not mining gold

#do once
  goal no-gold-mining-time = 0
  goal no-gold-mining-start = -1
#end do

#if unit-type-count 579 == 0 and unit-type-count 581 == 0
  #if goal no-gold-mining-start -1
    @up-get-fact game-time 0 no-gold-mining-start
  #end if
#else
  goal no-gold-mining-start = -1
#end if

#if goal no-gold-mining-start -1
  goal no-gold-mining-time = 0
#else
  @up-get-fact game-time 0 no-gold-mining-time
  @up-modify-goal no-gold-mining-time g:- no-gold-mining-start
#end if

// population

#do once
  goal population-space-remaining = 0
  goal current-population-goal = 0
#end do

@up-get-fact population-cap 0 population-space-remaining
@up-get-fact population 0 current-population-goal
@up-modify-goal population-space-remaining g:- current-population-goal

// gatherer numbers



// counts as goals

#do once
  goal total-villagers = 0
#end do
@up-get-fact unit-type-count villager total-villagers

#do once
  goal total-trade-carts = 0
#end do
@up-get-fact unit-type-count trade-cart total-trade-carts

#do once
  goal total-fishing-ships = 0
#end do
@up-get-fact unit-type-count fishing-ship total-fishing-ships

#do once
  goal town-center-count = 0
#end do
@up-get-fact building-type-count-total town-center town-center-count

// scores

#do once
  goal my-score = 0
  goal highest-enemy-score = 0
  goal lowest-enemy-score = 0
#end do

@up-get-fact current-score 0 my-score
@up-get-fact-max any-enemy current-score 0 highest-enemy-score
@up-get-fact-min any-enemy current-score 0 lowest-enemy-score

#do once
  goal score-percentage = 0
#end do
@up-modify-goal score-percentage g:= my-score
@up-modify-goal score-percentage c:* 100
@up-modify-goal score-percentage g:/ highest-enemy-score

#do once
  goal previous-target-player-score = 0
  goal target-player-score = 0
#end do
@up-get-target-fact current-score 0 target-player-score
goal target-player-score-delta = 0
@up-modify-goal target-player-score-delta g:= target-player-score
@up-modify-goal target-player-score-delta g:- previous-target-player-score
@up-modify-goal previous-target-player-score g:= target-player-score

// enemy populations

#template get-enemy-population-stats
  #do once
    goal my-{fact} = 0
    goal target-{fact} = 0
    goal target-{type}-percentage = 0
    goal largest-enemy-{fact} = 0
    goal largest-neutral-{fact} = 0 // consider neutrals as enemies
    goal largest-enemy-{type}-percentage = 0
  #end do

  @up-get-fact {fact} 0 my-{fact}

  @up-get-target-fact {fact} 0 target-{fact}
  @up-get-fact-max any-enemy {fact} 0 largest-enemy-{fact}
  @up-get-fact-max any-neutral {fact} 0 largest-neutral-{fact}
  @up-modify-goal target-{type}-percentage g:= my-{fact}
  @up-modify-goal target-{type}-percentage c:* 100
  @up-modify-goal largest-enemy-{type}-percentage g:= target-{type}-percentage
  @up-modify-goal target-{type}-percentage g:/ target-{fact}
  #if up-compare-goal largest-neutral-{fact} g:> largest-enemy-{fact}
    @up-modify-goal largest-enemy-{type}-percentage g:/ largest-neutral-{fact}
  #else
    @up-modify-goal largest-enemy-{type}-percentage g:/ largest-enemy-{fact}
  #end if
#end template

insert get-enemy-population-stats(type="military",fact="military-population")
insert get-enemy-population-stats(type="civilian",fact="civilian-population")
insert get-enemy-population-stats(type="warboat",fact="warboat-count")

// threat data

#do once
  goal threat-time = 0
  goal threat-player = 0
  goal threat-source = 0
  goal threat-target = 0
#end do
@up-get-threat-data threat-time threat-player threat-source threat-target

#if town-under-attack and strategic-number sn-maximum-town-size <= 30
  goal under-attack = 1
#else
  goal under-attack = 0
#end if

// treaty data

#do once
  goal treaty-time-left = 0
#end do

@up-get-treaty-data treaty-time-left

// unit class investment

goal ranged-blacksmith-techs-done = 0
goal cavalry-blacksmith-techs-done = 0
goal infantry-blacksmith-techs-done = 0

#if research-completed ri-fletching
  goal ranged-blacksmith-techs-done += 1
#end if

#if research-completed ri-bodkin-arrow
  goal ranged-blacksmith-techs-done += 1
#end if

#if research-completed ri-bracer
  goal ranged-blacksmith-techs-done += 1
#end if

#if research-completed ri-padded-archer-armor
  goal ranged-blacksmith-techs-done += 1
#end if

#if research-completed ri-leather-archer-armor
  goal ranged-blacksmith-techs-done += 1
#end if

#if research-completed ri-ring-archer-armor
  goal ranged-blacksmith-techs-done += 1
#end if

#if research-completed ri-forging
  goal infantry-blacksmith-techs-done += 1
  goal cavalry-blacksmith-techs-done += 1
#end if

#if research-completed ri-iron-casting
  goal infantry-blacksmith-techs-done += 1
  goal cavalry-blacksmith-techs-done += 1
#end if

#if research-completed ri-blast-furnace
  goal infantry-blacksmith-techs-done += 1
  goal cavalry-blacksmith-techs-done += 1
#end if

#if research-completed ri-scale-barding
  goal cavalry-blacksmith-techs-done += 1
#end if

#if research-completed ri-chain-barding
  goal cavalry-blacksmith-techs-done += 1
#end if

#if research-completed ri-plate-barding
  goal cavalry-blacksmith-techs-done += 1
#end if

#if research-completed ri-scale-mail
  goal infantry-blacksmith-techs-done += 1
#end if

#if research-completed ri-chain-mail
  goal infantry-blacksmith-techs-done += 1
#end if

#if research-completed ri-plate-mail
  goal infantry-blacksmith-techs-done += 1
#end if

goal partial-ranged-blacksmith-investment = 0
goal full-ranged-blacksmith-investment = 0
goal partial-cavalry-blacksmith-investment = 0
goal full-cavalry-blacksmith-investment = 0
goal partial-infantry-blacksmith-investment = 0
goal full-infantry-blacksmith-investment = 0

#template decide-blacksmith-investment
  #if current-age == feudal-age
    #if goal {type}-blacksmith-techs-done 1
      goal partial-{type}-blacksmith-investment = 1
    #else if goal {type}-blacksmith-techs-done 2
      goal partial-{type}-blacksmith-investment = 1
      goal full-{type}-blacksmith-investment = 1
    #end if
  #else if current-age == castle-age
    #if goal {type}-blacksmith-techs-done 3
      goal partial-{type}-blacksmith-investment = 1
    #else if goal {type}-blacksmith-techs-done 4
      goal partial-{type}-blacksmith-investment = 1
      goal full-{type}-blacksmith-investment = 1
    #end if
  #else if current-age == imperial-age
    #if goal {type}-blacksmith-techs-done 5
      goal partial-{type}-blacksmith-investment = 1
    #else if goal {type}-blacksmith-techs-done 6
      goal partial-{type}-blacksmith-investment = 1
      goal full-{type}-blacksmith-investment = 1
    #end if
  #end if
#end template

insert decide-blacksmith-investment(type="ranged")
insert decide-blacksmith-investment(type="cavalry")
insert decide-blacksmith-investment(type="infantry")

// unique unit information

#template get-unique-unit-information

  goal {prefix}unique-unit-is-cavalry = 0
  goal {prefix}unique-unit-is-infantry = 0
  goal {prefix}unique-unit-is-ranged = 0
  goal {prefix}unique-unit-is-cavalry-archer = 0
  goal {prefix}unique-unit-is-expensive = 0
  
  goal {prefix}unique-unit-counters-cavalry = 0
  goal {prefix}unique-unit-counters-infantry = 0
  goal {prefix}unique-unit-counters-ranged = 0
  goal {prefix}unique-unit-counters-cavalry-archer = 0
  goal {prefix}unique-unit-counters-unique-unit = 0

  #if players-civ {player} armenians
    goal {prefix}unique-unit-is-ranged = 1
  #end if
  #if players-civ {player} aztec
    goal {prefix}unique-unit-is-infantry = 1
    goal {prefix}unique-unit-counters-infantry = 1
  #end if
  #if players-civ {player} bengalis
    goal {prefix}unique-unit-is-cavalry-archer = 1
    goal {prefix}unique-unit-counters-infantry = 1
  #end if
  #if players-civ {player} berbers
    goal {prefix}unique-unit-is-cavalry-archer = 1
    goal {prefix}unique-unit-counters-cavalry-archer = 1
  #end if
  #if players-civ {player} bohemians
    goal {prefix}unique-unit-is-expensive = 1
    goal {prefix}unique-unit-counters-ranged = 1
  #end if
  #if players-civ {player} briton
    goal {prefix}unique-unit-is-ranged = 1
  #end if
  #if players-civ {player} burgundians
    goal {prefix}unique-unit-is-cavalry = 1
    goal {prefix}unique-unit-counters-infantry = 1
    goal {prefix}unique-unit-counters-ranged = 1
  #end if
  #if players-civ {player} burmese
    goal {prefix}unique-unit-is-cavalry-archer = 1
  #end if
  #if players-civ {player} bulgarians
    goal {prefix}unique-unit-is-cavalry = 1
    goal {prefix}unique-unit-is-infantry = 1
    goal {prefix}unique-unit-is-expensive = 1
  #end if
  #if players-civ {player} byzantine
    goal {prefix}unique-unit-is-cavalry = 1
    goal {prefix}unique-unit-counters-infantry = 1
  #end if
  #if players-civ {player} celtic
    goal {prefix}unique-unit-is-infantry = 1
  #end if
  #if players-civ {player} chinese
    goal {prefix}unique-unit-is-ranged = 1
  #end if
  #if players-civ {player} cumans
    goal {prefix}unique-unit-is-cavalry-archer = 1
  #end if
  #if players-civ {player} dravidians
    goal {prefix}unique-unit-is-infantry = 1
    goal {prefix}unique-unit-counters-infantry = 1
  #end if
  #if players-civ {player} ethiopian
    goal {prefix}unique-unit-is-infantry = 1
  #end if
  #if players-civ {player} frankish
    goal {prefix}unique-unit-is-infantry = 1
  #end if
  #if players-civ {player} georgians
    goal {prefix}unique-unit-is-cavalry = 1
  #end if
  #if players-civ {player} gothic
    goal {prefix}unique-unit-is-infantry = 1
  #end if
  #if players-civ {player} gurjaras
    goal {prefix}unique-unit-is-infantry = 1
    goal {prefix}unique-unit-counters-infantry = 1
  #end if
  #if players-civ {player} hun
    goal {prefix}unique-unit-is-cavalry = 1
    goal {prefix}unique-unit-is-expensive = 1
  #end if
  #if players-civ {player} incan
    goal {prefix}unique-unit-is-infantry = 1
    goal {prefix}unique-unit-counters-cavalry = 1
  #end if
  #if players-civ {player} indian
    goal {prefix}unique-unit-is-cavalry-archer = 1
  #end if
  #if players-civ {player} italian
    goal {prefix}unique-unit-is-ranged = 1
    goal {prefix}unique-unit-counters-cavalry = 1
  #end if
  #if players-civ {player} japanese
    goal {prefix}unique-unit-is-infantry = 1
    goal {prefix}unique-unit-counters-unique-unit = 1
  #end if
  #if players-civ {player} khmer
    goal {prefix}unique-unit-is-cavalry = 1
    goal {prefix}unique-unit-counters-infantry = 1
  #end if
  #if players-civ {player} korean
    goal {prefix}unique-unit-is-cavalry-archer = 1
    goal {prefix}unique-unit-counters-ranged = 1
  #end if
  #if players-civ {player} lithuanians
    goal {prefix}unique-unit-is-cavalry = 1
  #end if
  #if players-civ {player} magyar
    goal {prefix}unique-unit-is-cavalry = 1
  #end if
  #if players-civ {player} malay
    goal {prefix}unique-unit-is-infantry = 1
  #end if
  #if players-civ {player} malian
    goal {prefix}unique-unit-is-infantry = 1
  #end if
  #if players-civ {player} mayan
    goal {prefix}unique-unit-is-ranged = 1
  #end if
  #if players-civ {player} mongol
    goal {prefix}unique-unit-is-cavalry-archer = 1
  #end if
  #if players-civ {player} persian
    goal {prefix}unique-unit-is-cavalry = 1
    goal {prefix}unique-unit-is-expensive = 1
  #end if
  #if players-civ {player} poles
    goal {prefix}unique-unit-is-infantry = 1
  #end if
  #if players-civ {player} portuguese
    goal {prefix}unique-unit-is-expensive = 1
    goal {prefix}unique-unit-counters-infantry = 1
  #end if
  #if players-civ {player} romans
    goal {prefix}unique-unit-is-cavalry = 1
    goal {prefix}unique-unit-is-expensive = 1
    goal {prefix}unique-unit-counters-infantry = 1
  #end if
  #if players-civ {player} saracen
    goal {prefix}unique-unit-is-cavalry = 1
    goal {prefix}unique-unit-counters-cavalry = 1
  #end if
  #if players-civ {player} sicilians
    goal {prefix}unique-unit-is-infantry = 1
  #end if
  #if players-civ {player} slavic
    goal {prefix}unique-unit-is-cavalry = 1
    goal {prefix}unique-unit-is-expensive = 1
  #end if
  #if players-civ {player} spanish
    goal {prefix}unique-unit-is-cavalry-archer = 1
    goal {prefix}unique-unit-is-expensive = 1
  #end if
  #if players-civ {player} tatars
    goal {prefix}unique-unit-is-cavalry = 1
  #end if
  #if players-civ {player} teutonic
    goal {prefix}unique-unit-is-infantry = 1
    goal {prefix}unique-unit-is-expensive = 1
    goal {prefix}unique-unit-counters-infantry = 1
  #end if
  #if players-civ {player} turkish
    goal {prefix}unique-unit-is-ranged = 1
    goal {prefix}unique-unit-counters-infantry = 1
  #end if
  #if players-civ {player} vietnamese
    goal {prefix}unique-unit-is-ranged = 1
    goal {prefix}unique-unit-counters-ranged = 1
  #end if
  #if players-civ {player} viking
    goal {prefix}unique-unit-is-infantry = 1
  #end if
#end template

#do once
  insert get-unique-unit-information(prefix="",player="my-player-number")
#end do

// player type counts

#template add-to-player-count
  #if player-in-game {player}
    #if stance-toward {player} ally
      goal ally-players-count += 1
    #else if stance-toward {player} neutral
      goal neutral-players-count += 1
    #else if stance-toward {player} enemy
      goal enemy-players-count += 1
    #end if
  #end if
#end template

goal ally-players-count = 0
goal neutral-players-count = 0
goal enemy-players-count = 0

insert add-to-player-count(player=1)
insert add-to-player-count(player=2)
insert add-to-player-count(player=3)
insert add-to-player-count(player=4)
insert add-to-player-count(player=5)
insert add-to-player-count(player=6)
insert add-to-player-count(player=7)
insert add-to-player-count(player=8)

#if stance-toward my-player-number ally
  goal ally-players-count -= 1
#else if stance-toward my-player-number neutral
  goal neutral-players-count -= 1
#else if stance-toward my-player-number enemy
  goal enemy-players-count -= 1
#end if

goal neutral-or-enemy-players-count = 0
@up-modify-goal neutral-or-enemy-players-count g:+ enemy-players-count
@up-modify-goal neutral-or-enemy-players-count g:+ neutral-players-count
