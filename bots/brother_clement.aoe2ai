const ri-herbal-medicine = 441
const ri-inquisition = 492
const ri-grand-trunk-road = 506

#do once
  set name to "Brother Clement"
#end do

#template diplo-chat-alliance-offer
  @up-get-player-color {player} -1
  #select random
    chat const to all "%s, join me in worship." 7031232
  #randor
    chat const to all "An alliance, %s?" 7031232
  #randor
    chat const to all "Shall we join forces in the name of God, %s?" 7031232
  #randor
    chat const to all "We will be stronger together, %s." 7031232
  #end select
#end template

#template diplo-chat-our-betrayal
  @up-get-player-color {player} -1
  #select random
    chat const to all "Your demise is but a part of God's plan, %s." 7031232
  #randor
    chat const to all "%s. I find you in contempt of the Lord. Die." 7031232
  #randor
    chat const to all "%s you are a sinner, and will now perish." 7031232
  #end select
#end template

#template diplo-chat-their-betrayal
  @up-get-player-color {player} -1
  #select random
    chat const to all "%s? Did you dislike the peace?" 7031232
  #randor
    chat const to all "Unfortunate. I wished for nothing but peace, %s." 7031232
  #randor
    chat const to all "I should have known. I was blinded by love." 7031232
  #randor
    chat const to all "I will miss our friendship, %s." 7031232
  #end select
#end template

#template diplo-chat-accepted-our-alliance
  @up-get-player-color {player} -1
  #select random
    chat const to all "An excellent choice, %s." 7031232
  #randor
    chat const to all "Wonderful. Let us study together %s." 7031232
  #randor
    chat const to all "Excellent, %s. A blessed day." 7031232
  #randor
    chat const to all "Most wise, %s." 7031232
  #end select
#end template

#template diplo-chat-accepted-their-alliance
  @up-get-player-color {player} -1
  #select random
    chat const to all "Of course %s, let us have peace." 7031232
  #randor
    chat const to all "Your letter was most moving, %s. I accept." 7031232
  #randor
    chat const to all "%s? Hm. I suppose friendship knows no bounds." 7031232
  #end select
#end template

#template diplo-chat-refused-our-alliance
  @up-get-player-color {player} -1
  #select random
    chat const to all "Your desire for bloodshed is sickening, %s." 7031232
  #randor
    chat const to all "Alas, we are doomed to be enemies %s." 7031232
  #randor
    chat const to all "Reconsider my offer soon, %s." 7031232
  #end select
#end template

#template diplo-chat-refused-their-alliance
  @up-get-player-color {player} -1
  #select random
    chat const to all "You are sinful, %s. We cannot be friends." 7031232
  #randor
    chat const to all "I would sooner burn than ally with you, %s." 7031232
  #randor
    chat const to all "%s, I believe I have made my intentions clear." 7031232
  #randor
    chat const to all "We are beyond that point now, %s." 7031232
  #end select
#end template

#template diplo-chat-friendly-fire-warning
  @up-get-player-color {player} -1
  #select random
    chat const to all "Please %s, do not do that again." 7031232
  #randor
    chat const to all "That was a blasphemous action, %s. No more." 7031232
  #randor
    chat const to all "Don't you dare do that again, %s." 7031232
  #end select
#end template

#template diplo-chat-friendly-fire-betrayal
  @up-get-player-color {player} -1
  #select random
    chat const to all "You dare defy the will of God, %s?!" 7031232
  #randor
    chat const to all "How dare you!" 7031232
  #randor
    chat const to all "Heretical scum!" 7031232
  #randor
    chat const to all "That was the last straw!" 7031232
  #randor
    chat const to all "We are done, %s!" 7031232
  #end select
#end template

#template diplo-chat-resign
  chat to all "The abbey is destroyed. I have failed."
#end template

#template chat-heretic
  @up-get-player-color {player} -1
  #select random
    chat const to all "%s, you have fallen from grace." 7031232
  #randor
    chat const to all "A shame, %s. Greed has become you." 7031232
  #randor
    chat const to all "You bloat like a pig, %s." 7031232
  #randor
    chat const to all "%s you are a lecherous animal." 7031232
  #end select
  chat to {player} "5Donate and be cleansed of sin."
  goal chat-heretic = -1
#end template

#template chat-forgive
  @up-get-player-color {player} -1
  #select random
    chat const to all "Thank you for your donation %s. The Lord is pleased." 7031232
  #randor
    chat const to all "Thank you for your donation %s. Let there be forgiveness." 7031232
  #end select
#end template

#template diplo-should-player-be-marked
  #do once
    goal player-{player}-has-donated = 0
    goal player-{player}-donation-expire-at = -1
  #end do
  
  #if up-compare-goal diplo-game-time-goal g:> player-{player}-donation-expire-at
    goal player-{player}-donation-expire-at = -1
    goal player-{player}-has-donated = 0
  #end if
  
  // those who do not donate gold and have lots of it are marked
  #ifg goal player-{player}-has-donated 0 and up-allied-resource-amount {player} gold > 1000 and goal {marked_goal} 0 and players-building-type-count {player} monastery < 3
    chat to self "marked {player}"
    goal {marked_goal} = 1
    @clear-tribute-memory {player} gold
    insert chat-heretic(player={player})
  #end if
  
  // those who donate gold are no longer heretics
  #ifg (players-tribute-memory {player} gold > 0 or players-building-type-count {player} monastery >= 3) and goal {marked_goal} 1
    chat to self "unmarked {player}"
    goal {marked_goal} = 0
    insert diplo-set-future-stamp(goal="player-{player}-donation-expire-at",seconds="1200")
    goal player-{player}-has-donated = 1
    @clear-tribute-memory {player} gold
    insert chat-forgive(player={player})
  #end if
#end template

load "templates/diplomacy_engine.aoe2ai"
goal diplo-auto-resign = 0

#if game-time >= 1800 and building-type-count monastery == 0
  insert diplo-chat-resign
  @set-stance every-enemy ally
  @set-stance every-neutral ally
  resign
#end if

load "templates/lightning_castle.aoe2ai"

#if not player-valid 3
  goal duel = 1
#else
  goal duel = 0
#end if

#if goal duel 1
  #do once
    #delay by 2 minutes
      chat to all "It's okay. I know you suffer."
      #delay by 10 seconds
        chat to all "Confess, and be born anew."
      #end delay
    #end delay
  #end do
#end if

#if up-research-status c: castle-age >= research-pending
  #do once
    set up basics
    sn-maximum-attack-group-size = 1
    sn-minimum-attack-group-size = 1
    distribute villagers 30 30 40 0
    assign 8 builders to castle
    assign 2 builders to monastery
    assign 2 builders to siege-workshop
  #end do
  
  manage scouting
  
  #do once
    goal threat-time = 0
    goal threat-player = 0
    goal threat-source = 0
    goal threat-target = 0
  #end do
  @up-get-threat-data threat-time threat-player threat-source threat-target
  
  #if building-type-count-total town-center == 0 and stone-amount < 100
    build 1 market
    buy stone
  #end if
  build 1 town-center
  
  build 1 market
  sell wood when wood >= 500
  sell food when food >= 1200
  #if up-research-status c: imperial-age < research-pending
    buy food when gold >= 1000
  #end if
  
  build lumber camps
  #when
    build gold mining camps
    #if strategic-number sn-stone-gatherer-percentage > 0
      build stone mining camps
    #end if
  #then
    sn-camp-max-distance += 8
  #end when
  
  build mills
  #if wood-amount >= 200 or building-type-count-total farm < 6
    build farms
  #end if
  
  build houses with 10 headroom
  
  #do once
    #if building-type-count-total castle == 0
      #if strategic-number sn-stone-gatherer-percentage == 0
        distribute 9 villagers from wood and food and gold to stone
      #end if
    #else if strategic-number sn-stone-gatherer-percentage == 9
      distribute 9 villagers from stone to wood and food and gold
    #end if
  #end do
  
  build castle
  
  goal cap-monks = 0
  goal produce-monks = 1
  
  research imperial-age
  
  #if unit-type-count-total monk-set >= 5
    research lumber camp upgrades
    research gold mining camp upgrades
    research mill upgrades
    research ri-wheel-barrow
    research ri-hand-cart
    #if current-age >= imperial-age
      research ri-town-watch
      research ri-town-patrol
    #end if
  #end if
  
  research ri-capped-ram
  research ri-siege-ram
  
  research ri-inquisition
  research ri-grand-trunk-road
  
  // redemption
  #if research-available ri-redemption
    goal need-redemption = 0
    #respond to 1 mangonel-line
      goal need-redemption = 1
    #end respond
    #respond to 1 scorpion-line
      goal need-redemption = 1
    #end respond
    #respond to 1 battering-ram-line
      goal need-redemption = 1
    #end respond
    #respond to 1 trebuchet-set
      goal need-redemption = 1
    #end respond
    #respond to 1 organ-gun-line
      goal need-redemption = 1
    #end respond
    #if goal need-redemption 1 or gold-amount >= 1300
      research ri-redemption
      #if up-research-status c: ri-redemption < research-pending
        goal produce-monks = 0
      #end if
    #end if
  #end if
  
  // anti-monk techs
  #if research-available ri-atonement
    #respond to 2 monk-set
      research ri-atonement
      #if up-research-status c: ri-atonement < research-pending
        goal cap-monks = 1
      #end if
    #end respond
  #end if
  
  #if research-available ri-heresy
    #respond to 5 monk-set
      research ri-heresy
      #if up-research-status c: ri-heresy < research-pending
        goal cap-monks = 1
      #end if
    #end respond
  #end if
  
  #if research-available ri-faith
    #respond to 5 monk-set
      research ri-faith
      #if up-research-status c: ri-faith < research-pending
        goal cap-monks = 1
      #end if
    #end respond
  #end if

  // do these techs with an excess of gold
  #if gold-amount > 3000
    research ri-redemption
    research ri-herbal-medicine
    research ri-atonement
    research ri-heresy
    research ri-faith
  #end if
  
  // direct monk upgrades
  #stages
    #if unit-type-count monk-set >= 2
      research ri-sanctity
    #end if
    goal cap-monks = 1
  #advance when up-research-status c: ri-sanctity >= research-pending or not research-available ri-sanctity and building-type-count monastery >= 1
    research ri-fervor
    goal cap-monks = 1
  #advance when up-research-status c: ri-fervor >= research-pending or not research-available ri-fervor
    research ri-theocracy
    goal cap-monks = 1
  #advance when up-research-status c: ri-theocracy >= research-pending or not research-available ri-theocracy and current-age >= imperial-age
    research ri-illumination
    goal cap-monks = 1
  #advance when up-research-status c: ri-illumination >= research-pending or not research-available ri-illumination
    research ri-block-printing
    goal cap-monks = 1
  #advance when up-research-status c: ri-block-printing >= research-pending or not research-available ri-block-printing
    // nothing, monks could be uncapped.
  #end stages
  
  // military buildings
  #if building-type-count town-center >= 1
    #if current-age == castle-age
      build 4 monastery
      #if building-type-count-total monastery >= 4
        build 1 siege-workshop
      #end if
    #else
      build 10 monastery
      build 2 siege-workshop
    #end if
  #end if
  
  // training
  
  goal train-stuff = 1
  #if up-research-status c: imperial-age < research-pending and food-amount > 700 and gold-amount > 500 and (building-type-count castle >= 1 or building-type-count siege-workshop >= 1)
    goal train-stuff = 0
  #end if
  
  train mercenary-kipchak
  
  #if goal train-stuff 1
    #if current-age == imperial-age
      train 100 villagers with 50 trade
      #if unit-type-count-total trade-cart > 50
        delete unit trade-cart
      #end if
    #else
      train 100 villager
      delete unit trade-cart
    #end if
    
    #if goal produce-monks 1
      train 20 battering-ram-line
      #if goal cap-monks 1
        train 20 monk
      #else
        train monk
      #end if
    #end if
  #end if
  
  do basic targeting
  
  #do once
    goal attacking = 0
    goal done-pre-theocracy-attack = 0
  #end do
  
  #if unit-type-count monk-set >= 20 and not town-under-attack and player-in-game any-enemy and (goal done-pre-theocracy-attack 0 or research-completed ri-theocracy or game-time >= 3600)
    #if goal attacking 0
      #select random
        chat to all "'Love must be sincere. Hate what is evil; cling to what is good.'"
        chat to all "Romans 12:9. Repent."
      #randor
        chat to all "'Pride goes before destruction, a haughty spirit before a fall.'"
        chat to all "Proverbs 16:8. Repent."
      #randor
        chat to all "'What causes fights and quarrels among you? Don't they come from your desires that battle within you?'"
        chat to all "James 4:1. Repent."
      #randor
        chat to all "'Blessed are the merciful, for they will be shown mercy.'"
        chat to all "Matthew 5:7. Repent."
      #randor
        chat to all "'Do not judge, or you too will be judged.'"
        chat to all "Matthew 7:1. Repent."
      #randor
        chat to all "'So then, let us not be like others, who are asleep, but let us be awake and sober.'"
        chat to all "Thessalonians 5:6. Repent."
      #randor
        chat to all "'I keep my eyes always on the LORD. With him at my right hand, I shall not be shaken.'"
        chat to all "Psalm 16:8. Repent."
      #end select
    #end if
    goal attacking = 1
    #if not research-completed ri-theocracy
      goal done-pre-theocracy-attack = 1
    #end if
  #else if unit-type-count monk-set <= 13 or not player-in-game any-enemy
    goal attacking = 0
  #end if
  
  #if goal attacking 1 and players-stance any-enemy enemy
    sn-number-attack-groups = 200
  #else
    sn-number-attack-groups = 0
  #end if
  
  keep monk 15 tiles away from enemy castle
  keep monk 10 tiles away from enemy town-center
  
  auto expand town size to 30
  delete building feitoria
#end if

#if building-type-count market >= 1
  #do once
    #if food-amount >= 100
      #reply to enemy taunt 3
        chat to all "Even those who are misguided suffer in the same ways. Here."
        tribute 100 food to this-any-enemy
      #end reply
    #end if
    #if gold-amount >= 100
      #reply to enemy taunt 5
        chat to all "May the grace of God guide your path."
        tribute 100 gold to this-any-enemy
      #end reply
    #end if
  #end do
  
  #reply to ally taunt 3
    tribute 100 food to this-any-ally
  #end reply
  
  #reply to ally taunt 5
    tribute 100 gold to this-any-ally
  #end reply
#end if

#do once
  #if research-completed ri-sanctity
    chat to all "In times of trouble, seek forgiveness."
  #end if
  
  #if research-completed ri-theocracy
    chat to all "Confess to your sins, or burn in hellfire."
  #end if

  #if research-completed ri-heresy
    chat to all "'Watch out for false prophets. They come to you in sheep's clothing, but inwardly they are ferocious wolves.'"
    chat to all "Matthew 7:15."
  #end if
  
  #if game-time >= 1800 and population < 30
    chat to all "Lord, what is this trial?"
    #delay by 4 seconds
      chat to all "Have I not done enough to earn succour?"
    #end delay
  #end if
#end do

#if population > 100
  #do once
    goal target-pop = 0
  #end do
  @up-get-target-fact population 0 target-pop
  #if up-compare-goal target-pop < 30
    #do once
      chat to all "I'm sorry it had to be this way."
    #end do
  #end if
#end if