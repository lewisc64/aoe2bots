const ri-madrasah = 490

load "templates/lightning_castle.aoe2ai"

#if not player-valid 3
  goal duel = 1
#else
  goal duel = 0
#end if

#if goal duel 1
  #do once
    #delay by 2 minutes
      chat to all "It's okay. I know you suffer."
      #delay by 10 seconds
        chat to all "Confess, and be born anew."
      #end delay
    #end delay
  #end do
#end if

#if up-research-status c: castle-age >= research-pending
  #do once
    set up basics
    sn-maximum-attack-group-size = 1
    sn-minimum-attack-group-size = 1
    distribute villagers 30 30 40 0
    assign 8 builders to castle
    assign 2 builders to monastery
    assign 2 builders to siege-workshop
  #end do
  
  manage scouting
  
  #do once
    goal threat-time = 0
    goal threat-player = 0
    goal threat-source = 0
    goal threat-target = 0
  #end do
  @up-get-threat-data threat-time threat-player threat-source threat-target
  
  #if building-type-count-total town-center == 0 and stone-amount < 100
    build 1 market
    buy stone
  #end if
  build 1 town-center
  
  build 1 market
  sell wood when wood >= 500
  sell food when food >= 1200
  #if up-research-status c: imperial-age < research-pending
    buy food when gold >= 1000
  #end if
  
  build lumber camps
  #when
    build gold mining camps
    #if strategic-number sn-stone-gatherer-percentage > 0
      build stone mining camps
    #end if
  #then
    sn-camp-max-distance += 8
  #end when
  
  build mills
  #if wood-amount >= 200 or building-type-count-total farm < 6
    build farms
  #end if
  
  build houses with 10 headroom
  
  #do once
    #if building-type-count-total castle == 0
      #if strategic-number sn-stone-gatherer-percentage == 0
        distribute 9 villagers from wood and food and gold to stone
      #end if
    #else if strategic-number sn-stone-gatherer-percentage == 9
      distribute 9 villagers from stone to wood and food and gold
    #end if
  #end do
  
  build castle
  
  goal cap-monks = 0
  goal produce-monks = 1
  
  research imperial-age
  
  #if unit-type-count-total monk-set >= 5
    research lumber camp upgrades
    research gold mining camp upgrades
    research mill upgrades
    research ri-wheel-barrow
    research ri-hand-cart
    #if current-age >= imperial-age
      research ri-town-watch
      research ri-town-patrol
    #end if
  #end if
  
  research ri-capped-ram
  research ri-siege-ram
  
  research ri-madrasah
  
  goal need-redemption = 0
  #respond to 1 mangonel-line
    goal need-redemption = 1
  #end respond
  #respond to 1 scorpion-line
    goal need-redemption = 1
  #end respond
  #respond to 1 battering-ram-line
    goal need-redemption = 1
  #end respond
  #respond to 1 trebuchet-set
    goal need-redemption = 1
  #end respond
  #respond to 1 organ-gun-line
    goal need-redemption = 1
  #end respond
  #if goal need-redemption 1 or gold-amount >= 1300
    research ri-redemption
    #if up-research-status c: ri-redemption < research-pending
      goal produce-monks = 0
    #end if
  #end if
  
  #respond to 2 monk-set
    research ri-atonement
    #if up-research-status c: ri-atonement < research-pending
      goal cap-monks = 1
    #end if
  #end respond
  
  #respond to 5 monk-set
    research ri-heresy
    research ri-faith
    #if up-research-status c: ri-heresy < research-pending
      goal cap-monks = 1
    #end if
  #end respond
  
  #stages
    #if unit-type-count monk-set >= 2
      research ri-sanctity
    #end if
  #advance when up-research-status c: ri-sanctity >= research-pending
    research ri-fervor
  #advance when up-research-status c: ri-fervor >= research-pending
    research ri-theocracy
  #advance when up-research-status c: ri-theocracy >= research-pending
    research ri-illumination
  #advance when up-research-status c: ri-illumination >= research-pending
    research ri-block-printing
  #end stages
  
  #if up-research-status c: ri-block-printing < research-pending  
    goal cap-monks = 1
  #end if
  
  #if building-type-count town-center >= 1
    #if current-age == castle-age
      build 4 monastery
      #if building-type-count-total monastery >= 4
        build 1 siege-workshop
      #end if
    #else
      build 10 monastery
      build 2 siege-workshop
    #end if
  #end if
  
  #if current-age == imperial-age
    train 100 villagers with 50 trade
    #if unit-type-count-total trade-cart > 50
      delete unit trade-cart
    #end if
  #else
    train 100 villager
    delete unit trade-cart
  #end if
  
  #if goal produce-monks 1
    train 20 battering-ram-line
    #if goal cap-monks 1
      train 20 monk
    #else
      train monk
    #end if
  #end if
  
  do basic targeting
  
  #do once
    goal attacking = 0
    goal done-pre-theocracy-attack = 0
  #end do
  
  #if unit-type-count monk-set >= 20 and not town-under-attack and player-in-game any-enemy and (goal done-pre-theocracy-attack 0 or research-completed ri-theocracy)
    #if goal attacking 0
      #select random
        chat to all "'Love must be sincere. Hate what is evil; cling to what is good.'"
        chat to all "Romans 12:9. Repent."
      #randor
        chat to all "'Pride goes before destruction, a haughty spirit before a fall.'"
        chat to all "Proverbs 16:8. Repent."
      #randor
        chat to all "'What causes fights and quarrels among you?"
        chat to all "    Don't they come from your desires that battle within you?'"
        chat to all "James 4:1. Repent."
      #randor
        chat to all "'Blessed are the merciful, for they will be shown mercy.'"
        chat to all "Matthew 5:7. Repent."
      #randor
        chat to all "'Do not judge, or you too will be judged.'"
        chat to all "Matthew 7:1. Repent."
      #randor
        chat to all "'So then, let us not be like others,"
        chat to all "    who are asleep, but let us be awake and sober.'"
        chat to all "Thessalonians 5:6. Repent."
      #randor
        chat to all "'I keep my eyes always on the LORD."
        chat to all "    With him at my right hand, I shall not be shaken.'"
        chat to all "Psalm 16:8. Repent."
      #end select
    #end if
    goal attacking = 1
    #if not research-completed ri-theocracy
      goal done-pre-theocracy-attack = 1
    #end if
  #else if unit-type-count monk-set <= 13 or not player-in-game any-enemy
    goal attacking = 0
  #end if
  
  #if goal attacking 1
    sn-maximum-town-size = 255
    
    // monks and rams get stuck sometimes
    #repeat every 10 minutes
      @up-reset-unit c: -1
    #end repeat
    
  #else
    auto expand town size to 30
  #end if
  
  delete building feitoria
#end if

// diplomacy

const alliance-timeout = 60
const donation-timeout = 1200
const mute-period = 120

#do once
  @set-stance every-enemy neutral
  goal chat-alliance-offer = -1
  goal chat-alliance-our-betrayal = -1
  goal chat-alliance-their-betrayal = -1
  goal chat-alliance-accept-ours = -1
  goal chat-alliance-accept-theirs = -1
  goal chat-alliance-refuse-ours = -1
  goal chat-alliance-refuse-theirs = -1
  goal chat-outrage = -1
  goal chat-warning = -1
  goal chat-heretic = -1
  goal chat-forgive = -1
  goal locked-teams = 1
  
  goal game-time-goal = 0
#end do

#do once
  @set-stance my-player-number enemy
  #if stance-toward my-player-number enemy
    goal locked-teams = 0
    @set-stance my-player-number ally
  #end if
#end do

@up-get-fact game-time 0 game-time-goal

goal enemy-count = 0
goal ally-count = 0

#template count-stances
  #if player-in-game {player}
    #if stance-toward {player} ally
      goal ally-count += 1
    #else if stance-toward {player} enemy
      goal enemy-count += 1
    #end if
  #end if
#end template

insert count-stances(player=1)
insert count-stances(player=2)
insert count-stances(player=3)
insert count-stances(player=4)
insert count-stances(player=5)
insert count-stances(player=6)
insert count-stances(player=7)
insert count-stances(player=8)

#if stance-toward my-player-number enemy
  goal enemy-count -= 1
#else if stance-toward my-player-number ally
  goal ally-count -= 1
#end if

#template set-future-stamp
  @up-get-fact game-time 0 {goal}
  @up-modify-goal {goal} c:+ {seconds}
#end template

#template retry-alliance
  #if goal trying-to-ally-{player} 0
    goal player-{player}-alliance-offer-expire-at = -1
  #end if
#end template

#template handle-diplomacy

  #do once
    goal player-{player}-warnings = 1
    goal player-{player}-has-donated = 0
    goal player-{player}-is-heretic = 0
    goal player-{player}-alliance-offer-expire-at = -1
    goal player-{player}-donation-expire-at = -1
    goal trying-to-ally-{player} = 0
  #end do
  
  // donation timeout
  #if up-compare-goal game-time-goal g:> player-{player}-donation-expire-at
    goal player-{player}-donation-expire-at = -1
    goal player-{player}-has-donated = 0
  #end if
  
  // retry alliance if they are dying
  #if players-population {player} < 40 and population > 100 and stance-toward {player} enemy
    #do once
      insert retry-alliance(player={player})
    #end do
  #end if
  
  #if player-in-game {player}
  
    // enemy allies who threaten our monks or monasteries
    #ifg goal done 0 and goal threat-player {player} and players-stance {player} ally and (goal threat-target monk or goal threat-target monastery) and up-compare-goal threat-time c:< 5000
      chat to self "threatened by ally {player}"
      #ifg goal player-{player}-warnings 0
        @set-stance {player} enemy
        goal chat-outrage = {player}
        goal player-{player}-is-heretic = 1
        goal done = 1
      #else
        goal player-{player}-warnings -= 1
        goal chat-warning = {player}
        goal done = 1
      #end if
    #end if
  
    // try to ally our non-heretic enemies
    #ifg goal done 0 and not stance-toward {player} ally and not players-stance {player} ally and goal player-{player}-is-heretic 0 and goal player-{player}-alliance-offer-expire-at -1
      chat to self "request alliance {player}"
      insert set-future-stamp(goal="player-{player}-alliance-offer-expire-at",seconds="alliance-timeout")
      @set-stance {player} ally
      goal trying-to-ally-{player} = 1
      goal chat-alliance-offer = {player}
      goal done = 1
    #end if
    
    // those who do not donate gold and have lots of it are heretics
    #ifg goal done 0 and goal player-{player}-has-donated 0 and up-allied-resource-amount {player} gold > 1000 and goal player-{player}-is-heretic 0 and players-building-type-count {player} monastery < 3
      chat to self "heretic {player}"
      @clear-tribute-memory {player} gold
      goal player-{player}-is-heretic = 1
      goal chat-heretic = {player}
      goal done = 1
    #end if
    
    // those who donate gold are no longer heretics
    #ifg goal done 0 and players-tribute-memory {player} gold > 0 and goal player-{player}-is-heretic 1
      chat to self "un-heretic {player}"
      insert set-future-stamp(goal="player-{player}-donation-expire-at",seconds="donation-timeout")
      goal player-{player}-is-heretic = 0
      goal player-{player}-has-donated = 1
      @clear-tribute-memory {player} gold
      goal chat-forgive = {player}
      goal done = 1
    #end if
    
    #if goal trying-to-ally-{player} 0
    
      // enemy those who enemy us
      #ifg goal done 0 and players-stance {player} enemy and not stance-toward {player} enemy
        chat to self "enemy those who enemy us {player}"
        #if stance-toward {player} ally
          goal chat-alliance-their-betrayal = {player}
        #end if
        @set-stance {player} enemy
        goal done = 1
      #end if
      
      // enemy those who have us as neutral
      #ifg goal done 0 and players-stance {player} neutral and not stance-toward {player} enemy
        chat to self "enemy neutral {player}"
        @set-stance {player} enemy
        goal done = 1
      #end if
      
      // ally those who ally us
      #ifg goal done 0 and players-stance {player} ally and not stance-toward {player} ally and goal player-{player}-is-heretic 0
        chat to self "accept alliance {player}"
        goal chat-alliance-accept-theirs = {player}
        @set-stance {player} ally
        goal player-{player}-alliance-offer-expire-at = -1
        goal done = 1
      #end if
      
      // enemy those we consider heretics
      #ifg goal done 0 and not stance-toward {player} enemy and goal player-{player}-is-heretic 1 and goal enemy-count 0 and unit-type-count monk-set >= 20
        chat to self "enemy heretic {player}"
        #if stance-toward {player} ally
          goal chat-alliance-our-betrayal = {player}
          goal attacking = 1
        #end if
        @set-stance {player} enemy
        goal done = 1
      #end if
      
    #else
      
      // they accepted our alliance
      #ifg goal done 0 and players-stance {player} ally
        chat to self "they accepted alliance {player}"
        goal chat-alliance-accept-ours = {player}
        goal trying-to-ally-{player} = 0
        goal player-{player}-alliance-offer-expire-at = -1
        goal done = 1
      #end if
      
      // they refused our alliance
      #ifg goal done 0 and players-stance {player} enemy and up-compare-goal game-time-goal g:> player-{player}-alliance-offer-expire-at
        chat to self "they refused alliance {player}"
        goal chat-alliance-refuse-ours = {player}
        goal trying-to-ally-{player} = 0
        @set-stance {player} enemy
        goal done = 1
      #end if
      
    #end if
  #end if
  
  #if not player-in-game {player} and players-stance {player} ally
    @set-stance {player} ally
  #end if
  
#end template

#if goal locked-teams 0
  #repeat every 5 seconds
    goal done = 0
    insert handle-diplomacy(player=1)
    insert handle-diplomacy(player=2)
    insert handle-diplomacy(player=3)
    insert handle-diplomacy(player=4)
    insert handle-diplomacy(player=5)
    insert handle-diplomacy(player=6)
    insert handle-diplomacy(player=7)
    insert handle-diplomacy(player=8)
  #end repeat
#end if

#if game-time >= 1800 and building-type-count monastery == 0
  chat to all "The abbey is destroyed. I have failed."
  @set-stance every-enemy ally
  @set-stance every-neutral ally
  resign
#end if

#if goal locked-teams 0
  @set-stance my-player-number ally
#end if

// suppress initial diplo chats
#if game-time < mute-period
  goal chat-alliance-offer = -1
  goal chat-alliance-our-betrayal = -1
  goal chat-alliance-their-betrayal = -1
  goal chat-alliance-accept-ours = -1
  goal chat-alliance-accept-theirs = -1
  goal chat-alliance-refuse-ours = -1
  goal chat-alliance-refuse-theirs = -1
  goal chat-heretic = -1
  goal chat-forgive = -1
#end if

// after mute period is over, retry alliance requests to name and shame.
#if game-time >= mute-period
  #do once grouped
    insert retry-alliance(player=1)
    insert retry-alliance(player=2)
    insert retry-alliance(player=3)
    insert retry-alliance(player=4)
    insert retry-alliance(player=5)
    insert retry-alliance(player=6)
    insert retry-alliance(player=7)
    insert retry-alliance(player=8)
  #end do
#end if

// retry alliances if we are in trouble
#if game-time >= 1200 and population < 30
  #do once grouped
    insert retry-alliance(player=1)
    insert retry-alliance(player=2)
    insert retry-alliance(player=3)
    insert retry-alliance(player=4)
    insert retry-alliance(player=5)
    insert retry-alliance(player=6)
    insert retry-alliance(player=7)
    insert retry-alliance(player=8)
  #end do
#end if

#if not goal chat-heretic -1
  #select random
    @up-chat-data-to-all "Player %d, you have fallen from grace." g: chat-heretic
  #randor
    @up-chat-data-to-all "A shame, %d. Greed has become you." g: chat-heretic
  #randor
    @up-chat-data-to-all "You bloat like a pig, %d." g: chat-heretic
  #randor
    @up-chat-data-to-all "Player %d you are a lecherous animal." g: chat-heretic
  #end select
  chat to all "5Donate and be cleansed of sin."
  goal chat-heretic = -1
#end if

#if not goal chat-forgive -1
  #select random
    @up-chat-data-to-all "Thank you for your donation %d. The Lord is pleased." g: chat-forgive
  #randor
    @up-chat-data-to-all "Thank you for your donation %d. Let there be forgiveness." g: chat-forgive
  #end select
  goal chat-forgive = -1
#end if

#if not goal chat-alliance-offer -1
  #select random
    @up-chat-data-to-all "Player %d, join me in worship." g: chat-alliance-offer
  #randor
    @up-chat-data-to-all "An alliance, %d?" g: chat-alliance-offer
  #randor
    @up-chat-data-to-all "Shall we join forces in the name of God, %d?" g: chat-alliance-offer
  #randor
    @up-chat-data-to-all "We will be stronger together, %d." g: chat-alliance-offer
  #end select
  goal chat-alliance-offer = -1
#end if

#if not goal chat-alliance-our-betrayal -1
  #select random
    @up-chat-data-to-all "Your demise is but a part of God's plan, %d." g: chat-alliance-our-betrayal
  #randor
    @up-chat-data-to-all "Player %d. I find you in contempt of the Lord. Die." g: chat-alliance-our-betrayal
  #randor
    @up-chat-data-to-all "Player %d you are a sinner, and will now perish." g: chat-alliance-our-betrayal
  #end select
  goal chat-alliance-our-betrayal = -1
#end if

#if not goal chat-alliance-their-betrayal -1
  #select random
    @up-chat-data-to-all "Player %d? Did you dislike the peace?" g: chat-alliance-their-betrayal
  #randor
    @up-chat-data-to-all "Unfortunate. I wished for nothing but peace, %d." g: chat-alliance-their-betrayal
  #randor
    @up-chat-data-to-all "I should have known. I was blinded by love." g: chat-alliance-their-betrayal
  #randor
    @up-chat-data-to-all "I will miss our friendship, %d." g: chat-alliance-their-betrayal
  #end select
  goal chat-alliance-their-betrayal = -1
#end if

#if not goal chat-alliance-accept-ours -1
  #select random
    @up-chat-data-to-all "An excellent choice, %d." g: chat-alliance-accept-ours
  #randor
    @up-chat-data-to-all "Wonderful. Let us study together %d." g: chat-alliance-accept-ours
  #randor
    @up-chat-data-to-all "Excellent, %d. A blessed day." g: chat-alliance-accept-ours
  #randor
    @up-chat-data-to-all "Most wise, %d." g: chat-alliance-accept-ours
  #end select
  goal chat-alliance-accept-ours = -1
#end if

#if not goal chat-alliance-accept-theirs -1
  #select random
    @up-chat-data-to-all "Of course %d, let us have peace." g: chat-alliance-accept-theirs
  #randor
    @up-chat-data-to-all "Your letter was most moving, %d. I accept." g: chat-alliance-accept-theirs
  #randor
    @up-chat-data-to-all "Player %d? Hm. I suppose friendship knows no bounds." g: chat-alliance-accept-theirs
  #end select
  goal chat-alliance-accept-theirs = -1
#end if

#if not goal chat-alliance-refuse-ours -1
  #select random
    @up-chat-data-to-all "Your desire for bloodshed is sickening, %d." g: chat-alliance-refuse-ours
  #randor
    @up-chat-data-to-all "Alas, we are doomed to be enemies %d." g: chat-alliance-refuse-ours
  #randor
    @up-chat-data-to-all "Reconsider my offer soon, %d." g: chat-alliance-refuse-ours
  #end select
  goal chat-alliance-refuse-ours = -1
#end if

#if not goal chat-alliance-refuse-theirs -1
  #select random
    @up-chat-data-to-all "You are sinful, %d. We cannot be friends." g: chat-alliance-refuse-theirs
  #randor
    @up-chat-data-to-all "I would sooner burn than ally with you, %d." g: chat-alliance-refuse-theirs
  #randor
    @up-chat-data-to-all "Player %d, I believe I have made my intentions clear." g: chat-alliance-refuse-theirs
  #randor
    @up-chat-data-to-all "We are beyond that point now, %d." g: chat-alliance-refuse-theirs
  #end select
  goal chat-alliance-refuse-theirs = -1
#end if

#if not goal chat-outrage -1
  #select random
    @up-chat-data-to-all "You dare defy the will of God, %d?!" g: chat-outrage
  #randor
    @up-chat-data-to-all "How dare you!" g: chat-outrage
  #randor
    @up-chat-data-to-all "Heretical scum!" g: chat-outrage
  #randor
    @up-chat-data-to-all "That was the last straw!" g: chat-outrage
  #randor
    @up-chat-data-to-all "We are done, %d!" g: chat-outrage
  #end select
  goal chat-outrage = -1
#end if

#if not goal chat-warning -1
  #select random
    @up-chat-data-to-all "Please %d, do not do that again." g: chat-warning
  #randor
    @up-chat-data-to-all "That was a blasphemous action, %d. No more." g: chat-warning
  #randor
    @up-chat-data-to-all "Don't you dare do that again, %d." g: chat-warning
  #end select
  goal chat-warning = -1
#end if

#if building-type-count market >= 1
  #do once
    #if food-amount >= 100
      #reply to enemy taunt 3
        chat to all "Even those who are misguided suffer in the same ways. Here."
        tribute 100 food to this-any-enemy
      #end reply
    #end if
    #if gold-amount >= 100
      #reply to enemy taunt 5
        chat to all "May the grace of God guide your path."
        tribute 100 gold to this-any-enemy
      #end reply
    #end if
  #end do
  
  #reply to ally taunt 3
    tribute 100 food to this-any-ally
  #end reply
  
  #reply to ally taunt 5
    tribute 100 gold to this-any-ally
  #end reply
#end if

#do once
  #if research-completed ri-sanctity
    chat to all "In times of trouble, seek forgiveness."
  #end if
  
  #if research-completed ri-theocracy
    chat to all "Confess to your sins, or burn in hellfire."
  #end if

  #if research-completed ri-heresy
    chat to all "'Watch out for false prophets."
    chat to all "    They come to you in sheep's clothing,"
    chat to all "    but inwardly they are ferocious wolves.'"
    chat to all "Matthew 7:15."
  #end if
  
  #if game-time >= 1800 and population < 30
    chat to all "Lord, what is this trial?"
    #delay by 4 seconds
      chat to all "Have I not done enough to earn succour?"
    #end delay
  #end if
#end do

#if population > 100
  #do once
    goal target-pop = 0
  #end do
  @up-get-target-fact population 0 target-pop
  #if up-compare-goal target-pop < 30
    #do once
      chat to all "I'm sorry it had to be this way."
    #end do
  #end if
#end if