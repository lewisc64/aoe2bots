const ri-el-dorado = 4
const ri-garland-wars = 24

const tc-arrow = 54
const watch-tower-arrow = 786
const castle-arrow = 746
const castle-arrow-chemistry = 747

const max-wood = 1500
const max-food = 1500
const max-gold = 1500

#select random persistant
  do basic diplomacy
#randor
  do basic diplomacy without backstabbing
#end select

load "templates/fast_castle.aoe2ai"

#if up-research-status c: castle-age >= research-pending
  #do once
    set up basics
    
    sn-maximum-attack-group-size = 1
    sn-minimum-attack-group-size = 1
    sn-special-attack-type2 = villager

    assign 4 builders to town-center-foundation
    assign 8 builders to castle
    
    distribute villagers 30 45 20 5
    #if building-type-count castle >= 1 and research-completed ri-couriers
      distribute 5 villagers from stone to food
    #end if
    scout opposite
  #end do
  
  #if escrow-amount wood < 300
    escrow 10 wood
  #else
    escrow 0 wood
  #end if
  
  #if escrow-amount food < 1000
    escrow 10 food
  #else
    escrow 0 food
  #end if
  
  #if escrow-amount gold < 800
    escrow 10 gold
  #else
    escrow 0 gold
  #end if

  load "charlotte/stats.aoe2ai"
  
  research castle-age with food and gold escrow
  research imperial-age with food and gold escrow
  
  #if up-compare-goal non-escrowed-wood >= 200
    research mill upgrades with wood and food escrow
    research lumber camp upgrades with wood and food escrow
    research gold mining camp upgrades with wood and food escrow

    #if current-age == castle-age
      research ri-wheel-barrow with wood and food escrow
    #end if

    #if current-age == imperial-age
      research ri-hand-cart with wood and food escrow
    #end if
  #end if

  #when
    research ri-garland-wars with food and gold escrow
    research ri-el-dorado with food and gold escrow
    research ri-couriers with food and gold escrow
  #then always
    research ri-conscription with food and gold escrow
  #end when

  research ri-eagle-warrior with food and gold escrow => research ri-arson with food and gold escrow
  research ri-elite-eagle-warrior with food and gold escrow
  research ri-squires with food escrow
  research blacksmith infantry upgrades with food and gold escrow
  
  research ri-capped-ram with food escrow
  research ri-siege-ram with food escrow

  #if current-age >= feudal-age
    research ri-loom with gold escrow
  #end if

  #if current-age >= imperial-age
    train 120 villagers with 30 trade using escrow for caravan
  #else
    train 120 villager
  #end if
  research ri-loom with gold escrow

  #if not town-under-attack or goal threat-source castle
    goal build-siege = 1
  #else
    goal build-siege = 0
  #end if

  #if up-research-status c: ri-eagle-warrior >= research-pending
    #if building-type-count siege-workshop >= 1 and unit-type-count-total battering-ram-line < 3 and goal build-siege 1
      train battering-ram-line
    #else if building-type-count castle >= 1 and unit-type-count-total trebuchet-set < 2 and current-age == imperial-age and goal build-siege 1
      train trebuchet
    #else if up-compare-goal non-escrowed-food c:>= 70
      train eagle-warrior-line
    #end if
  #end if

  train 1 monk

  #if current-age == feudal-age and military-population >= 8 or military-population >= 30
    #if strategic-number sn-maximum-town-size < 255
      sn-maximum-town-size += 255
    #end if
    #if cc-players-unit-type-count target-player tc-arrow > 0 or cc-players-unit-type-count any-enemy target-player > 0 or cc-players-unit-type-count target-player castle-arrow-chemistry > 0
      #if unit-type-count battering-ram-line == 0 and unit-type-count trebuchet-set == 0 and unit-type-count bombard-cannon == 0
        #if goal threat-source town-center or goal threat-source castle
          sn-maximum-town-size -= 255
          retreat
        #end if
      #end if
    #end if
  #else if strategic-number sn-maximum-town-size >= 255
    sn-maximum-town-size -= 255
  #end if

  #do once
    target closest enemy
    scout enemy
  #end do
  #if not player-in-game target-player
    target closest enemy
    scout enemy
  #end if

  #if up-research-status c: feudal-age >= research-pending and not research-completed castle-age
    build 2 barracks with wood escrow
  #else if current-age == castle-age
    build 3 barracks with wood escrow
  #else if current-age == imperial-age
    build 6 barracks with wood escrow
  #end if

  build mills
  build lumber camps
  #if up-compare-goal non-escrowed-wood >= 200
    build 1 blacksmith
    build 1 market
    build 2 siege-workshop
    build 1 monastery
  #end if
  build 3 town-center
  build castle
  
  build houses with 20 headroom
  
  #when
    build gold mining camps
    build stone mining camps
  #then
    sn-camp-max-distance += 8
  #end when
  
  #if up-compare-goal non-escrowed-wood >= 100
    build farms
  #end if
  
  trade excess wood and food and gold at 1500
  auto balance wood and food and gold
  
  auto expand town size to 30
#end if
