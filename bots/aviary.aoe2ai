const ri-el-dorado = 4
const ri-garland-wars = 24
const ri-andean-sling = 516

const tc-arrow = 54
const watch-tower-arrow = 786
const castle-arrow = 746
const castle-arrow-chemistry = 747

const max-wood = 1500
const max-food = 1500
const max-gold = 1500

#do once
  set up basics
  take boar
  
  sn-maximum-attack-group-size = 1
  sn-minimum-attack-group-size = 1
  sn-special-attack-type2 = villager

  assign 4 builders to town-center-foundation
#end do

#do once
  goal escrowed-wood = 0
  goal escrowed-food = 0
  goal escrowed-gold = 0
  goal non-escrowed-wood = 0
  goal non-escrowed-food = 0
  goal non-escrowed-gold = 0
#end do

@up-get-fact escrow-amount wood escrowed-wood
@up-get-fact escrow-amount food escrowed-food
@up-get-fact escrow-amount gold escrowed-gold

@up-get-fact resource-amount wood non-escrowed-wood
@up-get-fact resource-amount food non-escrowed-food
@up-get-fact resource-amount gold non-escrowed-gold

@up-modify-goal non-escrowed-wood g:- escrowed-wood
@up-modify-goal non-escrowed-food g:- escrowed-food
@up-modify-goal non-escrowed-gold g:- escrowed-gold

#do once
  goal threat-time = 0
  goal threat-player = 0
  goal threat-source = 0
  goal threat-target = 0
#end do
@up-get-threat-data threat-time threat-player threat-source threat-target

#if building-type-count-total house == 0
  #do once
    assign 2 builders to house
  #end do
#else
  #do once
    assign 1 builder to house
  #end do
#end if

research feudal-age with food escrow
research castle-age with food and gold escrow
research imperial-age with food and gold escrow

#if up-compare-goal non-escrowed-wood >= 200
  research mill upgrades with wood and food escrow
  research lumber camp upgrades with wood and food escrow
  research gold mining camp upgrades with wood and food escrow

  #if current-age == castle-age
    research ri-wheel-barrow with wood and food escrow
  #end if

  #if current-age == imperial-age
    research ri-hand-cart with wood and food escrow
  #end if
#end if

research ri-garland-wars with food and gold escrow
research ri-el-dorado with food and gold escrow
research ri-couriers with food and gold escrow

research ri-eagle-warrior with food and gold escrow => research ri-arson with food and gold escrow
research ri-elite-eagle-warrior with food and gold escrow

research ri-squires with food escrow

research blacksmith infantry upgrades with food and gold escrow
#if current-age >= castle-age
  research blacksmith ranged upgrades with food and gold escrow
  research ri-thumb-ring with food and wood escrow
  build 1 university
  research ri-ballistics with wood and gold escrow
  research ri-andean-sling with food and gold escrow
#end if

//research ri-capped-ram with food escrow
//research ri-siege-ram with food escrow

#if current-age >= feudal-age
  research ri-loom with gold escrow
#end if

train 120 villager
research ri-loom with gold escrow

#if building-type-count siege-workshop >= 1 and unit-type-count-total battering-ram-line < 3 and current-age <= castle-age
  train battering-ram-line
#else if building-type-count castle >= 1 and unit-type-count-total trebuchet-set < 2 and current-age == imperial-age
  train trebuchet
#else
  #if building-type-count archery-range >= 2
    train eagle-warrior-line => train eagle-warrior-line => train eagle-warrior-line => train slinger
  #else
    train eagle-warrior-line
  #end if
#end if

train 1 monk

#if current-age == feudal-age and military-population >= 8 or military-population >= 30
  #if strategic-number sn-maximum-town-size < 255
    sn-maximum-town-size += 255
  #end if
  #if cc-players-unit-type-count target-player tc-arrow > 0 or cc-players-unit-type-count any-enemy target-player > 0 or cc-players-unit-type-count target-player castle-arrow-chemistry > 0
    #if unit-type-count battering-ram-line == 0 and unit-type-count trebuchet-set == 0 and unit-type-count bombard-cannon == 0
      #if goal threat-source town-center or goal threat-source castle
        sn-maximum-town-size -= 255
        retreat
      #end if
    #end if
  #end if
#else if strategic-number sn-maximum-town-size >= 255
  sn-maximum-town-size -= 255
#end if

#do once
  target closest enemy
#end do
#if not player-valid target-player
  target closest enemy
#end if

#if up-research-status c: feudal-age >= research-pending and not research-completed castle-age
  build 2 barracks with wood escrow
  #if current-age >= castle-age
    build 2 archery-range
  #end if
#else if current-age == castle-age
  build 3 barracks with wood escrow
  #if current-age >= castle-age
    build 3 archery-range
  #end if
#else if current-age == imperial-age
  build 5 barracks with wood escrow
  #if current-age >= castle-age
    build 5 archery-range
  #end if
#end if

build mills
build lumber camps
#if up-compare-goal non-escrowed-wood >= 200
  build 1 blacksmith
  build 1 market
  build 2 siege-workshop
  build 1 monastery
#end if
build 3 town-center
build castle

#if up-research-status c: feudal-age < research-pending

  #do once
    distribute villagers 25 75 0 0
    sn-enable-training-queue = 1
  #end do
  
  #if food-amount < 50
    drop off food
  #end if
  
  build houses with 5 headroom
  
  #if civilian-population >= 24
    sn-enable-training-queue = 0
  #end if
  
  #if wood-amount > 100
    build farms
  #end if
  
  lure boars

#else

  #do once
    distribute villagers 30 45 20 5
    #if building-type-count castle >= 1 and research-completed ri-couriers
      distribute 5 villagers from stone to food
    #end if
    scout opposite
  #end do

  #if wood-amount < 300
    escrow 10 wood
  #else
    escrow 0 wood
  #end if
  
  #if food-amount < 1000
    escrow 10 food
  #else
    escrow 0 food
  #end if
  
  #if gold-amount < 800
    escrow 10 gold
  #else
    escrow 0 gold
  #end if
  
  #repeat every 60 seconds
    #if up-compare-goal non-escrowed-wood > max-wood
      distribute 4 villagers from wood to food and gold
    #end if
    
    #if up-compare-goal non-escrowed-food > max-food
      distribute 4 villagers from food to wood and gold
    #end if
    
    #if up-compare-goal non-escrowed-gold > max-gold
      distribute 4 villagers from gold to wood and food
    #end if
  #end repeat
  
  build houses with 20 headroom
  
  #if up-compare-goal non-escrowed-wood >= 200
    build farms
  #end if
  
  #when
    build gold mining camps
    build stone mining camps
  #then
    sn-camp-max-distance += 8
  #end when

#end if
