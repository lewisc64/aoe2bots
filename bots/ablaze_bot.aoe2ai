// plays as Tatars

#do once
  set name to "AblazeBot"

  #if not civ-selected tatars
    chat to all "I cannot burn bright without Tatars."
  #end if
#end do

load "templates/basic_diplomacy.aoe2ai"
load "templates/fast_castle.aoe2ai"

#if current-age >= castle-age

  #do once
    distribute villagers 30 50 20 0
    sn-object-repair-level = 0
    chat to all "I will light the all-consuming flame!"
  #end do

  auto balance wood and food and gold
  trade excess wood and food and gold at 2000

  #if wood-amount >= 200
    build farms
    build mills
  #end if

  build 3 town-center

  build houses with 10 headroom
  build lumber camps
  #when
    build gold mining camps
  #then
    sn-camp-max-distance += 8
  #end when

  build 1 blacksmith
  #if building-type-count-total blacksmith >= 1
    build 1 siege-workshop
    #if building-type-count-total siege-workshop >= 1
      build 1 market
      build 1 university
    #end if
  #end if

  #do once
    goal did-attack-ground = 0
  #end do

  // everything burns!

  $clear
  $focus myself
  $find local 1 town-center
  $target is first local
  $clear local
  $find local mangonel-line
  $find remote 40 building-class
  $remove from remote where object-data-hitpoints < 200
  $find remote 40 building-class
  $remove from remote where object-data-hitpoints < 200
  $find remote 40 building-class
  $remove from remote where object-data-hitpoints < 200
  $order remote list by 44 ascending
  $set goal healthy-building-count to remote list length

  #if goal healthy-building-count 0
    #if goal did-attack-ground 1
      @up-reset-unit c: mangonel-line
      goal did-attack-ground = 0
      #do once
        chat to all "AAAAAAAAAAAAAHHHHH EVERYTHING BURNS!!!"
      #end do
    #end if
  #else
    $attack ground
    goal did-attack-ground = 1
  #end if

  research imperial-age

  #if current-age >= imperial-age
    build 8 siege-workshop
    build 1 barracks
    research ri-arson
    research ri-chemistry
  #end if

  train 5 mangonel-line
  #if unit-type-count-total mangonel-line >= 5
    train 75 flaming-camel
  #end if

  do basic targeting

  #if unit-type-count-total flaming-camel >= 20
    attack
    #do once
      chat to all "AHAHAHAHAA! YESS BURNN!!!!"
    #end do
  #end if

  #if current-age == imperial-age
    train 120 villagers with 20 trade
  #else
    train 120 villager
  #end if

  auto expand town size

#end if
