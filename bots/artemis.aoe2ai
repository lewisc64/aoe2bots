// play as Ethiopians

#do once
  goal threat-time = 0
  goal threat-player = 0
  goal threat-source = 0
  goal threat-target = 0
#end do

@up-get-threat-data threat-time threat-player threat-source threat-target

do basic diplomacy

#do once
  set up basics

  assign 4 builders to town-center
  assign 2 builders to archery-range
  assign 8 builders to castle
#end do

build 1 town-center
build lumber camps
build mills
#when
  build gold mining camps
  #if strategic-number sn-stone-gatherer-percentage > 0
    build stone mining camps
  #end if
#then
  sn-camp-max-distance += 8
#end when

#if up-research-status c: feudal-age < research-pending

  #do once
    distribute villagers 20 80 0 0
    sn-enable-training-queue = 1
  #end do
  
  #if food-amount >= 300
    sn-enable-training-queue = 0
  #end if

  #if wood-amount >= 100
    build farms
  #end if
  
  lure boars
  
  build houses with 5 headroom
  
  research ri-loom
  research feudal-age

#else

  #if wood-amount < 300
    escrow 10 wood
  #else
    escrow 0 wood
  #end if
  
  #if food-amount < 1000
    escrow 10 food
  #else
    escrow 0 food
  #end if
  
  #if gold-amount < 800
    escrow 10 gold
  #else
    escrow 0 gold
  #end if
  
  #do once
    distribute villagers 80 0 20 0
  #end do
  #if building-type-count archery-range >= 2
    #do once
      distribute villagers 40 30 30 0
    #end do
    auto balance wood and food and gold
  #end if
  
  trade excess at 2000
  
  #if wood-amount >= 100 and building-type-count-total archery-range >= 2
    build farms
  #end if
  build houses with 10 headroom
  build 1 barracks
  #if building-type-count-total archery-range >= 2
    #when
      build 1 blacksmith
    #then always
      build 1 market
    #end when
  #end if
  build 1 university
  #if current-age == feudal-age
    build 2 archery-range
  #else if current-age == castle-age
    build 3 archery-range
    build 1 siege-workshop
  #else if current-age == imperial-age
    build 5 archery-range
    build 2 siege-workshop
    build 2 barracks
  #end if
  
  #if building-type-count-total archery-range >= 3
    build 1 monastery
    train 1 monk
  #end if
  
  #if up-research-status c: castle-age < research-pending
    sell wood when wood >= 300
    sell food when food >= 900
    buy food when gold >= 300
  #end if
  
  research ri-loom
  research castle-age with food and gold escrow
  research imperial-age with food and gold escrow
  
  #when
    research ri-fletching with food and gold escrow
  #then always
    research ri-padded-archer-armor with food and gold escrow
    research lumber camp upgrades with wood and food escrow
    research mill upgrades with wood and food escrow
    research gold mining camp upgrades with wood and food escrow
  #end when
  
  #when
    research ri-crossbow with food and gold escrow
  #then always
    #when
      research ri-bodkin-arrow with food and gold escrow
    #then always
      research ri-ballistics with wood and gold escrow
      research ranged blacksmith upgrades with food and gold escrow
      research ri-thumb-ring with wood and food escrow
      research ri-wheel-barrow with wood and food escrow
      research ri-hand-cart with wood and food escrow
    #end when
  #end when
  #when
    research ri-arbalest with food and gold escrow
  #then always
    research ri-chemistry with food and gold escrow
  #end when
  
  #if unit-type-count-total battering-ram-line >= 2
    research ri-capped-ram with food escrow
    research ri-siege-ram with food escrow
  #end if
  
  #if unit-type-count-total mangonel-line >= 5
    research ri-onager with food and gold escrow
    research ri-siege-onager with food and gold escrow
  #end if
  
  respond to knight-line with 5 spearman-line
  
  #if up-research-status c: castle-age >= research-pending or military-population < 20
    #if building-type-count siege-workshop >= 1 and unit-type-count-total battering-ram-line < 2 and (not town-under-attack or goal threat-source castle)
      train battering-ram-line
    #else
      #if players-unit-type-count target-player skirmisher-line > 0
        #if current-age == feudal-age
          #do once
            distribute 10 villagers from gold to food
          #end do
          train 20 skirmisher-line
          #if unit-type-count-total skirmisher-line >= 20
            train archer-line
          #end if
        #else if current-age >= castle-age
          train mangonel-line
          #if unit-type-count-total mangonel-line >= 2
            train 10 battering-ram-line
            train archer-line
          #end if
        #else if town-under-attack
          train skirmisher-line
        #end if
      #else
        train 10 battering-ram-line
        train archer-line
      #end if
    #end if
  #end if

#end if

#if current-age == imperial-age
  train 120 villagers with 30 trade
#else
  train 120 villager
#end if

#do once
  target closest enemy
#end do
#if not player-in-game target-player or not stance-toward target-player enemy
  target closest enemy
#end if

#if military-population >= 8 and current-age == feudal-age or military-population >= 20 and goal attacking 0 and not town-under-attack
  goal attacking = 1
#end if

#if goal attacking 1
  #if enemy-buildings-in-town
    sn-maximum-town-size -= 1
  #else
    sn-maximum-town-size += 5
  #end if
#else
  auto expand town size to 30
#end if
