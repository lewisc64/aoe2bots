// play as Italians

const ri-pavise = 494
const ri-silk-road = 499

const min-detection-units = 2
const max-rams = 10

// bootstrap

#do once
  set up basics
  assign 4 builders to town-center-foundation
  assign 8 builders to castle
  assign 2 builders to barracks
  assign 2 builders to archery-range
  goal produce-units = 1
#end do

load "charlotte/stats.aoe2ai"

build 1 town-center // for nomad

#do once
  goal has-rush-buildings = 0
#end do
#if building-type-count-total archery-range >= 2
  goal has-rush-buildings = 1
#end if

// diplomacy

do basic diplomacy

// escrow

#template set-escrow
  #if escrow-amount wood < 400
    escrow {percent} wood
  #else
    escrow 0 wood
  #end if
  #if escrow-amount food < 1000
    escrow {percent} food
  #else
    escrow 0 food
  #end if
  #if escrow-amount gold < 800
    escrow {percent} gold
  #else
    escrow 0 gold
  #end if
#end template

#if current-age >= feudal-age
  insert set-escrow(percent=10)
#end if

// camps

#if game-time > 30
  build lumber camps
#end if

#template mining-camps
  #if strategic-number sn-{type}-gatherer-percentage > 0
    #when
      build {type} mining camps
    #then
      sn-camp-max-distance += 8
    #end when
  #end if
#end template

insert mining-camps(type="gold")
#if goal has-rush-buildings 1
  insert mining-camps(type="stone")
#end if

// houses

#if current-age == dark-age
  build houses with 5 headroom
#else
  build houses with 10 headroom
#end if

// farms

#if building-type-count lumber-camp >= 1
  build mills
#end if
#if up-compare-goal non-escrowed-wood c:>= 200 or building-type-count-total farm < 6 and up-research-status c: feudal-age >= research-pending
  build farms
#end if

// castles

build castle

// unit decisions

#do once
  goal produce-archers = 1
  goal produce-unique-units = 0
  goal produce-knights = 0
  goal produce-monks = 0
  goal produce-scouts = 0
  goal produce-spearmen = 0
  goal produce-battering-rams = 1
#end do

#if current-age == feudal-age
  #respond to min-detection-units skirmisher-line
    goal produce-scouts = 1
  #end respond
#else if current-age >= castle-age
  #do once
    goal produce-scouts = 1
  #end do
#end if

#if building-type-count castle >= 3 and goal produce-archers 1
  #do once
    goal produce-archers = 0
    goal produce-unique-units = 1
  #end do
#end if

#respond to min-detection-units war-elephant-line
  goal produce-archers = 0
  goal produce-monks = 1
#end respond

#respond to min-detection-units cataphract-line
  goal produce-archers = 0
  goal produce-monks = 1
#end respond

#respond to min-detection-units knight-line
  goal produce-scouts = 0
  goal produce-spearmen = 1
#end respond

#respond to min-detection-units skirmisher-line
  goal produce-spearmen = 0
  goal produce-scouts = 1
#end respond

#respond to min-detection-units battle-elephant-line
  goal produce-archers = 0
  goal produce-monks = 1
#end respond

// research

research ri-loom with gold escrow
research feudal-age with food escrow

#if current-age >= feudal-age and goal has-rush-buildings 1
  research ri-wheel-barrow with wood and food escrow
  research ri-hand-cart with wood and food escrow
  research castle-age with food and gold escrow
  research imperial-age with food and gold escrow

  build 1 lumber-camp
  research lumber camp upgrades with wood and food escrow

  build 1 mill
  research mill upgrades with wood and food escrow

  #if strategic-number sn-gold-gatherer-percentage > 0
    build 1 mining-camp
    research gold mining camp upgrades with wood and food escrow
  #end if

  build 1 castle
  research ri-conscription with food and gold escrow
  #if players-building-type-count any-ally market >= 1
    research ri-silk-road with food and gold escrow
  #end if

  #if goal produce-archers 1
    build 1 archery-range
    research ri-crossbow with food and gold escrow
    research ri-arbalest with food and gold escrow
  #end if

  #if goal produce-unique-units 1
    build 1 castle
    research my-unique-unit-upgrade with food and gold escrow
  #end if

  #if goal produce-archers 1 or goal produce-unique-units 1
    build 1 blacksmith
    research ranged blacksmith upgrades with food and gold escrow
    
    build 1 archery-range
    research ri-thumb-ring with wood and food escrow
    
    build 1 university
    research ri-ballistics with wood and gold escrow
    research ri-chemistry with food and gold escrow
    
    build 1 castle
    research ri-pavise with food and gold escrow
  #end if

  #if goal produce-scouts 1
    build 1 stable
    research ri-light-cavalry with food and gold escrow
    research ri-hussar with food and gold escrow
  #end if

  #if goal produce-knights 1
    build 1 stable
    research ri-cavalier with food and gold escrow
    research ri-paladin with food and gold escrow
  #end if

  #if goal produce-scouts 1 or goal produce-knights 1
    build 1 blacksmith
    research cavalry blacksmith upgrades with food and gold escrow
    
    build 1 stable
    research ri-bloodlines with food and gold escrow
    research ri-husbandry with food escrow
  #end if

  #if goal produce-spearmen 1
    build 1 barracks
    research ri-pikeman with food and gold escrow
    research ri-halberdier with food and gold escrow
    research ri-squires with food escrow
    
    build 1 blacksmith
    research infantry blacksmith upgrades with food and gold escrow
  #end if

  #if goal produce-monks 1
    research ri-sanctity with gold escrow
    research ri-fervor with gold escrow
    #respond to 1 scorpion-line
      research ri-redemption with gold escrow
    #end respond
    #respond to 1 mangonel-line
      research ri-redemption with gold escrow
    #end respond
    #respond to 1 bombard-cannon
      research ri-redemption with gold escrow
    #end respond
    #respond to 5 monk-set
      research ri-atonement with gold escrow
    #end respond
    #stages
      research ri-theocracy with gold escrow
    #advance when up-research-status c: ri-theocracy >= research-pending
      research ri-illumination with gold escrow
    #advance when up-research-status c: ri-illumination >= research-pending
      research ri-block-printing with gold escrow
    #end stages
  #end if

  #if goal produce-battering-rams 1
    build 1 siege-workshop
    research ri-capped-ram with food escrow
    research ri-siege-ram with food escrow
  #end if
#end if

// villagers

build 3 town-center

#if current-age == imperial-age
  train 120 villager with 30 trade
#else
  train 120 villager
#end if

// relics

build 1 monastery
train 1 monk

// economy

lure boars

#if up-research-status c: feudal-age < research-pending
  #do once
    distribute villagers 20 80 0 0
  #end do
#else
  #do once
    distribute villagers 80 20 0 0
  #end do
  #if current-age == feudal-age
    #do once
      distribute villagers 45 35 15 5
    #end do
  #end if
  auto balance wood and food and gold
  trade excess wood and food and gold at 2000
  #if town-under-attack
    build 1 market
    trade excess wood and food and gold at 300
  #end if
#end if

// military buildings

#template build-military-building
  #if current-age == feudal-age
    build 3 {building}
  #else if current-age == castle-age
    build 4 {building}
  #else if current-age == imperial-age
    build 9 {building}
  #end if
#end template

#if up-research-status c: feudal-age >= research-pending
  build 1 barracks
#end if

#if goal produce-archers 1
  insert build-military-building(building="archery-range")
#end if

#if goal produce-scouts 1 or goal produce-knights 1
  insert build-military-building(building="stable")
#end if

#if goal produce-spearmen 1
  insert build-military-building(building="barracks")
#end if

#if goal produce-unique-units 1
  insert build-military-building(building="castle")
#end if

#if goal produce-monks 1
  insert build-military-building(building="monastery")
#end if

#if goal produce-battering-rams 1
  insert build-military-building(building="siege-workshop")
#end if

// military production

#do once
  goal total-type-production = 0
  goal expected-units-per-type = 0
  goal current-unit-amount = 0
#end do

#template add-to-total-type-production
  #if unit-available {unit}
    @up-modify-goal total-type-production g:+ {goal}
  #end if
#end template

#template train-unit
  @up-get-fact unit-type-count {unit} current-unit-amount
  #if goal produce-units 1 and goal {goal} 1 and up-compare-goal current-unit-amount g:<= expected-units-per-type
    train {unit}
  #end if
#end template

goal total-type-production = 0
insert add-to-total-type-production(unit="archer-line",goal="produce-archers")
insert add-to-total-type-production(unit="scout-cavalry-line",goal="produce-scouts")
insert add-to-total-type-production(unit="knight-line",goal="produce-knights")
insert add-to-total-type-production(unit="spearman-line",goal="produce-spearmen")
insert add-to-total-type-production(unit="my-unique-unit-line",goal="produce-unique-units")
insert add-to-total-type-production(unit="monk",goal="produce-monks")
#if unit-type-count-total battering-ram-line >= max-rams
  insert add-to-total-type-production(unit="battering-ram-line",goal="produce-battering-rams")
#end if

@up-get-fact military-population 0 expected-units-per-type
@up-modify-goal expected-units-per-type g:/ total-type-production

#if up-compare-goal non-escrowed-wood c:>= 200 or military-population < 10
  insert train-unit(unit="archer-line",goal="produce-archers")
  #if unit-type-count-total battering-ram-line >= max-rams
    insert train-unit(unit="battering-ram-line",goal="produce-battering-rams")
  #end if
  insert train-unit(unit="spearman-line",goal="produce-spearmen")
  insert train-unit(unit="my-unique-unit-line",goal="produce-unique-units")
#end if
insert train-unit(unit="scout-cavalry-line",goal="produce-scouts")
insert train-unit(unit="knight-line",goal="produce-knights")
insert train-unit(unit="monk",goal="produce-monks")

// attacking

#do once
  target closest enemy
#end do
#if not player-in-game target-player or not stance-toward target-player enemy
  target closest enemy
#end if

#if up-compare-goal target-military-percentage c:>= 110 and military-population >= 10 or (up-compare-goal population-space-remaining c:<= 5 and current-age == imperial-age)
  sn-maximum-town-size = 255
#end if

#if up-compare-goal target-military-percentage c:< 100 and strategic-number sn-maximum-town-size == 255
  sn-maximum-town-size = 20
#end if

// wrap up

#if strategic-number sn-maximum-town-size != 255
  auto expand town size to 30
#end if
