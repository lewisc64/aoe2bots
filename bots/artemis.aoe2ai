const ri-pavise = 494
const ri-silk-road = 499

const wood-threshold = 500
const food-threshold = 500
const gold-threshold = 500

const max-villagers = 120
const max-rams = 8

#template production-goal-names
  {before}villager-production{after}
  {before}archer-production{after}
  {before}skirmisher-production{after}
  {before}battering-ram-production{after}
  {before}scout-production{after}
#end template

#do once
  set up basics
  
  assign 4 builders to town-center-foundation
  assign 8 builders to castle
  assign 2 builders to barracks
  assign 2 builders to archery-range
  assign 2 builders to stable
  assign 2 builders to siege-workshop
  
  goal temp = 0
  goal attacking = 0
  goal do-auto-balance = 0
#end do

do basic targeting
do basic diplomacy

load "charlotte/stats.aoe2ai"

escrow 0 wood
escrow 0 food
escrow 0 gold
escrow 0 stone

lure boars

#if current-age == dark-age
  build 2 house
  build houses with 5 headroom
#else
  build houses with 10 headroom
#end if

#if game-time > 60
  build lumber camps
#end if
#template build-mining-camps
  #if strategic-number sn-{resource}-gatherer-percentage > 0
    #when
      build {resource} mining camps
    #then
      sn-camp-max-distance += 5
    #end when
  #end if
#end template
insert build-mining-camps(resource="gold")
insert build-mining-camps(resource="stone")

#if up-research-status c: feudal-age >= research-pending
  build 1 mining-camp with wood escrow
#end if

#if building-type-count lumber-camp >= 1
  #if wood-amount >= 100 and current-age == dark-age or wood-amount >= 200
    build mills
    build farms
  #end if
#end if

build castle

// the plan

#do once
  insert production-goal-names(before="goal ",after=" = 0")
#end do

#stages
  distribute villagers 20 80 0 0
  goal villager-production = 1
  goal archer-production = 0
#advance when up-research-status c: feudal-age >= research-pending
  distribute villagers 50 20 30 0
  goal archer-production = 2
#advance when unit-type-count archer-line >= 12
  #if goal under-attack 1
    distribute villagers 50 20 30 0
    goal archer-production = 2
  #else
    distribute villagers 28 38 28 6
    goal archer-production = 0
  #end if
  sell wood when wood > 400
  buy food when gold > 400
  sell food when food > 900
#advance when up-research-status c: castle-age >= research-pending
  #do once
    distribute villagers 28 38 28 6
  #end do
  goal do-auto-balance = 1
  goal villager-production = 3
  goal archer-production = 2
  goal battering-ram-production = 1
#advance when civilian-population >= 120
  goal archer-production = 1
  goal battering-ram-production = 0
#advance when up-research-status c: imperial-age >= research-pending
#advance when up-research-status c: ri-arbalest >= research-pending
#advance when up-research-status c: ri-bracer >= research-pending
#advance when up-research-status c: ri-ring-archer-armor >= research-pending
#advance when up-research-status c: ri-chemistry >= research-pending
  goal archer-production = 8
  goal battering-ram-production = 1
#end stages

#if up-compare-goal target-military-percentage c:>= 110
  goal attacking = 1
#end if

#if up-compare-goal target-military-percentage c:< 100
  goal attacking = 0
#end if

// research

research ri-loom
research ri-double-bit-axe
research ri-bow-saw
research ri-two-man-saw

research ri-horse-collar
research ri-heavy-plow
research ri-crop-rotation

research ri-gold-mining
research ri-gold-shaft-mining
research feudal-age

research castle-age
build 1 university
build 1 siege-workshop
research imperial-age

// unit research

#template research-ranged-upgrades
  build 1 blacksmith
  research ranged blacksmith upgrades
  build 1 university
  research ri-ballistics
  research ri-chemistry
#end template

#template research-foot-archer-upgrades
  build 1 archery-range
  research ri-thumb-ring
  #if civ-selected italian
    build 1 castle
    research ri-pavise
  #end if
#end template

#template research-cavalry-upgrades
  build 1 blacksmith
  research cavalry blacksmith upgrades
  build 1 stable
  research ri-bloodlines
  research ri-husbandry
#end template

#if not goal archer-production 0
  build 1 archery-range
  research ri-crossbow
  research ri-arbalest
  insert research-ranged-upgrades
  insert research-foot-archer-upgrades
#end if

#if not goal skirmisher-production 0
  build 1 archery-range
  research ri-elite-skirmisher
  research ri-imperial-skirmisher
  insert research-ranged-upgrades
#end if

#if not goal battering-ram-production 0
  build 1 siege-workshop
  research ri-capped-ram
  research ri-siege-ram
#end if

#if not goal scout-production 0
  build 1 stable
  research ri-light-cavalry
  research ri-hussar
  insert research-cavalry-upgrades
#end if

// unit buildings

@up-get-fact building-type-count-total town-center temp
#if up-compare-goal temp g:< villager-production
  #if goal under-attack 0
    escrow 90 wood
    escrow 90 stone
  #end if
  build town-center with wood and stone escrow
#end if

@up-get-fact building-type-count-total archery-range temp
goal total-production = 0
@up-modify-goal total-production g:+ archer-production
@up-modify-goal total-production g:+ skirmisher-production
#if up-compare-goal temp g:< total-production
  #if goal under-attack 0
    escrow 90 wood
  #end if
  build 1 barracks with wood escrow
  build archery-range with wood escrow
#end if

@up-get-fact building-type-count-total stable temp
goal total-production = 0
@up-modify-goal total-production g:+ scout-production
#if up-compare-goal temp g:< total-production
  #if goal under-attack 0
    escrow 90 wood
  #end if
  build 1 barracks with wood escrow
  build stable with wood escrow
#end if

@up-get-fact building-type-count-total siege-workshop temp
goal total-production = 0
@up-modify-goal total-production g:+ battering-ram-production
#if up-compare-goal temp g:< total-production
  #if goal under-attack 0
    escrow 90 wood
  #end if
  build 1 blacksmith with wood escrow
  build siege-workshop with wood escrow
#end if

// unit production

#template train-unit-internal
  #if goal {production_goal} {number} and up-pending-objects c: {unit} < {number}
    train {unit}
  #end if
#end template

#template train-unit
  insert train-unit-internal(production_goal="{production_goal}",unit="{unit}",number=1)
  insert train-unit-internal(production_goal="{production_goal}",unit="{unit}",number=2)
  insert train-unit-internal(production_goal="{production_goal}",unit="{unit}",number=3)
  insert train-unit-internal(production_goal="{production_goal}",unit="{unit}",number=4)
  insert train-unit-internal(production_goal="{production_goal}",unit="{unit}",number=5)
  insert train-unit-internal(production_goal="{production_goal}",unit="{unit}",number=6)
  insert train-unit-internal(production_goal="{production_goal}",unit="{unit}",number=7)
  insert train-unit-internal(production_goal="{production_goal}",unit="{unit}",number=8)
#end template

#if unit-type-count-total villager < max-villagers
  insert train-unit(production_goal="villager-production",unit="villager")
#end if
#if unit-type-count-total battering-ram-line < max-rams
  insert train-unit(production_goal="battering-ram-production",unit="battering-ram-line")
#end if

#if goal battering-ram-production 0 or up-pending-objects c: battering-ram-line > 0 or unit-type-count-total battering-ram-line >= max-rams or (goal under-attack 1 and not goal threat-source castle)
  insert train-unit(production_goal="archer-production",unit="archer-line")
  insert train-unit(production_goal="skirmisher-production",unit="skirmisher-line")
  insert train-unit(production_goal="scout-production",unit="scout-cavalry-line")
#end if

build 1 market
#if goal under-attack 1
  trade excess wood and food and gold at 300
#end if
trade excess wood and food and gold at 2000

#if goal do-auto-balance 1
  auto balance wood and food and gold
#end if

// attacking and town size

#if goal attacking 0
  #if strategic-number sn-maximum-town-size > 30
    sn-maximum-town-size = 30
  #end if
  auto expand town size to 30
#else
  sn-maximum-town-size = 255
  trade excess wood and food and gold at 500
#end if
