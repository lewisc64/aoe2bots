const ri-pavise = 494
const ri-silk-road = 499

const wood-threshold = 500
const food-threshold = 500
const gold-threshold = 500

const max-villagers = 120
const max-rams = 8

#template production-goal-names
  {before}villager-production{after}
  {before}archer-production{after}
  {before}skirmisher-production{after}
  {before}battering-ram-production{after}
  {before}scout-production{after}
#end template

#do once
  set name to "Artemis"
  set up basics
  
  assign 4 builders to town-center-foundation
  assign 8 builders to castle
  assign 2 builders to barracks
  assign 2 builders to archery-range
  assign 2 builders to stable
  assign 2 builders to siege-workshop
  
  goal temp = 0
  goal attacking = 0
  goal do-auto-balance = 0
#end do

do basic targeting
do basic diplomacy

load "charlotte/stats.aoe2ai"

escrow 0 wood
escrow 0 food
escrow 0 gold
escrow 0 stone

#template escrow-resources
  #if escrow-amount wood < 300
    escrow {percentage} wood
  #else
    escrow 0 wood
  #end if
  #if escrow-amount food < 1300
    escrow {percentage} food
  #else
    escrow 0 food
  #end if
  #if escrow-amount gold < 800
    escrow {percentage} gold
  #else
    escrow 0 gold
  #end if
#end template

#if unit-type-count-total archer-line < 10 or up-research-status c: ri-arbalest >= research-pending and up-research-status c: ri-ring-archer-armor >= research-pending and up-research-status c: ri-bracer >= research-pending
  insert escrow-resources(percentage=0)
#else if unit-type-count-total archer-line < 20
  insert escrow-resources(percentage=10)
#else if unit-type-count-total archer-line < 30
  insert escrow-resources(percentage=20)
#else
  insert escrow-resources(percentage=40)
#end if

lure boars

#if current-age == dark-age
  build 2 house
  build houses with 5 headroom
#else
  build houses with 10 headroom
#end if

#if game-time > 60
  build lumber camps
#end if
#template build-mining-camps
  #if strategic-number sn-{resource}-gatherer-percentage > 0
    #when
      build {resource} mining camps
    #then
      sn-camp-max-distance += 5
    #end when
  #end if
#end template
insert build-mining-camps(resource="gold")
insert build-mining-camps(resource="stone")

#if up-research-status c: feudal-age >= research-pending
  build 1 mining-camp with wood escrow
#end if

#if building-type-count lumber-camp >= 1
  #if wood-amount >= 100 and current-age == dark-age or wood-amount >= 200
    build mills
    build farms
  #end if
#end if

build castle

// the plan

#do once
  insert production-goal-names(before="goal ",after=" = 0")
#end do

#if up-research-status c: feudal-age < research-pending
  distribute villagers 20 80 0 0
  goal villager-production = 1
  goal archer-production = 0
#else if up-research-status c: castle-age < research-pending
  #do once
    distribute villagers 40 40 20 0
    scout opposite
  #end do
  goal do-auto-balance = 1
  goal villager-production = 1
  goal archer-production = 2
#else if up-research-status c: imperial-age < research-pending
  goal do-auto-balance = 1
  goal villager-production = 4
  goal battering-ram-production = 1
  goal archer-production = 4
#else
  #do once
    goal do-auto-balance = 1
    goal archer-production = 8
    goal battering-ram-production = 3
    goal villager-production = 4
  #end do
  #repeat every 5 minutes
    #if wood-amount > 1000 and food-amount > 1000 and gold-amount > 1000
      goal archer-production += 1
    #end if
  #end repeat
#end if

#if up-compare-goal target-military-percentage c:>= 110
  goal attacking = 1
#else if up-compare-goal target-military-percentage c:< 100
  goal attacking = 0
#end if

// research

research ri-loom with gold escrow
research ri-double-bit-axe with wood and food escrow
research ri-bow-saw with wood and food escrow
research ri-two-man-saw with wood and food escrow

research ri-horse-collar with wood and food escrow
research ri-heavy-plow with wood and food escrow
research ri-crop-rotation with wood and food escrow

research ri-gold-mining with wood and food escrow
research ri-gold-shaft-mining with wood and food escrow
research feudal-age with food escrow

research castle-age with food and gold escrow
build 1 university
build 1 siege-workshop
research imperial-age with food and gold escrow

// unit research

#template research-ranged-upgrades
  build 1 blacksmith
  research ranged blacksmith upgrades with food and gold escrow
  build 1 university
  research ri-ballistics with wood and gold escrow
  research ri-chemistry with food and gold escrow
#end template

#template research-foot-archer-upgrades
  build 1 archery-range
  research ri-thumb-ring
  #if civ-selected italian
    build 1 castle
    research ri-pavise with food and gold escrow
  #end if
#end template

#template research-cavalry-upgrades
  build 1 blacksmith
  research cavalry blacksmith upgrades with food and gold escrow
  build 1 stable
  research ri-bloodlines with food and gold escrow
  research ri-husbandry with food escrow
#end template

#if not goal archer-production 0
  build 1 archery-range
  research ri-crossbow with food and gold escrow
  research ri-arbalest with food and gold escrow
  insert research-ranged-upgrades
  insert research-foot-archer-upgrades
#end if

#if not goal skirmisher-production 0
  build 1 archery-range
  research ri-elite-skirmisher with wood and gold escrow
  research ri-imperial-skirmisher with wood and gold escrow
  insert research-ranged-upgrades
#end if

#if not goal battering-ram-production 0
  build 1 siege-workshop
  research ri-capped-ram with food escrow
  research ri-siege-ram with food escrow
#end if

#if not goal scout-production 0
  build 1 stable
  research ri-light-cavalry with food and gold escrow
  research ri-hussar with food and gold escrow
  insert research-cavalry-upgrades
#end if

// unit buildings

@up-get-fact building-type-count-total town-center temp
#if up-compare-goal temp g:< villager-production
  #if goal under-attack 0
    escrow 90 wood
    escrow 90 stone
  #end if
  buy stone when stone < 100
  build town-center with wood and stone escrow
#end if

@up-get-fact building-type-count-total archery-range temp
goal total-production = 0
@up-modify-goal total-production g:+ archer-production
@up-modify-goal total-production g:+ skirmisher-production
#if up-compare-goal temp g:< total-production
  #if goal under-attack 0
    escrow 90 wood
  #end if
  build 1 barracks with wood escrow
  build archery-range with wood escrow
#end if

@up-get-fact building-type-count-total stable temp
goal total-production = 0
@up-modify-goal total-production g:+ scout-production
#if up-compare-goal temp g:< total-production
  #if goal under-attack 0
    escrow 90 wood
  #end if
  build 1 barracks with wood escrow
  build stable with wood escrow
#end if

@up-get-fact building-type-count-total siege-workshop temp
goal total-production = 0
@up-modify-goal total-production g:+ battering-ram-production
#if up-compare-goal temp g:< total-production
  #if goal under-attack 0
    escrow 90 wood
  #end if
  build 1 blacksmith with wood escrow
  build siege-workshop with wood escrow
#end if

// unit production

#template train-unit-internal
  #if goal {production_goal} {number} and up-pending-objects c: {unit} < {number}
    train {unit}
  #end if
#end template

#template train-unit
  insert train-unit-internal(production_goal="{production_goal}",unit="{unit}",number=1)
  insert train-unit-internal(production_goal="{production_goal}",unit="{unit}",number=2)
  insert train-unit-internal(production_goal="{production_goal}",unit="{unit}",number=3)
  insert train-unit-internal(production_goal="{production_goal}",unit="{unit}",number=4)
  insert train-unit-internal(production_goal="{production_goal}",unit="{unit}",number=5)
  insert train-unit-internal(production_goal="{production_goal}",unit="{unit}",number=6)
  insert train-unit-internal(production_goal="{production_goal}",unit="{unit}",number=7)
  insert train-unit-internal(production_goal="{production_goal}",unit="{unit}",number=8)
#end template

#if unit-type-count-total villager < max-villagers
  insert train-unit(production_goal="villager-production",unit="villager")
#end if
#if unit-type-count-total battering-ram-line < max-rams
  insert train-unit(production_goal="battering-ram-production",unit="battering-ram-line")
#end if

#if building-type-count siege-workshop == 0 or goal battering-ram-production 0 or up-pending-objects c: battering-ram-line > 0 or unit-type-count-total battering-ram-line >= max-rams or (goal under-attack 1 and not goal threat-source castle)
  insert train-unit(production_goal="archer-production",unit="archer-line")
  insert train-unit(production_goal="skirmisher-production",unit="skirmisher-line")
  insert train-unit(production_goal="scout-production",unit="scout-cavalry-line")
#end if

build 1 market
#if goal under-attack 1
  trade excess wood and food and gold at 300
#end if
trade excess wood and food and gold at 2000

#if goal do-auto-balance 1
  auto balance wood and food and gold
#end if

// attacking and town size

#if goal attacking 0
  #if strategic-number sn-maximum-town-size > 30
    sn-maximum-town-size = 30
  #end if
  auto expand town size to 30
#else
  sn-maximum-town-size = 255
  trade excess wood and food and gold at 500
#end if
