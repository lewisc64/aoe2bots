// plays as the byzantines

const ri-logistica = 61

load "charlotte/stats.aoe2ai"
load "templates/fast_castle.aoe2ai"

#select random persistant
  do basic diplomacy
#randor
  do basic diplomacy without backstabbing
#end select

#if current-age == feudal-age
  build stone mining camps
#end if

#if up-research-status c: castle-age >= research-pending
  #do once
    distribute villagers 40 40 10 10
    assign 8 builders to castle
    assign 4 builders to town-center-foundation
    
    sn-percent-building-cancellation = 0
    goal make-trash = 0
  #end do
  
  #if dropsite-min-distance stone == -1
    #do once
      chat to self "out of stone."
      distribute 10 villagers from stone to wood and food and gold
    #end do
    
    #if building-type-count-total castle < 3
      buy stone
    #end if
  #end if
  
  auto balance wood and food and gold
  trade excess wood and food and gold and stone at 2000
  auto expand town size to 30
  
  #if escrow-amount food < 1600 and not research-completed ri-logistica
    escrow 20 food
  #else
    escrow 0 food
  #end if
  
  #if escrow-amount gold < 800 and not research-completed ri-logistica
    escrow 20 gold
  #else
    escrow 0 gold
  #end if
  
  #if players-building-type-count any-ally market == 0 and up-compare-goal no-gold-mining-time c:> 60
    goal make-trash = 1
  #end if

  build lumber camps
  build houses with 15 headroom
  #when
    build gold mining camps
    build stone mining camps
  #then
    sn-camp-max-distance += 8
  #end when
  
  #if up-compare-goal non-escrowed-wood >= 100
    build farms
  #end if
  
  build castle
  #if building-type-count-total castle >= 1
    build 6 town-center
  #end if
  build 1 barracks
  build 1 stable
  build 1 castle
  build 1 monastery
  
  #if current-age >= imperial-age
    research ri-hand-cart with wood and food escrow
    research ri-murder-holes with food and stone escrow
    research ri-conscription with food and gold escrow
  #end if
  
  research mill upgrades with wood and food escrow
  research lumber camp upgrades with wood and food escrow
  research mining camp upgrades with wood and food escrow
  
  research cavalry blacksmith upgrades with food and gold escrow
  research ri-bloodlines with food and gold escrow
  research ri-husbandry with food escrow
  
  research imperial-age with food and gold escrow
  research my-unique-unit-upgrade with food and gold escrow => research ri-logistica with food and gold escrow
  
  #respond to 5 monk
    research ri-heresy with gold escrow
  #end respond
  
  #if current-age >= imperial-age
    train 130 villager with 35 trade
  #else
    train 130 villager
  #end if
  
  train 1 monk
  
  #if current-age == castle-age
    train 15 my-unique-unit
  #else
    #if unit-type-count-total trebuchet-set < 2 and (not town-under-attack or goal threat-source castle)
      train trebuchet
    #else
      #if not town-under-attack
        train 5 trebuchet
      #end if
      train my-unique-unit
      #if goal make-trash 1
        build 6 stable
        research ri-light-cavalry with food and gold escrow
        research ri-hussar with food and gold escrow
        train scout-cavalry-line
      #end if
    #end if
  #end if
  
  #do once
    target closest enemy
  #end do
  #if not player-in-game target-player
    target closest enemy
  #end if
  
  #if military-population >= 30
    sn-maximum-town-size = 255
  #else
    @up-modify-sn sn-maximum-town-size s:= sn-minimum-town-size
  #end if
#end if
