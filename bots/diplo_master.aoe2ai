const ri-treadmill-crane = 54
const ri-herbal-medicine = 441
const town-size = 30

#if current-age >= castle-age
  #if escrow-amount food < 1000 and current-age <= castle-age
    escrow 10 food
  #else
    escrow 0 food
  #end if

  #if escrow-amount gold < 1000
    escrow 10 gold
  #else
    escrow 0 gold
  #end if
#end if

#do once
  set up scouting
  set up new building system
  set up micro
  chat to all "21I look forward to you joining my side."
#end do once

build 1 town-center
build houses

#if building-available monastery and building-type-count-total monastery < 3
  escrow 50 wood
  build 3 monastery with wood escrow
#else
  escrow 0 wood
#end if
#if wood-amount > 300
  build monastery
#end if
build lumber camps
#if building-type-count-total town-center >= 1
  build mills
  build 1 blacksmith
  build 1 market
  build 1 university
  build stone walls with perimeter 2
  build stone gates on perimeter 2
  build bombard-tower
  build watch-tower-line
#end if

#if building-type-count-total mill == 0 and game-time >= 360
  build 1 mill
#end if

research feudal-age
research castle-age
research imperial-age with food and gold escrow

train 2 monk

research ri-loom with gold escrow
research ri-fervor with gold escrow
#respond to 2 monk-set
  research ri-atonement with gold escrow
#end respond
#if gold-amount >= 1000
  research ri-atonement with gold escrow
  research ri-faith with gold escrow
  research ri-herbal-medicine with gold escrow
  research ri-heresy with gold escrow
#end if
research ri-redemption with gold escrow
research ri-sanctity with gold escrow
research ri-illumination with gold escrow
research ri-block-printing with gold escrow
research ri-theocracy with gold escrow
#respond to 20 monk-set
  #do once
    chat to all "Shall it be a war of words, then? So be it."
  #end do once
  research ri-heresy with gold escrow
  research ri-faith with gold escrow
#end respond

research ri-masonry
research ri-fortified-wall
research ri-architecture
research ri-treadmill-crane
research ri-murder-holes
research ri-arrowslits
research ri-chemistry
research ri-ballistics

research ri-fletching
research ri-bodkin-arrow
research ri-bracer

research ri-guard-tower
research ri-keep
research ri-bombard-tower

train monk
#when
  #if military-population >= 50 and not(enemy-buildings-in-town)
    sn-maximum-town-size += 5
    #repeat every 10 minutes
      @up-reset-unit c: monk
    #end repeat
  #else if strategic-number sn-maximum-town-size > town-size
    sn-maximum-town-size -= 1
  #end if
#then
  #do once
    chat to all "I am sending out diplomats to convice you that our cause is just."
    #delay by 10 seconds
      chat to all "You will see sense."
    #end delay
  #end do once
#end when

#if current-age == dark-age
  #do once
    distribute villagers 20 80 0 0
  #end do once
  train 27 villager
  build 3 farm
  
#end if

#if current-age >= feudal-age

  build farms
  #add action up-modify-sn sn-camp-max-distance c:+ 5
    build gold mining camps
    build stone mining camps
  #remove action

#end if

#if current-age == feudal-age
  
  #do once
    distribute villagers 50 20 30 0
  #end do once
  train 29 villager
  sell wood when wood >= 200
  buy food when gold >= 300
  sell food when food >= 900

#end if

#if current-age >= castle-age
  #do once
    distribute villagers 30 30 30 10
    enable walls with perimeter 2
    sn-maximum-town-size = town-size
  #end do once
  
  auto balance food and gold
  
  #if current-age >= imperial-age
    train 130 villager with 30 trade
  #else
    train 130 villager
  #end if
  build houses with 15 headroom
  //sell stone when stone >= 750

#end if

#if unit-type-count-total villager >= 130
  delete unit villager
#end if

#if current-age == castle-age
  
  buy food when gold >= 900
  sell food when food >= 1300

#end if