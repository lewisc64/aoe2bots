// plays as the bulgarians

#do once
  set name to "Exiled Knight Harriet"
  set up basics
  assign 4 builders to town-center-foundation
  assign 8 builders to castle
  assign 8 builders to krepost
  assign 4 builders to stone-wall-line
  #if not civ-selected bulgarians
    chat to all "I need to play as the Bulgarians."
  #end if
#end do

#template diplo-chat-alliance-offer
  @up-get-player-color {player} -1
  #select random
    chat const to all "We should join forces %s." 7031232
  #randor
    chat const to all "Don't be afraid %s, you can trust me." 7031232
  #randor
    chat const to all "%s, let's ally." 7031232
  #randor
    chat const to all "%s, think of all we could do together." 7031232
  #end select
#end template

#template diplo-chat-our-betrayal
  @up-get-player-color {player} -1
  #select random
    chat const to all "I'm sorry %s." 7031232
  #randor
    chat const to all "Prepare yourself, %s." 7031232
  #randor
    chat const to all "Forgive me for this %s." 7031232
  #randor
    chat const to all "Unfortunately %s, times change." 7031232
  #end select
#end template

#template diplo-chat-their-betrayal
  @up-get-player-color {player} -1
  #select random
    chat const to all "Oh, %s..." 7031232
  #randor
    chat const to all "%s? This was not part of the plan." 7031232
  #randor
    chat const to all "I suppose it's understandable %s." 7031232
  #end select
#end template

#template diplo-chat-accepted-our-alliance
  @up-get-player-color {player} -1
  chat const to all "Thank you %s." 7031232
#end template

#template diplo-chat-accepted-their-alliance
  @up-get-player-color {player} -1
  chat const to all "Agreed %s." 7031232
#end template

#template diplo-chat-refused-our-alliance
  @up-get-player-color {player} -1
  #select random
    chat const to all "You will see one day, %s." 7031232
  #randor
    chat const to all "Perhaps next time." 7031232
  #end select
#end template

#template diplo-chat-refused-their-alliance
  @up-get-player-color {player} -1
  chat const to all "I can't do that %s." 7031232
#end template

#template diplo-chat-friendly-fire-warning
  @up-get-player-color {player} -1
  chat const to all "%s? Was that a mistake?" 7031232
#end template

#template diplo-chat-friendly-fire-betrayal
  chat to all "Oh souls, you mean to do me harm."
#end template

#template diplo-chat-resign
  chat to all "Until we meet again."
#end template

#template diplo-should-player-be-marked
  #if military-population >= 30
    goal {marked_goal} = 1
  #else if military-population < 10
    goal {marked_goal} = 0
  #end if
#end template

load "templates/diplomacy_engine.aoe2ai"

load "templates/fast_castle.aoe2ai"

#if up-research-status c: castle-age >= research-pending
  #do once
    distribute villagers 20 45 25 10
  #end do
  auto balance wood and food and gold
  trade excess wood and food and gold at 2000

  #if strategic-number sn-wood-gatherer-percentage < 10
    distribute 2 villagers from food and gold to wood
  #end if
  #if strategic-number sn-food-gatherer-percentage < 30
    distribute 2 villagers from wood and gold to food
  #end if
  #if strategic-number sn-gold-gatherer-percentage < 10
    distribute 2 villagers from wood and food to gold
  #end if

  #template get-non-escrowed-amount-goal
    goal non-escrowed-{resource}-amount = 0
    goal escrowed-{resource} = 0
    @up-get-fact escrow-amount {resource} escrowed-{resource}
    @up-get-fact {resource}-amount 0 non-escrowed-{resource}-amount
    @up-modify-goal non-escrowed-{resource}-amount g:- escrowed-{resource}
  #end template

  insert get-non-escrowed-amount-goal(resource="wood")
  insert get-non-escrowed-amount-goal(resource="food")
  insert get-non-escrowed-amount-goal(resource="gold")
  insert get-non-escrowed-amount-goal(resource="stone")

  #if up-enemy-units-in-town > 10 and town-under-attack
    trade excess wood and food and gold at 300
  #end if

  #if up-research-status c: ri-elite-konnik >= research-pending
    escrow 0 food with maximum 0
    escrow 0 gold with maximum 0
  #else if military-population < 10 or up-enemy-units-in-town >= 10 and town-under-attack
    escrow 0 food with maximum 1000
    escrow 0 gold with maximum 1000
  #else if military-population < 20
    escrow 10 food with maximum 1000
    escrow 10 gold with maximum 1000
  #else if military-population < 30
    escrow 20 food with maximum 1000
    escrow 20 gold with maximum 1000
  #else
    escrow 40 food with maximum 1000
    escrow 40 gold with maximum 1000
  #end if
  
  build 1 town-center
  build lumber camps
  #when
    build gold mining camps
    build stone mining camps
  #then
    sn-camp-max-distance += 8
  #end when
  #if up-compare-goal non-escrowed-wood-amount c:>= 100
    build farms
    build mills
  #end if
  build houses with 20 headroom
  build 2 barracks
  build 1 stable
  build 1 blacksmith
  build 1 monastery
  train 1 monk
  #if (building-type-count-total krepost < 3 or building-type-count-total castle < 1) and up-compare-goal non-escrowed-stone-amount c:< 650 and up-compare-goal non-escrowed-gold-amount >= 1000
    build 1 market
    buy stone
  #end if

  build 1 krepost
  #if building-type-count krepost >= 1
    build 3 town-center
    #if building-type-count-total town-center >= 3
      build 3 siege-workshop
      build 1 castle
      #if building-type-count-total castle >= 1
        build krepost
      #end if
    #end if
  #end if

  #respond to 10 monk-set
    research ri-heresy with escrow
    research ri-devotion with escrow
    research ri-faith with escrow
  #end respond

  #respond to 10 missionary
    research ri-heresy with escrow
    research ri-devotion with escrow
    research ri-faith with escrow
  #end respond
  
  research lumber camp upgrades with escrow
  research mill upgrades with escrow
  research gold mining camp upgrades with escrow
  research ri-wheel-barrow with escrow
  research ri-bloodlines with escrow
  research ri-husbandry with escrow
  #if current-age == imperial-age
    research ri-hand-cart with escrow
  #end if
  research cavalry blacksmith upgrades with escrow
  #if up-research-status c: ri-scale-barding >= research-pending
    research ri-scale-mail with escrow
  #end if
  #if up-research-status c: ri-chain-barding >= research-pending
    research ri-chain-mail with escrow
  #end if
  #if up-research-status c: ri-plate-barding >= research-pending
    research ri-plate-mail with escrow
  #end if
  research imperial-age with escrow
  research ri-stirrups with escrow
  research my-unique-unit-upgrade with escrow
  research ri-capped-ram with escrow
  research ri-siege-ram with escrow
  research ri-siege-engineers with escrow
  research ri-light-cavalry with escrow
  research ri-hussar with escrow
  research ri-pikeman with escrow
  research ri-halberdier with escrow
  
  #if current-age >= imperial-age
    research ri-conscription
    build 6 siege-workshop
    build 3 barracks
    build 3 stable
    build 12 barracks
    build 12 stable
  #end if

  #if up-compare-goal non-escrowed-gold-amount c:>= 70 and (building-type-count krepost >= 1 or building-type-count castle >= 1)
    #if town-under-attack and up-enemy-units-in-town > 0
      #if building-type-count krepost == 0
        train balanced 15 konnik-line 4 spearman-line
      #else
        train balanced 15 krepost-konnik-line 4 spearman-line
      #end if
    #else
      train 3 trebuchet
      #if building-type-count krepost == 0
        train balanced 15 konnik-line 4 spearman-line 1 battering-ram-line 1 trebuchet
      #else
        train balanced 15 krepost-konnik-line 4 spearman-line 1 battering-ram-line 1 trebuchet
      #end if
    #end if
  #else
    train balanced 1 scout-cavalry-line 1 spearman-line
  #end if

  train mercenary-kipchak
  
  #if current-age == imperial-age
    train 120 villager with 30 trade
  #else
    train 120 villager with escrow
  #end if
  
  goal my-military-population = 0
  goal military-required = 0
  @up-get-fact military-population 0 my-military-population
  @up-get-player-fact target-player military-population 0 military-required
  @up-modify-goal military-required c:* 15
  @up-modify-goal military-required c:/ 10

  do basic targeting

  #if military-population >= 20 and up-compare-goal my-military-population g:> military-required or population >= 195 and player-in-game any-enemy
    attack with duc
  #end if
  
  auto expand town size
#end if
