// plays as the Aztecs

const ri-garland-wars = 24
const ri-atlatl = 460

const max-rams = 10
const max-skirmishers = 20

#template diplo-chat-alliance-offer
  @up-get-player-color {player} -1
  chat const to all "YOU HELP ME %s." 7031232
#end template

#template diplo-chat-our-betrayal
  @up-get-player-color {player} -1
  #select random
    chat const to all "%s IS WEAK." 7031232
  #randor
    chat const to all "%s HAS NO RECOURSE" 7031232
  #randor
    chat const to all "%s WILL BE EXPUNGED." 7031232
  #end select
#end template

#template diplo-chat-their-betrayal
  @up-get-player-color {player} -1
  #select random
    chat const to all "I HAVE FACED WORSE." 7031232
  #randor
    chat const to all "ANOTHER CHALLENGER." 7031232
  #randor
    chat const to all "YOU HAD FANGS THIS WHOLE TIME %s. CUTE." 7031232
  #end select
#end template

#template diplo-chat-accepted-our-alliance
  @up-get-player-color {player} -1
  chat const to all "YES. GOOD." 7031232
#end template

#template diplo-chat-accepted-their-alliance
  @up-get-player-color {player} -1
  chat const to all "I WILL HUNT WITH YOU." 7031232
#end template

#template diplo-chat-refused-our-alliance
  @up-get-player-color {player} -1
  chat const to all "NO MATTER. LONE WOLVES SUFFER IN THE END." 7031232
#end template

#template diplo-chat-refused-their-alliance
  @up-get-player-color {player} -1
  chat const to all "I WILL NOT HUNT WITH YOU." 7031232
#end template

#template diplo-chat-friendly-fire-warning
  @up-get-player-color {player} -1
  chat const to all "THE PREY TWITCHES. IT IS NOT TIME %s." 7031232
#end template

#template diplo-chat-friendly-fire-betrayal
  @up-get-player-color {player} -1
  chat const to all "YOU MADE YOURSELF A PRIORITY." 7031232
#end template

#template diplo-chat-resign
  chat to all "I WILL BE BACK."
#end template

#template diplo-should-player-be-marked
  #if military-population >= 20
    goal {marked_goal} = 1
  #else
    goal {marked_goal} = 0
  #end if
#end template

#do once
  set name to "Apex Predator"
  chat to all "I SMELL PREY."
#end do

load "templates/diplomacy_engine.aoe2ai"

load "charlotte/stats.aoe2ai"
load "templates/fast_castle.aoe2ai"

#if up-research-status c: castle-age >= research-pending
  #do once
    distribute villagers 35 40 15 10
    
    assign 8 builders to castle
    assign 8 builders to town-center-foundation
  #end do

  manage scouting
  do basic targeting
  
  #if building-type-count-total castle == 0 and strategic-number sn-stone-gatherer-percentage == 10
    #do once
      distribute 18 villagers from wood and food and gold to stone
    #end do
  #end if
  
  #if building-type-count-total castle >= 1 and strategic-number sn-stone-gatherer-percentage > 10
    #do once
      distribute 18 villagers from stone to wood and food and gold
    #end do
  #end if
  
  auto balance wood and food and gold

  #if stone-amount < 650
    buy stone when gold > 2000
  #end if
  trade excess wood and food and gold at 2000
  
  #template escrow-resources
    escrow {percentage} wood with maximum 275
    escrow {percentage} food with maximum 1000
    escrow {percentage} gold with maximum 500
  #end template
  
  #if military-population < 10 or up-research-status c: my-unique-unit-upgrade >= research-pending
    insert escrow-resources(percentage=0)
    #if up-research-status c: my-unique-unit-upgrade >= research-pending
      @up-release-escrow
    #end if
  #else if military-population < 20
    insert escrow-resources(percentage=10)
  #else if military-population < 30
    insert escrow-resources(percentage=20)
  #else
    insert escrow-resources(percentage=40)
  #end if
  
  build lumber camps
  #when
    build stone mining camps
    build gold mining camps
  #then
    sn-camp-max-distance += 8
  #end when
  
  build houses with 20 headroom
  #if up-compare-goal non-escrowed-wood c:>= 200
    build farms
  #end if
  
  build castle
  build 1 town-center with escrow
  #if building-type-count-total castle >= 1
    build 3 town-center with escrow
  #end if
  build 1 barracks
  build 1 blacksmith
  build 1 monastery
  build 1 siege-workshop
  build 1 university
  build 3 archery-range
  
  research ri-wheel-barrow with escrow
  research ri-hand-cart with escrow
  
  research lumber camp upgrades with escrow
  research gold mining camp upgrades with escrow
  research mill upgrades with escrow
  
  research ri-arson with escrow
  research ri-squires with escrow
  research infantry blacksmith upgrades with escrow
  research ri-conscription with escrow
  research my-unique-unit-upgrade with escrow
  research ri-garland-wars with escrow
  
  research ri-elite-skirmisher with escrow
  research ri-imperial-skirmisher with escrow
  research ranged blacksmith upgrades with escrow
  research ri-atlatl with escrow
  research ri-ballistics with escrow
  research ri-chemistry with escrow
  
  research ri-capped-ram with escrow
  research ri-siege-ram with escrow
  
  research ri-murder-holes with escrow
  
  research imperial-age with escrow
  
  #if current-age == imperial-age
    train 120 villager with 30 trade
  #else
    train 120 villager
  #end if
  train 1 monk
  
  #if goal under-attack 1 or up-compare-goal non-escrowed-food c:>= 110
    #if building-type-count siege-workshop >= 1 and unit-type-count-total battering-ram-line < max-rams and up-pending-objects c: battering-ram-line == 0 and military-population >= 20
      train battering-ram-line
    #else
      #if building-type-count castle >= 1
        #if up-pending-objects c: skirmisher-line >= 3
          train my-unique-unit-line
        #else
          train balanced 1 my-unique-unit-line 1 skirmisher-line
        #end if
      #else
        train skirmisher-line
      #end if
    #end if
  #end if
  
  #if up-compare-goal target-military-percentage c:>= 110 and military-population >= 20
    goal attacking = 1
  #else if up-compare-goal target-military-percentage c:< 100
    goal attacking = 0
  #end if
  
  #if goal attacking 1
    attack with duc
    #do once
      chat to all "THE HUNT BEGINS."
    #end do
  #end if
  
  #if goal attacking 1 and up-compare-goal target-military-percentage c:>= 200 and military-population >= 30
    #do once
      chat to all "YOU PEEL LIKE A RABBIT."
    #end do
  #end if

  auto expand town size

#end if